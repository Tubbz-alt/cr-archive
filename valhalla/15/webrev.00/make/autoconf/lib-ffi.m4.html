<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <title>New make/autoconf/lib-ffi.m4</title>
    <link rel="stylesheet" href="../../style.css" />
  </head>
  <body>
    <pre>
  1 #
  2 # Copyright (c) 2015, 2020, Oracle and/or its affiliates. All rights reserved.
  3 # DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.
  4 #
  5 # This code is free software; you can redistribute it and/or modify it
  6 # under the terms of the GNU General Public License version 2 only, as
  7 # published by the Free Software Foundation.  Oracle designates this
  8 # particular file as subject to the &quot;Classpath&quot; exception as provided
  9 # by Oracle in the LICENSE file that accompanied this code.
 10 #
 11 # This code is distributed in the hope that it will be useful, but WITHOUT
 12 # ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 13 # FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
 14 # version 2 for more details (a copy is included in the LICENSE file that
 15 # accompanied this code).
 16 #
 17 # You should have received a copy of the GNU General Public License version
 18 # 2 along with this work; if not, write to the Free Software Foundation,
 19 # Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA.
 20 #
 21 # Please contact Oracle, 500 Oracle Parkway, Redwood Shores, CA 94065 USA
 22 # or visit www.oracle.com if you need additional information or have any
 23 # questions.
 24 #
 25 
 26 ################################################################################
 27 # Setup libffi (Foreign Function Interface)
 28 ################################################################################
 29 AC_DEFUN_ONCE([LIB_SETUP_LIBFFI],
 30 [
 31   UTIL_ARG_ENABLE(NAME: libffi-bundling, DEFAULT: false,
 32       RESULT: ENABLE_LIBFFI_BUNDLING,
 33       DESC: [enable bundling of libffi.so to make the built JDK runnable on more systems])
 34 
 35   AC_ARG_WITH(libffi, [AS_HELP_STRING([--with-libffi],
 36       [specify prefix directory for the libffi package
 37       (expecting the libraries under PATH/lib and the headers under PATH/include)])])
 38   AC_ARG_WITH(libffi-include, [AS_HELP_STRING([--with-libffi-include],
 39       [specify directory for the libffi include files])])
 40   AC_ARG_WITH(libffi-lib, [AS_HELP_STRING([--with-libffi-lib],
 41       [specify directory for the libffi library])])
 42 
 43   if test &quot;x$NEEDS_LIB_FFI&quot; = xfalse; then
 44     if (test &quot;x${with_libffi}&quot; != x &amp;&amp; test &quot;x${with_libffi}&quot; != xno) || \
 45         (test &quot;x${with_libffi_include}&quot; != x &amp;&amp; test &quot;x${with_libffi_include}&quot; != xno) || \
 46         (test &quot;x${with_libffi_lib}&quot; != x &amp;&amp; test &quot;x${with_libffi_lib}&quot; != xno); then
 47       AC_MSG_WARN([[libffi not used, so --with-libffi[-*] is ignored]])
 48     fi
 49     LIBFFI_CFLAGS=
 50     LIBFFI_LIBS=
 51   else
 52     LIBFFI_FOUND=no
 53 
 54     if test &quot;x${with_libffi}&quot; = xno || test &quot;x${with_libffi_include}&quot; = xno || test &quot;x${with_libffi_lib}&quot; = xno; then
 55       AC_MSG_ERROR([It is not possible to disable the use of libffi. Remove the --without-libffi option.])
 56     fi
 57 
 58     if test &quot;x${with_libffi}&quot; != x; then
 59       LIBFFI_LIB_PATH=&quot;${with_libffi}/lib&quot;
 60       LIBFFI_LIBS=&quot;-L${with_libffi}/lib -lffi&quot;
 61       LIBFFI_CFLAGS=&quot;-I${with_libffi}/include&quot;
 62       LIBFFI_FOUND=yes
 63     fi
 64     if test &quot;x${with_libffi_include}&quot; != x; then
 65       LIBFFI_CFLAGS=&quot;-I${with_libffi_include}&quot;
 66       LIBFFI_FOUND=yes
 67     fi
 68     if test &quot;x${with_libffi_lib}&quot; != x; then
 69       LIBFFI_LIB_PATH=&quot;${with_libffi_lib}&quot;
 70       LIBFFI_LIBS=&quot;-L${with_libffi_lib} -lffi&quot;
 71       LIBFFI_FOUND=yes
 72     fi
 73     # Do not try pkg-config if we have a sysroot set.
 74     if test &quot;x$SYSROOT&quot; = x; then
 75       if test &quot;x$LIBFFI_FOUND&quot; = xno; then
 76         # Figure out LIBFFI_CFLAGS and LIBFFI_LIBS
 77         PKG_CHECK_MODULES([LIBFFI], [libffi], [LIBFFI_FOUND=yes], [LIBFFI_FOUND=no])
 78       fi
 79     fi
 80     if test &quot;x$LIBFFI_FOUND&quot; = xno; then
 81       AC_CHECK_HEADERS([ffi.h],
 82           [
 83             LIBFFI_FOUND=yes
 84             LIBFFI_CFLAGS=
 85             LIBFFI_LIBS=-lffi
 86           ],
 87           [LIBFFI_FOUND=no]
 88       )
 89     fi
 90     if test &quot;x$LIBFFI_FOUND&quot; = xno; then
 91       HELP_MSG_MISSING_DEPENDENCY([ffi])
 92       AC_MSG_ERROR([Could not find libffi! $HELP_MSG])
 93     fi
 94 
 95     AC_MSG_CHECKING([if libffi works])
 96     AC_LANG_PUSH(C)
 97     OLD_CFLAGS=&quot;$CFLAGS&quot;
 98     CFLAGS=&quot;$CFLAGS $LIBFFI_CFLAGS&quot;
 99     OLD_LIBS=&quot;$LIBS&quot;
100     LIBS=&quot;$LIBS $LIBFFI_LIBS&quot;
101     AC_LINK_IFELSE([AC_LANG_PROGRAM([#include &lt;ffi.h&gt;],
102         [
103           ffi_call(NULL, NULL, NULL, NULL);
104           return 0;
105         ])],
106         [LIBFFI_WORKS=yes],
107         [LIBFFI_WORKS=no]
108     )
109     CFLAGS=&quot;$OLD_CFLAGS&quot;
110     LIBS=&quot;$OLD_LIBS&quot;
111     AC_LANG_POP(C)
112     AC_MSG_RESULT([$LIBFFI_WORKS])
113 
114     if test &quot;x$LIBFFI_WORKS&quot; = xno; then
115       HELP_MSG_MISSING_DEPENDENCY([ffi])
116       AC_MSG_ERROR([Found libffi but could not link and compile with it. $HELP_MSG])
117     fi
118 
119     # Find the libffi.so.X to bundle
120     if test &quot;x${ENABLE_LIBFFI_BUNDLING}&quot; = &quot;xtrue&quot;; then
121       AC_MSG_CHECKING([for libffi lib file location])
122       if test &quot;x${LIBFFI_LIB_PATH}&quot; != x; then
123         if test -e ${LIBFFI_LIB_PATH}/libffi.so.?; then
124           LIBFFI_LIB_FILE=&quot;${LIBFFI_LIB_PATH}/libffi.so.?&quot;
125         else
126           AC_MSG_ERROR([Could not locate libffi.so.? for bundling in ${LIBFFI_LIB_PATH}])
127         fi
128       else
129         # If we don&#39;t have an explicit path, look in a few obvious places
130         if test &quot;x${OPENJDK_TARGET_CPU}&quot; = &quot;xx86&quot;; then
131           if test -e ${SYSROOT}/usr/lib/libffi.so.? ; then
132             LIBFFI_LIB_FILE=&quot;${SYSROOT}/usr/lib/libffi.so.?&quot;
133           elif test -e ${SYSROOT}/usr/lib/i386-linux-gnu/libffi.so.? ; then
134             LIBFFI_LIB_FILE=&quot;${SYSROOT}/usr/lib/i386-linux-gnu/libffi.so.?&quot;
135           else
136             AC_MSG_ERROR([Could not locate libffi.so.? for bundling])
137           fi
138         elif test &quot;x${OPENJDK_TARGET_CPU}&quot; = &quot;xx86_64&quot;; then
139           if test -e ${SYSROOT}/usr/lib64/libffi.so.? ; then
140             LIBFFI_LIB_FILE=&quot;${SYSROOT}/usr/lib64/libffi.so.?&quot;
141           elif test -e ${SYSROOT}/usr/lib/x86_64-linux-gnu/libffi.so.? ; then
142             LIBFFI_LIB_FILE=&quot;${SYSROOT}/usr/lib/x86_64-linux-gnu/libffi.so.?&quot;
143           else
144             AC_MSG_ERROR([Could not locate libffi.so.? for bundling])
145           fi
146         else
147           # Fallback on the default /usr/lib dir
148           if test -e ${SYSROOT}/usr/lib/libffi.so.? ; then
149             LIBFFI_LIB_FILE=&quot;${SYSROOT}/usr/lib/libffi.so.?&quot;
150           else
151             AC_MSG_ERROR([Could not locate libffi.so.? for bundling])
152           fi
153         fi
154       fi
155       # Make sure the wildcard is evaluated
156       LIBFFI_LIB_FILE=&quot;$(ls ${LIBFFI_LIB_FILE})&quot;
157       AC_MSG_RESULT([${LIBFFI_LIB_FILE}])
158     fi
159   fi
160 
161   AC_SUBST(LIBFFI_CFLAGS)
162   AC_SUBST(LIBFFI_LIBS)
163   AC_SUBST(ENABLE_LIBFFI_BUNDLING)
164   AC_SUBST(LIBFFI_LIB_FILE)
165 ])
    </pre>
  </body>
</html>