<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <title>Old make/autoconf/util.m4</title>
    <link rel="stylesheet" href="../../style.css" />
  </head>
  <body>
    <pre>
  1 #
  2 # Copyright (c) 2011, 2020, Oracle and/or its affiliates. All rights reserved.
  3 # DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.
  4 #
  5 # This code is free software; you can redistribute it and/or modify it
  6 # under the terms of the GNU General Public License version 2 only, as
  7 # published by the Free Software Foundation.  Oracle designates this
  8 # particular file as subject to the &quot;Classpath&quot; exception as provided
  9 # by Oracle in the LICENSE file that accompanied this code.
 10 #
 11 # This code is distributed in the hope that it will be useful, but WITHOUT
 12 # ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 13 # FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
 14 # version 2 for more details (a copy is included in the LICENSE file that
 15 # accompanied this code).
 16 #
 17 # You should have received a copy of the GNU General Public License version
 18 # 2 along with this work; if not, write to the Free Software Foundation,
 19 # Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA.
 20 #
 21 # Please contact Oracle, 500 Oracle Parkway, Redwood Shores, CA 94065 USA
 22 # or visit www.oracle.com if you need additional information or have any
 23 # questions.
 24 #
 25 
 26 m4_include([util_paths.m4])
 27 m4_include([util_windows.m4])
 28 
 29 ###############################################################################
 30 # Create a function/macro that takes a series of named arguments. The call is
 31 # similar to AC_DEFUN, but the setup of the function looks like this:
 32 # UTIL_DEFUN_NAMED([MYFUNC], [FOO *BAR], [$@], [
 33 # ... do something
 34 #   AC_MSG_NOTICE([Value of BAR is ARG_BAR])
 35 # ])
 36 # A star (*) in front of a named argument means that it is required and it&#39;s
 37 # presence will be verified. To pass e.g. the first value as a normal indexed
 38 # argument, use [m4_shift($@)] as the third argument instead of [$@]. These
 39 # arguments are referenced in the function by their name prefixed by ARG_, e.g.
 40 # &quot;ARG_FOO&quot;.
 41 #
 42 # The generated function can be called like this:
 43 # MYFUNC(FOO: [foo-val],
 44 #     BAR: [
 45 #         $ECHO hello world
 46 #     ])
 47 # Note that the argument value must start on the same line as the argument name.
 48 #
 49 # Argument 1: Name of the function to define
 50 # Argument 2: List of legal named arguments, with a * prefix for required arguments
 51 # Argument 3: Argument array to treat as named, typically $@
 52 # Argument 4: The main function body
 53 AC_DEFUN([UTIL_DEFUN_NAMED],
 54 [
 55   AC_DEFUN($1, [
 56     m4_foreach(arg, m4_split(m4_normalize($2)), [
 57       m4_if(m4_bregexp(arg, [^\*]), -1,
 58         [
 59           m4_set_add(legal_named_args, arg)
 60         ],
 61         [
 62           m4_set_add(legal_named_args, m4_substr(arg, 1))
 63           m4_set_add(required_named_args, m4_substr(arg, 1))
 64         ]
 65       )
 66     ])
 67 
 68     m4_foreach([arg], [$3], [
 69       m4_if(m4_bregexp(arg, [: ]), -1, m4_define([arg], m4_bpatsubst(arg, [:], [: ])))
 70       m4_define(arg_name, m4_substr(arg, 0, m4_bregexp(arg, [: ])))
 71       m4_set_contains(legal_named_args, arg_name, [],[AC_MSG_ERROR([Internal error: m4_if(arg_name, , arg, arg_name) is not a valid named argument to [$1]. Valid arguments are &#39;m4_set_contents(defined_args, [ ]) m4_set_contents(legal_named_args, [ ])&#39;.])])
 72       m4_set_remove(required_named_args, arg_name)
 73       m4_set_remove(legal_named_args, arg_name)
 74       m4_pushdef([ARG_][]arg_name, m4_bpatsubst(m4_substr(arg, m4_incr(m4_incr(m4_bregexp(arg, [: ])))), [^\s*], []))
 75       m4_set_add(defined_args, arg_name)
 76       m4_undefine([arg_name])
 77     ])
 78     m4_set_empty(required_named_args, [], [
 79       AC_MSG_ERROR([Internal error: Required named arguments are missing for [$1]. Missing arguments: &#39;m4_set_contents(required_named_args, [ ])&#39;])
 80     ])
 81     m4_foreach([arg], m4_indir([m4_dquote]m4_set_listc([legal_named_args])), [
 82       m4_pushdef([ARG_][]arg, [])
 83       m4_set_add(defined_args, arg)
 84     ])
 85     m4_set_delete(legal_named_args)
 86     m4_set_delete(required_named_args)
 87 
 88     # Execute function body
 89     $4
 90 
 91     m4_foreach([arg], m4_indir([m4_dquote]m4_set_listc([defined_args])), [
 92       m4_popdef([ARG_][]arg)
 93     ])
 94 
 95     m4_set_delete(defined_args)
 96   ])
 97 ])
 98 
 99 ###############################################################################
100 # Assert that a programmatic condition holds. If not, exit with an error message.
101 # Check that a shell expression gives return code 0
102 #
103 # $1: The shell expression to evaluate
104 # $2: A message to describe the expression in case of failure
105 # $2: An message to print in case of failure [optional]
106 #
107 AC_DEFUN([UTIL_ASSERT_SHELL_TEST],
108 [
109   ASSERTION_MSG=&quot;m4_normalize([$3])&quot;
110   if $1; then
111     $ECHO Assertion failed: $2
112     if test &quot;x$3&quot; != x; then
113       $ECHO Assertion message: &quot;$3&quot;
114     fi
115     exit 1
116   fi
117 ])
118 
119 
120 ###############################################################################
121 # Assert that a programmatic condition holds. If not, exit with an error message.
122 # Check that two strings are equal.
123 #
124 # $1: The actual string found
125 # $2: The expected string
126 # $3: An message to print in case of failure [optional]
127 #
128 AC_DEFUN([UTIL_ASSERT_STRING_EQUALS],
129 [
130   UTIL_ASSERT_SHELL_TEST(
131       [test &quot;x[$1]&quot; != &quot;x[$2]&quot;],
132       [Actual value &#39;[$1]&#39; \(&quot;[$1]&quot;\) did not match expected value &#39;[$2]&#39; \(&quot;[$2]&quot;\)],
133       $3)
134 ])
135 
136 ###############################################################################
137 # Assert that a programmatic condition holds. If not, exit with an error message.
138 # Check that two strings not are equal.
139 #
140 # $1: The actual string found
141 # $2: The expected string
142 # $3: An message to print in case of failure [optional]
143 #
144 AC_DEFUN([UTIL_ASSERT_STRING_NOT_EQUALS],
145 [
146   UTIL_ASSERT_SHELL_TEST(
147       [test &quot;x[$1]&quot; = &quot;x[$2]&quot;],
148       [Actual value &#39;[$1]&#39; \(&quot;[$1]&quot;\) unexpectedly matched &#39;[$2]&#39; \(&quot;[$2]&quot;\)],
149       $3)
150 ])
151 
152 ###############################################################################
153 # Assert that a programmatic condition holds. If not, exit with an error message.
154 # Check that the given expression evaluates to the string &#39;true&#39;
155 #
156 # $1: The expression to evaluate
157 # $2: An message to print in case of failure [optional]
158 #
159 AC_DEFUN([UTIL_ASSERT_TRUE],
160 [
161   UTIL_ASSERT_STRING_EQUALS($1, true, $3)
162 ])
163 
164 ###############################################################################
165 # Assert that a programmatic condition holds. If not, exit with an error message.
166 # Check that the given expression does not evaluate to the string &#39;true&#39;
167 #
168 # $1: The expression to evaluate
169 # $2: An message to print in case of failure [optional]
170 #
171 AC_DEFUN([UTIL_ASSERT_NOT_TRUE],
172 [
173   UTIL_ASSERT_STRING_NOT_EQUALS($1, true, $3)
174 ])
175 
176 ###############################################################################
177 # Check if a list of space-separated words are selected only from a list of
178 # space-separated legal words. Typical use is to see if a user-specified
179 # set of words is selected from a set of legal words.
180 #
181 # Sets the specified variable to list of non-matching (offending) words, or to
182 # the empty string if all words are matching the legal set.
183 #
184 # $1: result variable name
185 # $2: list of values to check
186 # $3: list of legal values
187 AC_DEFUN([UTIL_GET_NON_MATCHING_VALUES],
188 [
189   # grep filter function inspired by a comment to http://stackoverflow.com/a/1617326
190   # Notice that the original variant fails on SLES 10 and 11
191   # Some grep versions (at least bsd) behaves strangely on the base case with
192   # no legal_values, so make it explicit.
193   values_to_check=`$ECHO $2 | $TR &#39; &#39; &#39;\n&#39;`
194   legal_values=`$ECHO $3 | $TR &#39; &#39; &#39;\n&#39;`
195   if test -z &quot;$legal_values&quot;; then
196     $1=&quot;$2&quot;
197   else
198     result=`$GREP -Fvx &quot;$legal_values&quot; &lt;&lt;&lt; &quot;$values_to_check&quot; | $GREP -v &#39;^$&#39;`
199     $1=${result//$&#39;\n&#39;/ }
200   fi
201 ])
202 
203 ###############################################################################
204 # Check if a list of space-separated words contains any word(s) from a list of
205 # space-separated illegal words. Typical use is to see if a user-specified
206 # set of words contains any from a set of illegal words.
207 #
208 # Sets the specified variable to list of matching illegal words, or to
209 # the empty string if no words are matching the illegal set.
210 #
211 # $1: result variable name
212 # $2: list of values to check
213 # $3: list of illegal values
214 AC_DEFUN([UTIL_GET_MATCHING_VALUES],
215 [
216   # grep filter function inspired by a comment to http://stackoverflow.com/a/1617326
217   # Notice that the original variant fails on SLES 10 and 11
218   # Some grep versions (at least bsd) behaves strangely on the base case with
219   # no legal_values, so make it explicit.
220   values_to_check=`$ECHO $2 | $TR &#39; &#39; &#39;\n&#39;`
221   illegal_values=`$ECHO $3 | $TR &#39; &#39; &#39;\n&#39;`
222   if test -z &quot;$illegal_values&quot;; then
223     $1=&quot;&quot;
224   else
225     result=`$GREP -Fx &quot;$illegal_values&quot; &lt;&lt;&lt; &quot;$values_to_check&quot; | $GREP -v &#39;^$&#39;`
226     $1=${result//$&#39;\n&#39;/ }
227   fi
228 ])
229 
230 ###############################################################################
231 # Converts an ISO-8601 date/time string to a unix epoch timestamp. If no
232 # suitable conversion method was found, an empty string is returned.
233 #
234 # Sets the specified variable to the resulting list.
235 #
236 # $1: result variable name
237 # $2: input date/time string
238 AC_DEFUN([UTIL_GET_EPOCH_TIMESTAMP],
239 [
240   timestamp=$($DATE --utc --date=$2 +&quot;%s&quot; 2&gt; /dev/null)
241   if test &quot;x$timestamp&quot; = x; then
242     # GNU date format did not work, try BSD date options
243     timestamp=$($DATE -j -f &quot;%F %T&quot; &quot;$2&quot; &quot;+%s&quot; 2&gt; /dev/null)
244     if test &quot;x$timestamp&quot; = x; then
245       # Perhaps the time was missing
246       timestamp=$($DATE -j -f &quot;%F %T&quot; &quot;$2 00:00:00&quot; &quot;+%s&quot; 2&gt; /dev/null)
247       # If this did not work, we give up and return the empty string
248     fi
249   fi
250   $1=$timestamp
251 ])
252 
253 ###############################################################################
254 # Sort a space-separated list, and remove duplicates.
255 #
256 # Sets the specified variable to the resulting list.
257 #
258 # $1: result variable name
259 # $2: list of values to sort
260 AC_DEFUN([UTIL_SORT_LIST],
261 [
262   values_to_sort=`$ECHO $2 | $TR &#39; &#39; &#39;\n&#39;`
263   result=`$SORT -u &lt;&lt;&lt; &quot;$values_to_sort&quot; | $GREP -v &#39;^$&#39;`
264   $1=${result//$&#39;\n&#39;/ }
265 ])
266 
267 ###############################################################################
268 # Test if $1 is a valid argument to $3 (often is $JAVA passed as $3)
269 # If so, then append $1 to $2 \
270 # Also set JVM_ARG_OK to true/false depending on outcome.
271 AC_DEFUN([UTIL_ADD_JVM_ARG_IF_OK],
272 [
273   $ECHO &quot;Check if jvm arg is ok: $1&quot; &gt;&amp;AS_MESSAGE_LOG_FD
274   $ECHO &quot;Command: $3 $1 -version&quot; &gt;&amp;AS_MESSAGE_LOG_FD
275   OUTPUT=`$3 $1 $USER_BOOT_JDK_OPTIONS -version 2&gt;&amp;1`
276   FOUND_WARN=`$ECHO &quot;$OUTPUT&quot; | $GREP -i warn`
277   FOUND_VERSION=`$ECHO $OUTPUT | $GREP &quot; version \&quot;&quot;`
278   if test &quot;x$FOUND_VERSION&quot; != x &amp;&amp; test &quot;x$FOUND_WARN&quot; = x; then
279     $2=&quot;[$]$2 $1&quot;
280     JVM_ARG_OK=true
281   else
282     $ECHO &quot;Arg failed:&quot; &gt;&amp;AS_MESSAGE_LOG_FD
283     $ECHO &quot;$OUTPUT&quot; &gt;&amp;AS_MESSAGE_LOG_FD
284     JVM_ARG_OK=false
285   fi
286 ])
287 
288 ###############################################################################
289 # Register a --with argument but mark it as deprecated
290 # $1: The name of the with argument to deprecate, not including --with-
291 AC_DEFUN([UTIL_DEPRECATED_ARG_WITH],
292 [
293   AC_ARG_WITH($1, [AS_HELP_STRING([--with-$1],
294       [Deprecated. Option is kept for backwards compatibility and is ignored])],
295       [AC_MSG_WARN([Option --with-$1 is deprecated and will be ignored.])])
296 ])
297 
298 ###############################################################################
299 # Register a --enable argument but mark it as deprecated
300 # $1: The name of the with argument to deprecate, not including --enable-
301 AC_DEFUN([UTIL_DEPRECATED_ARG_ENABLE],
302 [
303   AC_ARG_ENABLE($1, [AS_HELP_STRING([--enable-$1],
304       [Deprecated. Option is kept for backwards compatibility and is ignored])],
305       [AC_MSG_WARN([Option --enable-$1 is deprecated and will be ignored.])])
306 ])
307 
308 ###############################################################################
309 # Register an --enable-* argument as an alias for another argument.
310 # $1: The name of the enable argument for the new alias, not including --enable-
311 # $2: The full name of the argument of which to make this an alias, including
312 #     --enable- or --with-.
313 AC_DEFUN([UTIL_ALIASED_ARG_ENABLE],
314 [
315   AC_ARG_ENABLE($1, [AS_HELP_STRING([--enable-$1], [alias for $2])], [
316     # Use m4 to strip initial -- from target ($2), convert - to _, prefix enable_
317     # to new alias name, and create a shell variable assignment,
318     # e.g.: enable_old_style=&quot;$enable_new_alias&quot;
319     m4_translit(m4_bpatsubst($2, --), -, _)=&quot;$[enable_]m4_translit($1, -, _)&quot;
320   ])
321 ])
322 
323 ###############################################################################
324 # Creates a command-line option using the --enable-* pattern. Will return a
325 # value of &#39;true&#39; or &#39;false&#39; in the RESULT variable, depending on whether the
326 # option was enabled or not by the user. The option can not be turned on if it
327 # is not available, as specified by AVAILABLE and/or AVAILABLE_CHECK.
328 #
329 # Arguments:
330 #   NAME: The base name of this option (i.e. what follows --enable-). Required.
331 #   RESULT: The name of the variable to set to the result. Defaults to
332 #     &lt;NAME in uppercase&gt;_RESULT.
333 #   DEFAULT: The default value for this option. Can be true, false or auto.
334 #     Defaults to true.
335 #   AVAILABLE: If true, this option is allowed to be selected. Defaults to true.
336 #   DESC: A description of this option. Defaults to a generic and unhelpful
337 #     string.
338 #   DEFAULT_DESC: A message describing the default value, for the help. Defaults
339 #     to the literal value of DEFAULT.
340 #   CHECKING_MSG: The message to present to user when checking this option.
341 #     Defaults to a generic message.
342 #   CHECK_AVAILABLE: An optional code block to execute to determine if the
343 #     option should be available. Must set AVAILABLE to &#39;false&#39; if not.
344 #   IF_GIVEN:  An optional code block to execute if the option was given on the
345 #     command line (regardless of the value).
346 #   IF_NOT_GIVEN:  An optional code block to execute if the option was not given
347 #     on the command line (regardless of the value).
348 #   IF_ENABLED:  An optional code block to execute if the option is turned on.
349 #   IF_DISABLED:  An optional code block to execute if the option is turned off.
350 #
351 UTIL_DEFUN_NAMED([UTIL_ARG_ENABLE],
352     [*NAME RESULT DEFAULT AVAILABLE DESC DEFAULT_DESC CHECKING_MSG
353     CHECK_AVAILABLE IF_GIVEN IF_NOT_GIVEN IF_ENABLED IF_DISABLED], [$@],
354 [
355   ##########################
356   # Part 1: Set up m4 macros
357   ##########################
358 
359   # If DEFAULT is not specified, set it to &#39;true&#39;.
360   m4_define([ARG_DEFAULT], m4_if(ARG_DEFAULT, , true, ARG_DEFAULT))
361 
362   # If AVAILABLE is not specified, set it to &#39;true&#39;.
363   m4_define([ARG_AVAILABLE], m4_if(ARG_AVAILABLE, , true, ARG_AVAILABLE))
364 
365   # If DEFAULT_DESC is not specified, calculate it from DEFAULT.
366   m4_define([ARG_DEFAULT_DESC], m4_if(ARG_DEFAULT_DESC, , m4_if(ARG_DEFAULT, true, enabled, m4_if(ARG_DEFAULT, false, disabled, ARG_DEFAULT)), ARG_DEFAULT_DESC))
367 
368   # If RESULT is not specified, set it to &#39;ARG_NAME[_ENABLED]&#39;.
369   m4_define([ARG_RESULT], m4_if(ARG_RESULT, , m4_translit(ARG_NAME, [a-z-], [A-Z_])[_ENABLED], ARG_RESULT))
370   # Construct shell variable names for the option
371   m4_define(ARG_OPTION, [enable_]m4_translit(ARG_NAME, [-], [_]))
372   m4_define(ARG_GIVEN, m4_translit(ARG_NAME, [a-z-], [A-Z_])[_GIVEN])
373 
374   # If DESC is not specified, set it to a generic description.
375   m4_define([ARG_DESC], m4_if(ARG_DESC, , [Enable the ARG_NAME feature], m4_normalize(ARG_DESC)))
376 
377   # If CHECKING_MSG is not specified, set it to a generic description.
378   m4_define([ARG_CHECKING_MSG], m4_if(ARG_CHECKING_MSG, , [for --enable-ARG_NAME], ARG_CHECKING_MSG))
379 
380   # If the code blocks are not given, set them to the empty statements to avoid
381   # tripping up bash.
382   m4_define([ARG_CHECK_AVAILABLE], m4_if(ARG_CHECK_AVAILABLE, , :, ARG_CHECK_AVAILABLE))
383   m4_define([ARG_IF_GIVEN], m4_if(ARG_IF_GIVEN, , :, ARG_IF_GIVEN))
384   m4_define([ARG_IF_NOT_GIVEN], m4_if(ARG_IF_NOT_GIVEN, , :, ARG_IF_NOT_GIVEN))
385   m4_define([ARG_IF_ENABLED], m4_if(ARG_IF_ENABLED, , :, ARG_IF_ENABLED))
386   m4_define([ARG_IF_DISABLED], m4_if(ARG_IF_DISABLED, , :, ARG_IF_DISABLED))
387 
388   ##########################
389   # Part 2: Set up autoconf shell code
390   ##########################
391 
392   # Check that DEFAULT has a valid value
393   if test &quot;[x]ARG_DEFAULT&quot; != xtrue &amp;&amp; test &quot;[x]ARG_DEFAULT&quot; != xfalse &amp;&amp; \
394       test &quot;[x]ARG_DEFAULT&quot; != xauto ; then
395     AC_MSG_ERROR([Internal error: Argument DEFAULT to [UTIL_ARG_ENABLE] can only be true, false or auto, was: &#39;ARG_DEFAULT&#39;])
396   fi
397 
398   # Check that AVAILABLE has a valid value
399   if test &quot;[x]ARG_AVAILABLE&quot; != xtrue &amp;&amp; test &quot;[x]ARG_AVAILABLE&quot; != xfalse; then
400     AC_MSG_ERROR([Internal error: Argument AVAILABLE to [UTIL_ARG_ENABLE] can only be true or false, was: &#39;ARG_AVAILABLE&#39;])
401   fi
402 
403   AC_ARG_ENABLE(ARG_NAME, AS_HELP_STRING([--enable-]ARG_NAME,
404       [ARG_DESC [ARG_DEFAULT_DESC]]), [ARG_GIVEN=true], [ARG_GIVEN=false])
405 
406   # Check if the option is available
407   AVAILABLE=ARG_AVAILABLE
408   # Run the available check block (if any), which can overwrite AVAILABLE.
409   ARG_CHECK_AVAILABLE
410 
411   # Check if the option should be turned on
412   AC_MSG_CHECKING(ARG_CHECKING_MSG)
413   if test x$ARG_GIVEN = xfalse; then
414     if test ARG_DEFAULT = auto; then
415       # If not given, and default is auto, set it to true iff it&#39;s available.
416       ARG_RESULT=$AVAILABLE
417       REASON=&quot;from default &#39;auto&#39;&quot;
418     else
419       ARG_RESULT=ARG_DEFAULT
420       REASON=&quot;default&quot;
421     fi
422   else
423     if test x$ARG_OPTION = xyes; then
424       ARG_RESULT=true
425       REASON=&quot;from command line&quot;
426     elif test x$ARG_OPTION = xno; then
427       ARG_RESULT=false
428       REASON=&quot;from command line&quot;
429     elif test x$ARG_OPTION = xauto; then
430       if test ARG_DEFAULT = auto; then
431         # If both given and default is auto, set it to true iff it&#39;s available.
432         ARG_RESULT=$AVAILABLE
433       else
434         ARG_RESULT=ARG_DEFAULT
435       fi
436       REASON=&quot;from command line &#39;auto&#39;&quot;
437     else
438       AC_MSG_ERROR([Option [--enable-]ARG_NAME can only be &#39;yes&#39;, &#39;no&#39; or &#39;auto&#39;])
439     fi
440   fi
441 
442   if test x$ARG_RESULT = xtrue; then
443     AC_MSG_RESULT([enabled, $REASON])
444     if test x$AVAILABLE = xfalse; then
445       AC_MSG_ERROR([Option [--enable-]ARG_NAME is not available])
446     fi
447   else
448     AC_MSG_RESULT([disabled, $REASON])
449   fi
450 
451   # Execute result payloads, if present
452   if test x$ARG_GIVEN = xtrue; then
453     ARG_IF_GIVEN
454   else
455     ARG_IF_NOT_GIVEN
456   fi
457 
458   if test x$ARG_RESULT = xtrue; then
459     ARG_IF_ENABLED
460   else
461     ARG_IF_DISABLED
462   fi
463 ])
464 
465 ###############################################################################
466 # Test that variable $1 denoting a program is not empty. If empty, exit with an error.
467 # $1: variable to check
468 AC_DEFUN([UTIL_CHECK_NONEMPTY],
469 [
470   if test &quot;x[$]$1&quot; = x; then
471     AC_MSG_ERROR([Could not find required tool for $1])
472   fi
473 ])
474 
475 ###############################################################################
476 # Setup a tool for the given variable. If correctly specified by the user,
477 # use that value, otherwise search for the tool using the supplied code snippet.
478 # $1: variable to set
479 # $2: code snippet to call to look for the tool
480 # $3: code snippet to call if variable was used to find tool
481 AC_DEFUN([UTIL_SETUP_TOOL],
482 [
483   # Publish this variable in the help.
484   AC_ARG_VAR($1, [Override default value for $1])
485 
486   if [[ -z &quot;${$1+x}&quot; ]]; then
487     # The variable is not set by user, try to locate tool using the code snippet
488     $2
489   else
490     # The variable is set, but is it from the command line or the environment?
491 
492     # Try to remove the string !$1! from our list.
493     try_remove_var=${CONFIGURE_OVERRIDDEN_VARIABLES//!$1!/}
494     if test &quot;x$try_remove_var&quot; = &quot;x$CONFIGURE_OVERRIDDEN_VARIABLES&quot;; then
495       # If it failed, the variable was not from the command line. Ignore it,
496       # but warn the user (except for BASH, which is always set by the calling BASH).
497       if test &quot;x$1&quot; != xBASH; then
498         AC_MSG_WARN([Ignoring value of $1 from the environment. Use command line variables instead.])
499       fi
500       # Try to locate tool using the code snippet
501       $2
502     else
503       # If it succeeded, then it was overridden by the user. We will use it
504       # for the tool.
505 
506       # First remove it from the list of overridden variables, so we can test
507       # for unknown variables in the end.
508       CONFIGURE_OVERRIDDEN_VARIABLES=&quot;$try_remove_var&quot;
509 
510       tool_override=[$]$1
511       AC_MSG_NOTICE([User supplied override $1=&quot;$tool_override&quot;])
512 
513       # Check if we try to supply an empty value
514       if test &quot;x$tool_override&quot; = x; then
515         AC_MSG_CHECKING([for $1])
516         AC_MSG_RESULT([disabled])
517       else
518         # Split up override in command part and argument part
519         tool_and_args=($tool_override)
520         [ tool_command=${tool_and_args[0]} ]
521         [ unset &#39;tool_and_args[0]&#39; ]
522         [ tool_args=${tool_and_args[@]} ]
523 
524         # Check if the provided tool contains a complete path.
525         tool_basename=&quot;${tool_command##*/}&quot;
526         if test &quot;x$tool_basename&quot; = &quot;x$tool_command&quot;; then
527           # A command without a complete path is provided, search $PATH.
528           AC_MSG_NOTICE([Will search for user supplied tool &quot;$tool_basename&quot;])
529           AC_PATH_PROG($1, $tool_basename)
530           if test &quot;x[$]$1&quot; = x; then
531             AC_MSG_ERROR([User supplied tool $1=&quot;$tool_basename&quot; could not be found])
532           fi
533         else
534           # Otherwise we believe it is a complete path. Use it as it is.
535           AC_MSG_NOTICE([Will use user supplied tool &quot;$tool_command&quot;])
536           AC_MSG_CHECKING([for $tool_command])
537           if test ! -x &quot;$tool_command&quot;; then
538             AC_MSG_RESULT([not found])
539             AC_MSG_ERROR([User supplied tool $1=&quot;$tool_command&quot; does not exist or is not executable])
540           fi
541            $1=&quot;$tool_command&quot;
542           AC_MSG_RESULT([found])
543         fi
544         if test &quot;x$tool_args&quot; != x; then
545           # If we got arguments, re-append them to the command after the fixup.
546           $1=&quot;[$]$1 $tool_args&quot;
547         fi
548       fi
549     fi
550     $3
551   fi
552 ])
553 
554 ###############################################################################
555 # Call UTIL_SETUP_TOOL with AC_PATH_PROGS to locate the tool
556 # $1: variable to set
557 # $2: executable name (or list of names) to look for
558 # $3: [path]
559 AC_DEFUN([UTIL_PATH_PROGS],
560 [
561   UTIL_SETUP_TOOL($1, [AC_PATH_PROGS($1, $2, , $3)])
562 ])
563 
564 ###############################################################################
565 # Call UTIL_SETUP_TOOL with AC_CHECK_TOOLS to locate the tool
566 # $1: variable to set
567 # $2: executable name (or list of names) to look for
568 AC_DEFUN([UTIL_CHECK_TOOLS],
569 [
570   UTIL_SETUP_TOOL($1, [AC_CHECK_TOOLS($1, $2)])
571 ])
572 
573 ###############################################################################
574 # Like UTIL_PATH_PROGS but fails if no tool was found.
575 # $1: variable to set
576 # $2: executable name (or list of names) to look for
577 # $3: [path]
578 AC_DEFUN([UTIL_REQUIRE_PROGS],
579 [
580   UTIL_PATH_PROGS($1, $2, , $3)
581   UTIL_CHECK_NONEMPTY($1)
582 ])
583 
584 ###############################################################################
585 # Like UTIL_SETUP_TOOL but fails if no tool was found.
586 # $1: variable to set
587 # $2: autoconf macro to call to look for the special tool
588 AC_DEFUN([UTIL_REQUIRE_SPECIAL],
589 [
590   UTIL_SETUP_TOOL($1, [$2])
591   UTIL_CHECK_NONEMPTY($1)
592 ])
593 
594 ###############################################################################
595 # Like UTIL_REQUIRE_PROGS but also allows for bash built-ins
596 # $1: variable to set
597 # $2: executable name (or list of names) to look for
598 # $3: [path]
599 AC_DEFUN([UTIL_REQUIRE_BUILTIN_PROGS],
600 [
601   UTIL_SETUP_TOOL($1, [AC_PATH_PROGS($1, $2, , $3)])
602   if test &quot;x[$]$1&quot; = x; then
603     AC_MSG_NOTICE([Required tool $2 not found in PATH, checking built-in])
604     if command -v $2 &gt; /dev/null 2&gt;&amp;1; then
605       AC_MSG_NOTICE([Found $2 as shell built-in. Using it])
606       $1=&quot;$2&quot;
607     else
608       AC_MSG_ERROR([Required tool $2 also not found as built-in.])
609     fi
610   fi
611   UTIL_CHECK_NONEMPTY($1)
612 ])
    </pre>
  </body>
</html>