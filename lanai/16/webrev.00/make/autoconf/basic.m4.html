<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <title>New make/autoconf/basic.m4</title>
    <link rel="stylesheet" href="../../style.css" />
  </head>
  <body>
    <pre>
  1 #
  2 # Copyright (c) 2011, 2020, Oracle and/or its affiliates. All rights reserved.
  3 # DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.
  4 #
  5 # This code is free software; you can redistribute it and/or modify it
  6 # under the terms of the GNU General Public License version 2 only, as
  7 # published by the Free Software Foundation.  Oracle designates this
  8 # particular file as subject to the &quot;Classpath&quot; exception as provided
  9 # by Oracle in the LICENSE file that accompanied this code.
 10 #
 11 # This code is distributed in the hope that it will be useful, but WITHOUT
 12 # ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 13 # FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
 14 # version 2 for more details (a copy is included in the LICENSE file that
 15 # accompanied this code).
 16 #
 17 # You should have received a copy of the GNU General Public License version
 18 # 2 along with this work; if not, write to the Free Software Foundation,
 19 # Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA.
 20 #
 21 # Please contact Oracle, 500 Oracle Parkway, Redwood Shores, CA 94065 USA
 22 # or visit www.oracle.com if you need additional information or have any
 23 # questions.
 24 #
 25 
 26 m4_include([basic_tools.m4])
 27 m4_include([basic_windows.m4])
 28 
 29 ###############################################################################
 30 AC_DEFUN_ONCE([BASIC_INIT],
 31 [
 32   # Save the original command line. This is passed to us by the wrapper configure script.
 33   AC_SUBST(CONFIGURE_COMMAND_LINE)
 34   # AUTOCONF might be set in the environment by the user. Preserve for &quot;make reconfigure&quot;.
 35   AC_SUBST(AUTOCONF)
 36   # Save the path variable before it gets changed
 37   ORIGINAL_PATH=&quot;$PATH&quot;
 38   AC_SUBST(ORIGINAL_PATH)
 39   DATE_WHEN_CONFIGURED=`date`
 40   AC_SUBST(DATE_WHEN_CONFIGURED)
 41   AC_MSG_NOTICE([Configuration created at $DATE_WHEN_CONFIGURED.])
 42 ])
 43 
 44 ###############################################################################
 45 # Check that there are no unprocessed overridden variables left.
 46 # If so, they are an incorrect argument and we will exit with an error.
 47 AC_DEFUN([BASIC_CHECK_LEFTOVER_OVERRIDDEN],
 48 [
 49   if test &quot;x$CONFIGURE_OVERRIDDEN_VARIABLES&quot; != x; then
 50     # Replace the separating ! with spaces before presenting for end user.
 51     unknown_variables=${CONFIGURE_OVERRIDDEN_VARIABLES//!/ }
 52     AC_MSG_WARN([The following variables might be unknown to configure: $unknown_variables])
 53   fi
 54 ])
 55 
 56 ###############################################################################
 57 # Setup basic configuration paths, and platform-specific stuff related to PATHs.
 58 AC_DEFUN_ONCE([BASIC_SETUP_PATHS],
 59 [
 60   # Save the current directory this script was started from
 61   CONFIGURE_START_DIR=&quot;$PWD&quot;
 62 
 63   # We might need to rewrite ORIGINAL_PATH, if it includes &quot;#&quot;, to quote them
 64   # for make. We couldn&#39;t do this when we retrieved ORIGINAL_PATH, since SED
 65   # was not available at that time.
 66   REWRITTEN_PATH=`$ECHO &quot;$ORIGINAL_PATH&quot; | $SED -e &#39;s/#/\\\\#/g&#39;`
 67   if test &quot;x$REWRITTEN_PATH&quot; != &quot;x$ORIGINAL_PATH&quot;; then
 68     ORIGINAL_PATH=&quot;$REWRITTEN_PATH&quot;
 69     AC_MSG_NOTICE([Rewriting ORIGINAL_PATH to $REWRITTEN_PATH])
 70   fi
 71 
 72   if test &quot;x$OPENJDK_TARGET_OS&quot; = &quot;xwindows&quot;; then
 73     PATH_SEP=&quot;;&quot;
 74     EXE_SUFFIX=&quot;.exe&quot;
 75     BASIC_CHECK_PATHS_WINDOWS
 76   else
 77     PATH_SEP=&quot;:&quot;
 78     EXE_SUFFIX=&quot;&quot;
 79   fi
 80   AC_SUBST(PATH_SEP)
 81   AC_SUBST(EXE_SUFFIX)
 82 
 83   # We get the top-level directory from the supporting wrappers.
 84   AC_MSG_CHECKING([for top-level directory])
 85   AC_MSG_RESULT([$TOPDIR])
 86   AC_SUBST(TOPDIR)
 87   AC_SUBST(CONFIGURE_START_DIR)
 88 
 89   # We can only call UTIL_FIXUP_PATH after BASIC_CHECK_PATHS_WINDOWS.
 90   UTIL_FIXUP_PATH(CONFIGURE_START_DIR)
 91   UTIL_FIXUP_PATH(TOPDIR)
 92 
 93   # Locate the directory of this script.
 94   AUTOCONF_DIR=$TOPDIR/make/autoconf
 95 
 96   # Setup username (for use in adhoc version strings etc)
 97   # Outer [ ] to quote m4.
 98   [ USERNAME=`$ECHO &quot;$USER&quot; | $TR -d -c &#39;[a-z][A-Z][0-9]&#39;` ]
 99   AC_SUBST(USERNAME)
100 ])
101 
102 ###############################################################################
103 # Evaluates platform specific overrides for devkit variables.
104 # $1: Name of variable
105 AC_DEFUN([BASIC_EVAL_DEVKIT_VARIABLE],
106 [
107   if test &quot;x[$]$1&quot; = x; then
108     eval $1=&quot;\${$1_${OPENJDK_TARGET_CPU}}&quot;
109   fi
110 ])
111 
112 ###############################################################################
113 AC_DEFUN_ONCE([BASIC_SETUP_DEVKIT],
114 [
115   AC_ARG_WITH([devkit], [AS_HELP_STRING([--with-devkit],
116       [use this devkit for compilers, tools and resources])])
117 
118   if test &quot;x$with_devkit&quot; = xyes; then
119     AC_MSG_ERROR([--with-devkit must have a value])
120   elif test &quot;x$with_devkit&quot; != x &amp;&amp; test &quot;x$with_devkit&quot; != xno; then
121     UTIL_FIXUP_PATH([with_devkit])
122     DEVKIT_ROOT=&quot;$with_devkit&quot;
123     # Check for a meta data info file in the root of the devkit
124     if test -f &quot;$DEVKIT_ROOT/devkit.info&quot;; then
125       . $DEVKIT_ROOT/devkit.info
126       # This potentially sets the following:
127       # A descriptive name of the devkit
128       BASIC_EVAL_DEVKIT_VARIABLE([DEVKIT_NAME])
129       # Corresponds to --with-extra-path
130       BASIC_EVAL_DEVKIT_VARIABLE([DEVKIT_EXTRA_PATH])
131       # Corresponds to --with-toolchain-path
132       BASIC_EVAL_DEVKIT_VARIABLE([DEVKIT_TOOLCHAIN_PATH])
133       # Corresponds to --with-sysroot
134       BASIC_EVAL_DEVKIT_VARIABLE([DEVKIT_SYSROOT])
135 
136       # Identifies the Visual Studio version in the devkit
137       BASIC_EVAL_DEVKIT_VARIABLE([DEVKIT_VS_VERSION])
138       # The Visual Studio include environment variable
139       BASIC_EVAL_DEVKIT_VARIABLE([DEVKIT_VS_INCLUDE])
140       # The Visual Studio lib environment variable
141       BASIC_EVAL_DEVKIT_VARIABLE([DEVKIT_VS_LIB])
142       # Corresponds to --with-msvcr-dll
143       BASIC_EVAL_DEVKIT_VARIABLE([DEVKIT_MSVCR_DLL])
144       # Corresponds to --with-msvcp-dll
145       BASIC_EVAL_DEVKIT_VARIABLE([DEVKIT_MSVCP_DLL])
146       # Corresponds to --with-ucrt-dll-dir
147       BASIC_EVAL_DEVKIT_VARIABLE([DEVKIT_UCRT_DLL_DIR])
148     fi
149 
150     AC_MSG_CHECKING([for devkit])
151     if test &quot;x$DEVKIT_NAME&quot; != x; then
152       AC_MSG_RESULT([$DEVKIT_NAME in $DEVKIT_ROOT])
153     else
154       AC_MSG_RESULT([$DEVKIT_ROOT])
155     fi
156 
157     UTIL_PREPEND_TO_PATH([EXTRA_PATH],$DEVKIT_EXTRA_PATH)
158 
159     # Fallback default of just /bin if DEVKIT_PATH is not defined
160     if test &quot;x$DEVKIT_TOOLCHAIN_PATH&quot; = x; then
161       DEVKIT_TOOLCHAIN_PATH=&quot;$DEVKIT_ROOT/bin&quot;
162     fi
163     UTIL_PREPEND_TO_PATH([TOOLCHAIN_PATH],$DEVKIT_TOOLCHAIN_PATH)
164 
165     # If DEVKIT_SYSROOT is set, use that, otherwise try a couple of known
166     # places for backwards compatiblity.
167     if test &quot;x$DEVKIT_SYSROOT&quot; != x; then
168       SYSROOT=&quot;$DEVKIT_SYSROOT&quot;
169     elif test -d &quot;$DEVKIT_ROOT/$host_alias/libc&quot;; then
170       SYSROOT=&quot;$DEVKIT_ROOT/$host_alias/libc&quot;
171     elif test -d &quot;$DEVKIT_ROOT/$host/sys-root&quot;; then
172       SYSROOT=&quot;$DEVKIT_ROOT/$host/sys-root&quot;
173     fi
174 
175     if test &quot;x$DEVKIT_ROOT&quot; != x; then
176       DEVKIT_LIB_DIR=&quot;$DEVKIT_ROOT/lib&quot;
177       if test &quot;x$OPENJDK_TARGET_CPU_BITS&quot; = x64; then
178         DEVKIT_LIB_DIR=&quot;$DEVKIT_ROOT/lib64&quot;
179       fi
180       AC_SUBST(DEVKIT_LIB_DIR)
181     fi
182   fi
183 
184   # You can force the sysroot if the sysroot encoded into the compiler tools
185   # is not correct.
186   AC_ARG_WITH(sys-root, [AS_HELP_STRING([--with-sys-root],
187       [alias for --with-sysroot for backwards compatability])],
188       [SYSROOT=$with_sys_root]
189   )
190 
191   AC_ARG_WITH(sysroot, [AS_HELP_STRING([--with-sysroot],
192       [use this directory as sysroot])],
193       [SYSROOT=$with_sysroot]
194   )
195 
196   AC_ARG_WITH([tools-dir], [AS_HELP_STRING([--with-tools-dir],
197       [alias for --with-toolchain-path for backwards compatibility])],
198       [UTIL_PREPEND_TO_PATH([TOOLCHAIN_PATH],$with_tools_dir)]
199   )
200 
201   AC_ARG_WITH([toolchain-path], [AS_HELP_STRING([--with-toolchain-path],
202       [prepend these directories when searching for toolchain binaries (compilers etc)])],
203       [UTIL_PREPEND_TO_PATH([TOOLCHAIN_PATH],$with_toolchain_path)]
204   )
205 
206   AC_ARG_WITH([extra-path], [AS_HELP_STRING([--with-extra-path],
207       [prepend these directories to the default path])],
208       [UTIL_PREPEND_TO_PATH([EXTRA_PATH],$with_extra_path)]
209   )
210 
211   if test &quot;x$OPENJDK_BUILD_OS&quot; = &quot;xmacosx&quot;; then
212     # If a devkit has been supplied, find xcodebuild in the toolchain_path.
213     # If not, detect if Xcode is installed by running xcodebuild -version
214     # if no Xcode installed, xcodebuild exits with 1
215     # if Xcode is installed, even if xcode-select is misconfigured, then it exits with 0
216     if test &quot;x$DEVKIT_ROOT&quot; != x || /usr/bin/xcodebuild -version &gt;/dev/null 2&gt;&amp;1; then
217       # We need to use xcodebuild in the toolchain dir provided by the user, this will
218       # fall back on the stub binary in /usr/bin/xcodebuild
219       AC_PATH_PROG([XCODEBUILD], [xcodebuild], [/usr/bin/xcodebuild], [$TOOLCHAIN_PATH])
220     else
221       # this should result in SYSROOT being empty, unless --with-sysroot is provided
222       # when only the command line tools are installed there are no SDKs, so headers
223       # are copied into the system frameworks
224       XCODEBUILD=
225       AC_SUBST(XCODEBUILD)
226     fi
227 
228     AC_MSG_CHECKING([for sdk name])
229     AC_ARG_WITH([sdk-name], [AS_HELP_STRING([--with-sdk-name],
230         [use the platform SDK of the given name. @&lt;:@macosx@:&gt;@])],
231         [SDKNAME=$with_sdk_name]
232     )
233     AC_MSG_RESULT([$SDKNAME])
234 
235     # if toolchain path is specified then don&#39;t rely on system headers, they may not compile
236     HAVE_SYSTEM_FRAMEWORK_HEADERS=0
237     test -z &quot;$TOOLCHAIN_PATH&quot; &amp;&amp; \
238       HAVE_SYSTEM_FRAMEWORK_HEADERS=`test ! -f /System/Library/Frameworks/Foundation.framework/Headers/Foundation.h; echo $?`
239 
240     if test -z &quot;$SYSROOT&quot;; then
241       if test -n &quot;$XCODEBUILD&quot;; then
242         # if we don&#39;t have system headers, use default SDK name (last resort)
243         if test -z &quot;$SDKNAME&quot; -a $HAVE_SYSTEM_FRAMEWORK_HEADERS -eq 0; then
244           SDKNAME=${SDKNAME:-macosx}
245         fi
246 
247         if test -n &quot;$SDKNAME&quot;; then
248           # Call xcodebuild to determine SYSROOT
249           SYSROOT=`&quot;$XCODEBUILD&quot; -sdk $SDKNAME -version | $GREP &#39;^Path: &#39; | $SED &#39;s/Path: //&#39;`
250         fi
251       else
252         if test $HAVE_SYSTEM_FRAMEWORK_HEADERS -eq 0; then
253           AC_MSG_ERROR([No xcodebuild tool and no system framework headers found, use --with-sysroot or --with-sdk-name to provide a path to a valid SDK])
254         fi
255       fi
256     else
257       # warn user if --with-sdk-name was also set
258       if test -n &quot;$with_sdk_name&quot;; then
259         AC_MSG_WARN([Both SYSROOT and --with-sdk-name are set, only SYSROOT will be used])
260       fi
261     fi
262 
263     if test $HAVE_SYSTEM_FRAMEWORK_HEADERS -eq 0 -a -z &quot;$SYSROOT&quot;; then
264       # If no system framework headers, then SYSROOT must be set, or we won&#39;t build
265       AC_MSG_ERROR([Unable to determine SYSROOT and no headers found in /System/Library/Frameworks. Check Xcode configuration, --with-sysroot or --with-sdk-name arguments.])
266     fi
267 
268     # Perform a basic sanity test
269     if test ! -f &quot;$SYSROOT/System/Library/Frameworks/Foundation.framework/Headers/Foundation.h&quot;; then
270       if test -z &quot;$SYSROOT&quot;; then
271         AC_MSG_ERROR([Unable to find required framework headers, provide a path to an SDK via --with-sysroot or --with-sdk-name and be sure Xcode is installed properly])
272       else
273         AC_MSG_ERROR([Invalid SDK or SYSROOT path, dependent framework headers not found])
274       fi
275     fi
276 
277     # set SDKROOT too, Xcode tools will pick it up
278     SDKROOT=&quot;$SYSROOT&quot;
279     AC_SUBST(SDKROOT)
280   fi
281 
282   # Prepend the extra path to the global path
283   UTIL_PREPEND_TO_PATH([PATH],$EXTRA_PATH)
284 
285   AC_MSG_CHECKING([for sysroot])
286   AC_MSG_RESULT([$SYSROOT])
287   AC_MSG_CHECKING([for toolchain path])
288   AC_MSG_RESULT([$TOOLCHAIN_PATH])
289   AC_MSG_CHECKING([for extra path])
290   AC_MSG_RESULT([$EXTRA_PATH])
291 ])
292 
293 ###############################################################################
294 AC_DEFUN_ONCE([BASIC_SETUP_OUTPUT_DIR],
295 [
296 
297   AC_ARG_WITH(conf-name, [AS_HELP_STRING([--with-conf-name],
298       [use this as the name of the configuration @&lt;:@generated from important configuration options@:&gt;@])],
299       [ CONF_NAME=${with_conf_name} ])
300 
301   # Test from where we are running configure, in or outside of src root.
302   AC_MSG_CHECKING([where to store configuration])
303   if test &quot;x$CONFIGURE_START_DIR&quot; = &quot;x$TOPDIR&quot; \
304       || test &quot;x$CONFIGURE_START_DIR&quot; = &quot;x$CUSTOM_ROOT&quot; \
305       || test &quot;x$CONFIGURE_START_DIR&quot; = &quot;x$TOPDIR/make/autoconf&quot; \
306       || test &quot;x$CONFIGURE_START_DIR&quot; = &quot;x$TOPDIR/make&quot; ; then
307     # We are running configure from the src root.
308     # Create a default ./build/target-variant-debuglevel output root.
309     if test &quot;x${CONF_NAME}&quot; = x; then
310       AC_MSG_RESULT([in default location])
311       CONF_NAME=&quot;${OPENJDK_TARGET_OS}-${OPENJDK_TARGET_CPU}-${JVM_VARIANTS_WITH_AND}-${DEBUG_LEVEL}&quot;
312     else
313       AC_MSG_RESULT([in build directory with custom name])
314     fi
315 
316     if test &quot;x$CUSTOM_ROOT&quot; != x; then
317       WORKSPACE_ROOT=&quot;${CUSTOM_ROOT}&quot;
318     else
319       WORKSPACE_ROOT=&quot;${TOPDIR}&quot;
320     fi
321     OUTPUTDIR=&quot;${WORKSPACE_ROOT}/build/${CONF_NAME}&quot;
322     $MKDIR -p &quot;$OUTPUTDIR&quot;
323     if test ! -d &quot;$OUTPUTDIR&quot;; then
324       AC_MSG_ERROR([Could not create build directory $OUTPUTDIR])
325     fi
326   else
327     # We are running configure from outside of the src dir.
328     # Then use the current directory as output dir!
329     # If configuration is situated in normal build directory, just use the build
330     # directory name as configuration name, otherwise use the complete path.
331     if test &quot;x${CONF_NAME}&quot; = x; then
332       CONF_NAME=`$ECHO $CONFIGURE_START_DIR | $SED -e &quot;s!^${TOPDIR}/build/!!&quot;`
333     fi
334     OUTPUTDIR=&quot;$CONFIGURE_START_DIR&quot;
335     AC_MSG_RESULT([in current directory])
336 
337     # WARNING: This might be a bad thing to do. You need to be sure you want to
338     # have a configuration in this directory. Do some sanity checks!
339 
340     if test ! -e &quot;$OUTPUTDIR/spec.gmk&quot;; then
341       # If we have a spec.gmk, we have run here before and we are OK. Otherwise, check for
342       # other files
343       files_present=`$LS $OUTPUTDIR`
344       # Configure has already touched config.log and confdefs.h in the current dir when this check
345       # is performed.
346       filtered_files=`$ECHO &quot;$files_present&quot; \
347           | $SED -e &#39;s/config.log//g&#39; \
348               -e &#39;s/configure.log//g&#39; \
349               -e &#39;s/confdefs.h//g&#39; \
350               -e &#39;s/configure-support//g&#39; \
351               -e &#39;s/ //g&#39; \
352           | $TR -d &#39;\n&#39;`
353       if test &quot;x$filtered_files&quot; != x; then
354         AC_MSG_NOTICE([Current directory is $CONFIGURE_START_DIR.])
355         AC_MSG_NOTICE([Since this is not the source root, configure will output the configuration here])
356         AC_MSG_NOTICE([(as opposed to creating a configuration in &lt;src_root&gt;/build/&lt;conf-name&gt;).])
357         AC_MSG_NOTICE([However, this directory is not empty. This is not allowed, since it could])
358         AC_MSG_NOTICE([seriously mess up just about everything.])
359         AC_MSG_NOTICE([Try &#39;cd $TOPDIR&#39; and restart configure])
360         AC_MSG_NOTICE([(or create a new empty directory and cd to it).])
361         AC_MSG_ERROR([Will not continue creating configuration in $CONFIGURE_START_DIR])
362       fi
363     fi
364   fi
365   AC_MSG_CHECKING([what configuration name to use])
366   AC_MSG_RESULT([$CONF_NAME])
367 
368   UTIL_FIXUP_PATH(OUTPUTDIR)
369 
370   CONFIGURESUPPORT_OUTPUTDIR=&quot;$OUTPUTDIR/configure-support&quot;
371   $MKDIR -p &quot;$CONFIGURESUPPORT_OUTPUTDIR&quot;
372 
373   SPEC=&quot;$OUTPUTDIR/spec.gmk&quot;
374   AC_SUBST(SPEC)
375   AC_SUBST(CONF_NAME)
376   AC_SUBST(OUTPUTDIR)
377   AC_SUBST(WORKSPACE_ROOT)
378   AC_SUBST(CONFIGURESUPPORT_OUTPUTDIR)
379 
380   # The spec.gmk file contains all variables for the make system.
381   AC_CONFIG_FILES([$OUTPUTDIR/spec.gmk:$AUTOCONF_DIR/spec.gmk.in])
382   # The bootcycle-spec.gmk file contains support for boot cycle builds.
383   AC_CONFIG_FILES([$OUTPUTDIR/bootcycle-spec.gmk:$AUTOCONF_DIR/bootcycle-spec.gmk.in])
384   # The buildjdk-spec.gmk file contains support for building a buildjdk when cross compiling.
385   AC_CONFIG_FILES([$OUTPUTDIR/buildjdk-spec.gmk:$AUTOCONF_DIR/buildjdk-spec.gmk.in])
386   # The compare.sh is used to compare the build output to other builds.
387   AC_CONFIG_FILES([$OUTPUTDIR/compare.sh:$AUTOCONF_DIR/compare.sh.in])
388   # The generated Makefile knows where the spec.gmk is and where the source is.
389   # You can run make from the OUTPUTDIR, or from the top-level Makefile
390   # which will look for generated configurations
391   AC_CONFIG_FILES([$OUTPUTDIR/Makefile:$AUTOCONF_DIR/Makefile.in])
392 ])
393 
394 ###############################################################################
395 # Check if build directory is on local disk. If not possible to determine,
396 # we prefer to claim it&#39;s local.
397 # Argument 1: directory to test
398 # Argument 2: what to do if it is on local disk
399 # Argument 3: what to do otherwise (remote disk or failure)
400 AC_DEFUN([BASIC_CHECK_DIR_ON_LOCAL_DISK],
401 [
402   # df -l lists only local disks; if the given directory is not found then
403   # a non-zero exit code is given
404   if test &quot;x$DF&quot; = x; then
405     if test &quot;x$OPENJDK_BUILD_OS_ENV&quot; = &quot;xwindows.msys&quot;; then
406       # msys does not have df; use Windows &quot;net use&quot; instead.
407       IS_NETWORK_DISK=`net use | grep \`pwd -W | cut -d &quot;:&quot; -f 1 | tr a-z A-Z\`:`
408       if test &quot;x$IS_NETWORK_DISK&quot; = x; then
409         $2
410       else
411         $3
412       fi
413     else
414       # No df here, say it&#39;s local
415       $2
416     fi
417   else
418     # JDK-8189619
419     # df on AIX does not understand -l. On modern AIXes it understands &quot;-T local&quot; which
420     # is the same. On older AIXes we just continue to live with a &quot;not local build&quot; warning.
421     if test &quot;x$OPENJDK_TARGET_OS&quot; = xaix; then
422       DF_LOCAL_ONLY_OPTION=&#39;-T local&#39;
423     else
424       DF_LOCAL_ONLY_OPTION=&#39;-l&#39;
425     fi
426     if $DF $DF_LOCAL_ONLY_OPTION $1 &gt; /dev/null 2&gt;&amp;1; then
427       $2
428     else
429       # In WSL, local Windows drives are considered remote by df, but we are
430       # required to build into a directory accessible from windows, so consider
431       # them local here.
432       if test &quot;x$OPENJDK_BUILD_OS_ENV&quot; = &quot;xwindows.wsl&quot;; then
433         if $DF $1 | $GREP -q &quot;^[[A-Z]]:&quot;; then
434           $2
435         else
436           $3
437         fi
438       else
439         $3
440       fi
441     fi
442   fi
443 ])
444 
445 ###############################################################################
446 # Check that source files have basic read permissions set. This might
447 # not be the case in cygwin in certain conditions.
448 AC_DEFUN_ONCE([BASIC_CHECK_SRC_PERMS],
449 [
450   if test &quot;x$OPENJDK_BUILD_OS_ENV&quot; = &quot;xwindows.cygwin&quot;; then
451     file_to_test=&quot;$TOPDIR/LICENSE&quot;
452     if test `$STAT -c &#39;%a&#39; &quot;$file_to_test&quot;` -lt 400; then
453       AC_MSG_ERROR([Bad file permissions on src files. This is usually caused by cloning the repositories with a non cygwin hg in a directory not created in cygwin.])
454     fi
455   fi
456 ])
457 
458 ###############################################################################
459 AC_DEFUN_ONCE([BASIC_TEST_USABILITY_ISSUES],
460 [
461   AC_MSG_CHECKING([if build directory is on local disk])
462   BASIC_CHECK_DIR_ON_LOCAL_DISK($OUTPUTDIR,
463       [OUTPUT_DIR_IS_LOCAL=&quot;yes&quot;],
464       [OUTPUT_DIR_IS_LOCAL=&quot;no&quot;])
465   AC_MSG_RESULT($OUTPUT_DIR_IS_LOCAL)
466 
467   BASIC_CHECK_SRC_PERMS
468 
469   # Check if the user has any old-style ALT_ variables set.
470   FOUND_ALT_VARIABLES=`env | grep ^ALT_`
471 
472   # Before generating output files, test if they exist. If they do, this is a reconfigure.
473   # Since we can&#39;t properly handle the dependencies for this, warn the user about the situation
474   if test -e $OUTPUTDIR/spec.gmk; then
475     IS_RECONFIGURE=yes
476   else
477     IS_RECONFIGURE=no
478   fi
479 ])
480 
481 ################################################################################
482 #
483 # Default make target
484 #
485 AC_DEFUN_ONCE([BASIC_SETUP_DEFAULT_MAKE_TARGET],
486 [
487   AC_ARG_WITH(default-make-target, [AS_HELP_STRING([--with-default-make-target],
488       [set the default make target @&lt;:@exploded-image@:&gt;@])])
489   if test &quot;x$with_default_make_target&quot; = &quot;x&quot; \
490       || test &quot;x$with_default_make_target&quot; = &quot;xyes&quot;; then
491     DEFAULT_MAKE_TARGET=&quot;exploded-image&quot;
492   elif test &quot;x$with_default_make_target&quot; = &quot;xno&quot;; then
493     AC_MSG_ERROR([--without-default-make-target is not a valid option])
494   else
495     DEFAULT_MAKE_TARGET=&quot;$with_default_make_target&quot;
496   fi
497 
498   AC_SUBST(DEFAULT_MAKE_TARGET)
499 ])
500 
501 ###############################################################################
502 # Setup the default value for LOG=
503 #
504 AC_DEFUN_ONCE([BASIC_SETUP_DEFAULT_LOG],
505 [
506   AC_ARG_WITH(log, [AS_HELP_STRING([--with-log],
507       [[default vaue for make LOG argument [warn]]])])
508   AC_MSG_CHECKING([for default LOG value])
509   if test &quot;x$with_log&quot; = x; then
510     DEFAULT_LOG=&quot;&quot;
511   else
512     # Syntax for valid LOG options is a bit too complex for it to be worth
513     # implementing a test for correctness in configure. Just accept it.
514     DEFAULT_LOG=$with_log
515   fi
516   AC_MSG_RESULT([$DEFAULT_LOG])
517   AC_SUBST(DEFAULT_LOG)
518 ])
519 
520 ###############################################################################
521 # Code to run after AC_OUTPUT
522 AC_DEFUN_ONCE([BASIC_POST_CONFIG_OUTPUT],
523 [
524   # Try to move config.log (generated by autoconf) to the configure-support directory.
525   if test -e ./config.log; then
526     $MV -f ./config.log &quot;$CONFIGURESUPPORT_OUTPUTDIR/config.log&quot; 2&gt; /dev/null
527   fi
528 
529   # Rotate our log file (configure.log)
530   if test -e &quot;$OUTPUTDIR/configure.log.old&quot;; then
531     $RM -f &quot;$OUTPUTDIR/configure.log.old&quot;
532   fi
533   if test -e &quot;$OUTPUTDIR/configure.log&quot;; then
534     $MV -f &quot;$OUTPUTDIR/configure.log&quot; &quot;$OUTPUTDIR/configure.log.old&quot; 2&gt; /dev/null
535   fi
536 
537   # Move configure.log from current directory to the build output root
538   if test -e ./configure.log; then
539     $MV -f ./configure.log &quot;$OUTPUTDIR/configure.log&quot; 2&gt; /dev/null
540   fi
541 
542   # Make the compare script executable
543   $CHMOD +x $OUTPUTDIR/compare.sh
544 ])
    </pre>
  </body>
</html>