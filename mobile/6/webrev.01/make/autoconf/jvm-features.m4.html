<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <title>New make/autoconf/jvm-features.m4</title>
    <link rel="stylesheet" href="../../style.css" />
  </head>
  <body>
    <pre>
  1 #
  2 # Copyright (c) 2011, 2020, Oracle and/or its affiliates. All rights reserved.
  3 # DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.
  4 #
  5 # This code is free software; you can redistribute it and/or modify it
  6 # under the terms of the GNU General Public License version 2 only, as
  7 # published by the Free Software Foundation.  Oracle designates this
  8 # particular file as subject to the &quot;Classpath&quot; exception as provided
  9 # by Oracle in the LICENSE file that accompanied this code.
 10 #
 11 # This code is distributed in the hope that it will be useful, but WITHOUT
 12 # ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 13 # FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
 14 # version 2 for more details (a copy is included in the LICENSE file that
 15 # accompanied this code).
 16 #
 17 # You should have received a copy of the GNU General Public License version
 18 # 2 along with this work; if not, write to the Free Software Foundation,
 19 # Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA.
 20 #
 21 # Please contact Oracle, 500 Oracle Parkway, Redwood Shores, CA 94065 USA
 22 # or visit www.oracle.com if you need additional information or have any
 23 # questions.
 24 #
 25 
 26 ###############################################################################
 27 # Terminology used in this file:
 28 #
 29 # Valid features      == All possible features that the JVM knows about.
 30 # Deprecated features == Previously known features (not considered valid).
 31 # Available features  == Features that are possible to use in this configuration.
 32 # Default features    == Features that are on by default in this configuration.
 33 # Enabled features    == Features requested by the user to be present.
 34 # Disabled features   == Features excluded from being used by the user.
 35 # Active features     == The exact set of features to be used for a JVM variant.
 36 #
 37 # All valid features are considered available, unless listed as unavailable.
 38 # All available features will be turned on as default, unless listed in a filter.
 39 ###############################################################################
 40 
 41 # We need these as m4 defines to be able to loop over them using m4 later on.
 42 
 43 # All valid JVM features, regardless of platform
 44 m4_define(jvm_features_valid, m4_normalize( \
 45     ifdef([custom_jvm_features_valid], custom_jvm_features_valid) \
 46     \
 47     aot cds compiler1 compiler2 dtrace epsilongc g1gc graal jfr jni-check \
 48     jvmci jvmti link-time-opt management minimal nmt opt-size parallelgc \
 49     serialgc services shenandoahgc static-build vm-structs zero zgc \
 50 ))
 51 
 52 # Deprecated JVM features (these are ignored, but with a warning)
 53 m4_define(jvm_features_deprecated, m4_normalize(
 54     cmsgc trace \
 55 ))
 56 
 57 # Feature descriptions
 58 m4_define(jvm_feature_desc_aot, [enable ahead of time compilation (AOT)])
 59 m4_define(jvm_feature_desc_cds, [enable class data sharing (CDS)])
 60 m4_define(jvm_feature_desc_compiler1, [enable hotspot compiler C1])
 61 m4_define(jvm_feature_desc_compiler2, [enable hotspot compiler C2])
 62 m4_define(jvm_feature_desc_dtrace, [enable dtrace support])
 63 m4_define(jvm_feature_desc_epsilongc, [include the epsilon (no-op) garbage collector])
 64 m4_define(jvm_feature_desc_g1gc, [include the G1 garbage collector])
 65 m4_define(jvm_feature_desc_graal, [enable Graal (jdk.internal.vm.compiler)])
 66 m4_define(jvm_feature_desc_jfr, [enable JDK Flight Recorder (JFR)])
 67 m4_define(jvm_feature_desc_jni_check, [enable -Xcheck:jni support])
 68 m4_define(jvm_feature_desc_jvmci, [enable JVM Compiler Interface (JVMCI)])
 69 m4_define(jvm_feature_desc_jvmti, [enable Java Virtual Machine Tool Interface (JVM TI)])
 70 m4_define(jvm_feature_desc_link_time_opt, [enable link time optimization])
 71 m4_define(jvm_feature_desc_management, [enable java.lang.management API support])
 72 m4_define(jvm_feature_desc_minimal, [support building variant &#39;minimal&#39;])
 73 m4_define(jvm_feature_desc_nmt, [include native memory tracking (NMT)])
 74 m4_define(jvm_feature_desc_opt_size, [optimize the JVM library for size])
 75 m4_define(jvm_feature_desc_parallelgc, [include the parallel garbage collector])
 76 m4_define(jvm_feature_desc_serialgc, [include the serial garbage collector])
 77 m4_define(jvm_feature_desc_services, [enable diagnostic services and client attaching])
 78 m4_define(jvm_feature_desc_shenandoahgc, [include the Shenandoah garbage collector])
 79 m4_define(jvm_feature_desc_static_build, [build static library instead of dynamic])
 80 m4_define(jvm_feature_desc_vm_structs, [export JVM structures to the Serviceablility Agent])
 81 m4_define(jvm_feature_desc_zero, [support building variant &#39;zero&#39;])
 82 m4_define(jvm_feature_desc_zgc, [include the Z garbage collector])
 83 
 84 ###############################################################################
 85 # Parse command line options for JVM feature selection. After this function
 86 # has run $JVM_FEATURES_ENABLED, $JVM_FEATURES_DISABLED and $JVM_FEATURES_VALID
 87 # can be used.
 88 #
 89 AC_DEFUN_ONCE([JVM_FEATURES_PARSE_OPTIONS],
 90 [
 91   # Setup shell variables from the m4 lists
 92   UTIL_SORT_LIST(JVM_FEATURES_VALID, &quot;jvm_features_valid&quot;)
 93   UTIL_SORT_LIST(JVM_FEATURES_DEPRECATED, &quot;jvm_features_deprecated&quot;)
 94 
 95   # For historical reasons, some jvm features have their own, shorter names.
 96   # Keep those as aliases for the --enable-jvm-feature-* style arguments.
 97   UTIL_ALIASED_ARG_ENABLE(aot, --enable-jvm-feature-aot)
 98   UTIL_ALIASED_ARG_ENABLE(cds, --enable-jvm-feature-cds)
 99   UTIL_ALIASED_ARG_ENABLE(dtrace, --enable-jvm-feature-dtrace)
100 
101   # First check for features using the
102   # --with-jvm-features=&quot;&lt;[-]feature&gt;[,&lt;[-]feature&gt; ...]&quot; syntax.
103   AC_ARG_WITH([jvm-features], [AS_HELP_STRING([--with-jvm-features],
104       [JVM features to enable (foo) or disable (-foo), separated by comma. Use
105       &#39;--help&#39; to show possible values @&lt;:@none@:&gt;@])])
106   if test &quot;x$with_jvm_features&quot; != x; then
107     # Replace &quot;,&quot;  with &quot; &quot;.
108     user_jvm_feature_list=${with_jvm_features//,/ }
109     JVM_FEATURES_ENABLED=`$ECHO $user_jvm_feature_list | \
110         $AWK &#39;{ for (i=1; i&lt;=NF; i++) if (!match($i, /^-.*/)) printf(&quot;%s &quot;, $i) }&#39;`
111     JVM_FEATURES_DISABLED=`$ECHO $user_jvm_feature_list | \
112         $AWK &#39;{ for (i=1; i&lt;=NF; i++) if (match($i, /^-.*/)) printf(&quot;%s &quot;, substr($i, 2))}&#39;`
113 
114     # Verify that the user has provided only valid (or deprecated) features
115     UTIL_GET_NON_MATCHING_VALUES(invalid_features, $JVM_FEATURES_ENABLED \
116         $JVM_FEATURES_DISABLED, $JVM_FEATURES_VALID $JVM_FEATURES_DEPRECATED)
117     if test &quot;x$invalid_features&quot; != x; then
118       AC_MSG_NOTICE([Unknown JVM features specified: &#39;$invalid_features&#39;])
119       AC_MSG_NOTICE([The available JVM features are: &#39;$JVM_FEATURES_VALID&#39;])
120       AC_MSG_ERROR([Cannot continue])
121     fi
122 
123     # Check if the user has provided deprecated features
124     UTIL_GET_MATCHING_VALUES(deprecated_features, $JVM_FEATURES_ENABLED \
125         $JVM_FEATURES_DISABLED, $JVM_FEATURES_DEPRECATED)
126     if test &quot;x$deprecated_features&quot; != x; then
127       AC_MSG_WARN([Deprecated JVM features specified (will be ignored): &#39;$deprecated_features&#39;])
128       # Filter out deprecated features
129       UTIL_GET_NON_MATCHING_VALUES(JVM_FEATURES_ENABLED, \
130           $JVM_FEATURES_ENABLED, $deprecated_features)
131       UTIL_GET_NON_MATCHING_VALUES(JVM_FEATURES_DISABLED, \
132           $JVM_FEATURES_DISABLED, $deprecated_features)
133     fi
134   fi
135 
136   # Then check for features using the &quot;--enable-jvm-feature-&lt;feature&gt;&quot; syntax.
137   # Using m4, loop over all features with the variable FEATURE.
138   m4_foreach(FEATURE, m4_split(jvm_features_valid), [
139     # Create an m4 variable containing a shell variable name (like
140     # &quot;enable_jvm_feature_static_build&quot;), and the description.
141     m4_define(FEATURE_SHELL, [enable_jvm_feature_]m4_translit(FEATURE, -, _))
142     m4_define(FEATURE_DESCRIPTION, [jvm_feature_desc_]m4_translit(FEATURE, -, _))
143 
144     AC_ARG_ENABLE(jvm-feature-FEATURE, AS_HELP_STRING(
145         [--enable-jvm-feature-FEATURE], [enable jvm feature &#39;FEATURE&#39; (FEATURE_DESCRIPTION)]))
146 
147     if test &quot;x$FEATURE_SHELL&quot; = xyes; then
148       JVM_FEATURES_ENABLED=&quot;$JVM_FEATURES_ENABLED FEATURE&quot;
149     elif test &quot;x$FEATURE_SHELL&quot; = xno; then
150       JVM_FEATURES_DISABLED=&quot;$JVM_FEATURES_DISABLED FEATURE&quot;
151     elif test &quot;x$FEATURE_SHELL&quot; != x; then
152       AC_MSG_ERROR([Invalid value for --enable-jvm-feature-FEATURE: &#39;$FEATURE_SHELL&#39;])
153     fi
154 
155     m4_undefine([FEATURE_SHELL])
156     m4_undefine([FEATURE_DESCRIPTION])
157   ])
158 
159   # Likewise, check for deprecated arguments.
160   m4_foreach(FEATURE, m4_split(jvm_features_deprecated), [
161     AC_ARG_ENABLE(jvm-feature-FEATURE, AS_HELP_STRING(
162         [--enable-jvm-feature-FEATURE], 
163         [Deprecated. Option is kept for backwards compatibility and is ignored]))
164 
165     m4_define(FEATURE_SHELL, [enable_jvm_feature_]m4_translit(FEATURE, -, _))
166 
167     if test &quot;x$FEATURE_SHELL&quot; != x; then
168       AC_MSG_WARN([Deprecated JVM feature, will be ignored: --enable-jvm-feature-FEATURE])
169     fi
170 
171     m4_undefine([FEATURE_SHELL])
172   ])
173 
174   # Check if the user has both enabled and disabled a feature
175   UTIL_GET_MATCHING_VALUES(enabled_and_disabled, $JVM_FEATURES_ENABLED, \
176       $JVM_FEATURES_DISABLED)
177   if test &quot;x$enabled_and_disabled&quot; != x; then
178     AC_MSG_NOTICE([These feature are both enabled and disabled: &#39;$enabled_and_disabled&#39;])
179     AC_MSG_NOTICE([This can happen if you mix --with-jvm-features and --enable-jvm-feature-*])
180     AC_MSG_NOTICE([The recommendation is to only use --enable-jvm-feature-*])
181     AC_MSG_ERROR([Cannot continue])
182   fi
183 
184   # Clean up lists and announce results to user
185   UTIL_SORT_LIST(JVM_FEATURES_ENABLED, $JVM_FEATURES_ENABLED)
186   AC_MSG_CHECKING([for JVM features enabled by the user])
187   if test &quot;x$JVM_FEATURES_ENABLED&quot; != x; then
188     AC_MSG_RESULT([&#39;$JVM_FEATURES_ENABLED&#39;])
189   else
190     AC_MSG_RESULT([none])
191   fi
192 
193   UTIL_SORT_LIST(JVM_FEATURES_DISABLED, $JVM_FEATURES_DISABLED)
194   AC_MSG_CHECKING([for JVM features disabled by the user])
195   if test &quot;x$JVM_FEATURES_DISABLED&quot; != x; then
196     AC_MSG_RESULT([&#39;$JVM_FEATURES_DISABLED&#39;])
197   else
198     AC_MSG_RESULT([none])
199   fi
200 
201   # Makefiles use VALID_JVM_FEATURES in check-jvm-feature to verify correctness.
202   VALID_JVM_FEATURES=&quot;$JVM_FEATURES_VALID&quot;
203   AC_SUBST(VALID_JVM_FEATURES)
204 ])
205 
206 ###############################################################################
207 # Helper function for the JVM_FEATURES_CHECK_* suite.
208 # The code in the code block should assign &#39;false&#39; to the variable AVAILABLE
209 # if the feature is not available, and this function will handle everything
210 # else that is needed.
211 #
212 # arg 1: The name of the feature to test
213 # arg 2: The code block to execute
214 #
215 AC_DEFUN([JVM_FEATURES_CHECK_AVAILABILITY],
216 [
217   # Assume that feature is available
218   AVAILABLE=true
219 
220   # Execute feature test block
221   $2
222 
223   AC_MSG_CHECKING([if JVM feature &#39;$1&#39; is available])
224   if test &quot;x$AVAILABLE&quot; = &quot;xtrue&quot;; then
225     AC_MSG_RESULT([yes])
226   else
227     AC_MSG_RESULT([no])
228     JVM_FEATURES_PLATFORM_UNAVAILABLE=&quot;$JVM_FEATURES_PLATFORM_UNAVAILABLE $1&quot;
229   fi
230 ])
231 
232 ###############################################################################
233 # Check if the feature &#39;aot&#39; is available on this platform.
234 #
235 AC_DEFUN_ONCE([JVM_FEATURES_CHECK_AOT],
236 [
237   JVM_FEATURES_CHECK_AVAILABILITY(aot, [
238     AC_MSG_CHECKING([if platform is supported by AOT])
239     # AOT is only available where JVMCI is available since it requires JVMCI.
240     if test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xx86_64&quot; || \
241         test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xaarch64&quot;; then
242       AC_MSG_RESULT([yes])
243     else
244       AC_MSG_RESULT([no, $OPENJDK_TARGET_CPU])
245       AVAILABLE=false
246     fi
247 
248     AC_MSG_CHECKING([if AOT source code is present])
249     if test -e &quot;${TOPDIR}/src/jdk.internal.vm.compiler&quot; &amp;&amp; \
250         test -e &quot;${TOPDIR}/src/jdk.aot&quot;; then
251       AC_MSG_RESULT([yes])
252     else
253       AC_MSG_RESULT([no, missing src/jdk.internal.vm.compiler or src/jdk.aot])
254       AVAILABLE=false
255     fi
256   ])
257 ])
258 
259 ###############################################################################
260 # Check if the feature &#39;cds&#39; is available on this platform.
261 #
262 AC_DEFUN_ONCE([JVM_FEATURES_CHECK_CDS],
263 [
264   JVM_FEATURES_CHECK_AVAILABILITY(cds, [
265     AC_MSG_CHECKING([if platform is supported by CDS])
266     if test &quot;x$OPENJDK_TARGET_OS&quot; != xaix; then
267       AC_MSG_RESULT([yes])
268     else
269       AC_MSG_RESULT([no, $OPENJDK_TARGET_OS])
270       AVAILABLE=false
271     fi
272   ])
273 ])
274 
275 ###############################################################################
276 # Check if the feature &#39;dtrace&#39; is available on this platform.
277 #
278 AC_DEFUN_ONCE([JVM_FEATURES_CHECK_DTRACE],
279 [
280   JVM_FEATURES_CHECK_AVAILABILITY(dtrace, [
281     AC_MSG_CHECKING([for dtrace tool])
282     if test &quot;x$DTRACE&quot; != &quot;x&quot; &amp;&amp; test -x &quot;$DTRACE&quot;; then
283       AC_MSG_RESULT([$DTRACE])
284     else
285       AC_MSG_RESULT([no])
286       AVAILABLE=false
287     fi
288 
289     AC_CHECK_HEADERS([sys/sdt.h], [dtrace_headers_ok=true])
290     if test &quot;x$dtrace_headers_ok&quot; != &quot;xtrue&quot;; then
291       HELP_MSG_MISSING_DEPENDENCY([dtrace])
292       AC_MSG_NOTICE([Cannot enable dtrace with missing dependencies. See above.])
293       AVAILABLE=false
294     fi
295   ])
296 ])
297 
298 ###############################################################################
299 # Check if the feature &#39;graal&#39; is available on this platform.
300 #
301 AC_DEFUN_ONCE([JVM_FEATURES_CHECK_GRAAL],
302 [
303   JVM_FEATURES_CHECK_AVAILABILITY(graal, [
304     AC_MSG_CHECKING([if platform is supported by Graal])
305     # Graal is only available where JVMCI is available since it requires JVMCI.
306     if test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xx86_64&quot; || \
307         test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xaarch64&quot; ; then
308       AC_MSG_RESULT([yes])
309     else
310       AC_MSG_RESULT([no, $OPENJDK_TARGET_CPU])
311       AVAILABLE=false
312     fi
313   ])
314 ])
315 
316 ###############################################################################
317 # Check if the feature &#39;jfr&#39; is available on this platform.
318 #
319 AC_DEFUN_ONCE([JVM_FEATURES_CHECK_JFR],
320 [
321   JVM_FEATURES_CHECK_AVAILABILITY(jfr, [
322     AC_MSG_CHECKING([if platform is supported by JFR])
323     if test &quot;x$OPENJDK_TARGET_OS&quot; = xaix; then
324       AC_MSG_RESULT([no, $OPENJDK_TARGET_OS-$OPENJDK_TARGET_CPU])
325       AVAILABLE=false
326     else
327       AC_MSG_RESULT([yes])
328     fi
329   ])
330 ])
331 
332 ###############################################################################
333 # Check if the feature &#39;jvmci&#39; is available on this platform.
334 #
335 AC_DEFUN_ONCE([JVM_FEATURES_CHECK_JVMCI],
336 [
337   JVM_FEATURES_CHECK_AVAILABILITY(jvmci, [
338     AC_MSG_CHECKING([if platform is supported by JVMCI])
339     if test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xx86_64&quot; || \
340         test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xaarch64&quot; ; then
341       AC_MSG_RESULT([yes])
342     else
343       AC_MSG_RESULT([no, $OPENJDK_TARGET_CPU])
344       AVAILABLE=false
345     fi
346   ])
347 ])
348 
349 ###############################################################################
350 # Check if the feature &#39;shenandoahgc&#39; is available on this platform.
351 #
352 AC_DEFUN_ONCE([JVM_FEATURES_CHECK_SHENANDOAHGC],
353 [
354   JVM_FEATURES_CHECK_AVAILABILITY(shenandoahgc, [
355     AC_MSG_CHECKING([if platform is supported by Shenandoah])
356     if test &quot;x$OPENJDK_TARGET_CPU_ARCH&quot; = &quot;xx86&quot; || \
357         test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xaarch64&quot; ; then
358       AC_MSG_RESULT([yes])
359     else
360       AC_MSG_RESULT([no, $OPENJDK_TARGET_CPU])
361       AVAILABLE=false
362     fi
363   ])
364 ])
365 
366 ###############################################################################
367 # Check if the feature &#39;static-build&#39; is available on this platform.
368 #
369 AC_DEFUN_ONCE([JVM_FEATURES_CHECK_STATIC_BUILD],
370 [
371   JVM_FEATURES_CHECK_AVAILABILITY(static-build, [
372     AC_MSG_CHECKING([if static-build is enabled in configure])
373     if test &quot;x$STATIC_BUILD&quot; = &quot;xtrue&quot;; then
374       AC_MSG_RESULT([yes])
375     else
376       AC_MSG_RESULT([no, use --enable-static-build to enable static build.])
377       AVAILABLE=false
378     fi
379   ])
380 ])
381 
382 ###############################################################################
383 # Check if the feature &#39;zgc&#39; is available on this platform.
384 #
385 AC_DEFUN_ONCE([JVM_FEATURES_CHECK_ZGC],
386 [
387   JVM_FEATURES_CHECK_AVAILABILITY(zgc, [
388     AC_MSG_CHECKING([if platform is supported by ZGC])
389     if test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xx86_64&quot;; then
390       if test &quot;x$OPENJDK_TARGET_OS&quot; = &quot;xlinux&quot; || \
391           test &quot;x$OPENJDK_TARGET_OS&quot; = &quot;xwindows&quot; || \
392           test &quot;x$OPENJDK_TARGET_OS&quot; = &quot;xmacosx&quot;; then
393         AC_MSG_RESULT([yes])
394       else
395         AC_MSG_RESULT([no, $OPENJDK_TARGET_OS-$OPENJDK_TARGET_CPU])
396         AVAILABLE=false
397       fi
398     elif test &quot;x$OPENJDK_TARGET_OS-$OPENJDK_TARGET_CPU&quot; = &quot;xlinux-aarch64&quot;; then
399       AC_MSG_RESULT([yes])
400     else
401       AC_MSG_RESULT([no, $OPENJDK_TARGET_OS-$OPENJDK_TARGET_CPU])
402       AVAILABLE=false
403     fi
404 
405     if test &quot;x$OPENJDK_TARGET_OS&quot; = &quot;xwindows&quot;; then
406       AC_MSG_CHECKING([if Windows APIs required for ZGC is present])
407       AC_COMPILE_IFELSE(
408         [AC_LANG_PROGRAM([[#include &lt;windows.h&gt;]],
409           [[struct MEM_EXTENDED_PARAMETER x;]])
410         ],
411         [
412           AC_MSG_RESULT([yes])
413         ],
414         [
415           AC_MSG_RESULT([no, missing required APIs])
416           AVAILABLE=false
417         ]
418       )
419     fi
420   ])
421 ])
422 
423 ###############################################################################
424 # Setup JVM_FEATURES_PLATFORM_UNAVAILABLE and JVM_FEATURES_PLATFORM_FILTER
425 # to contain those features that are unavailable, or should be off by default,
426 # for this platform, regardless of JVM variant.
427 #
428 AC_DEFUN_ONCE([JVM_FEATURES_PREPARE_PLATFORM],
429 [
430   # The checks below should add unavailable features to
431   # JVM_FEATURES_PLATFORM_UNAVAILABLE.
432 
433   JVM_FEATURES_CHECK_AOT
434   JVM_FEATURES_CHECK_CDS
435   JVM_FEATURES_CHECK_DTRACE
436   JVM_FEATURES_CHECK_GRAAL
437   JVM_FEATURES_CHECK_JFR
438   JVM_FEATURES_CHECK_JVMCI
439   JVM_FEATURES_CHECK_SHENANDOAHGC
440   JVM_FEATURES_CHECK_STATIC_BUILD
441   JVM_FEATURES_CHECK_ZGC
442 
443   # Filter out features by default for all variants on certain platforms.
444   # Make sure to just add to JVM_FEATURES_PLATFORM_FILTER, since it could
445   # have a value already from custom extensions.
446   if test &quot;x$OPENJDK_TARGET_OS&quot; = xaix; then
447     JVM_FEATURES_PLATFORM_FILTER=&quot;$JVM_FEATURES_PLATFORM_FILTER jfr&quot;
448   fi
449 ])
450 
451 ###############################################################################
452 # Setup JVM_FEATURES_VARIANT_UNAVAILABLE and JVM_FEATURES_VARIANT_FILTER
453 # to contain those features that are unavailable, or should be off by default,
454 # for this particular JVM variant.
455 #
456 # arg 1: JVM variant
457 #
458 AC_DEFUN([JVM_FEATURES_PREPARE_VARIANT],
459 [
460   variant=$1
461 
462   # Check which features are unavailable for this JVM variant.
463   # This means that is not possible to build these features for this variant.
464   if test &quot;x$variant&quot; = &quot;xminimal&quot;; then
465     JVM_FEATURES_VARIANT_UNAVAILABLE=&quot;cds zero&quot;
466   elif test &quot;x$variant&quot; = &quot;xcore&quot;; then
467     JVM_FEATURES_VARIANT_UNAVAILABLE=&quot;cds minimal zero&quot;
468   elif test &quot;x$variant&quot; = &quot;xzero&quot;; then
469     JVM_FEATURES_VARIANT_UNAVAILABLE=&quot;aot cds compiler1 compiler2 \
470         epsilongc g1gc graal jvmci minimal shenandoahgc zgc&quot;
471   else
472     JVM_FEATURES_VARIANT_UNAVAILABLE=&quot;minimal zero&quot;
473   fi
474 
475   # Check which features should be off by default for this JVM variant.
476   if test &quot;x$variant&quot; = &quot;xclient&quot;; then
477     JVM_FEATURES_VARIANT_FILTER=&quot;aot compiler2 graal jvmci link-time-opt opt-size&quot;
478   elif test &quot;x$variant&quot; = &quot;xminimal&quot;; then
479     JVM_FEATURES_VARIANT_FILTER=&quot;aot cds compiler2 dtrace epsilongc g1gc \
480         graal jfr jni-check jvmci jvmti management nmt parallelgc services \
481         shenandoahgc vm-structs zgc&quot;
482     if test &quot;x$OPENJDK_TARGET_CPU&quot; = xarm ; then
483       JVM_FEATURES_VARIANT_FILTER=&quot;$JVM_FEATURES_VARIANT_FILTER opt-size&quot;
484     else
485       # Only arm-32 should have link-time-opt enabled as default.
486       JVM_FEATURES_VARIANT_FILTER=&quot;$JVM_FEATURES_VARIANT_FILTER \
487           link-time-opt&quot;
488     fi
489   elif test &quot;x$variant&quot; = &quot;xcore&quot;; then
490     JVM_FEATURES_VARIANT_FILTER=&quot;aot compiler1 compiler2 graal jvmci \
491         link-time-opt opt-size&quot;
492   elif test &quot;x$variant&quot; = &quot;xzero&quot;; then
493     JVM_FEATURES_VARIANT_FILTER=&quot;jfr link-time-opt opt-size&quot;
494   else
495     JVM_FEATURES_VARIANT_FILTER=&quot;link-time-opt opt-size&quot;
496   fi
497 ])
498 
499 ###############################################################################
500 # Calculate the actual set of active JVM features for this JVM variant. Store
501 # the result in JVM_FEATURES_ACTIVE.
502 #
503 # arg 1: JVM variant
504 #
505 AC_DEFUN([JVM_FEATURES_CALCULATE_ACTIVE],
506 [
507   variant=$1
508 
509   # The default is set to all valid features except those unavailable or listed
510   # in a filter.
511   if test &quot;x$variant&quot; != xcustom; then
512     UTIL_GET_NON_MATCHING_VALUES(default_for_variant, $JVM_FEATURES_VALID, \
513         $JVM_FEATURES_PLATFORM_UNAVAILABLE $JVM_FEATURES_VARIANT_UNAVAILABLE \
514         $JVM_FEATURES_PLATFORM_FILTER $JVM_FEATURES_VARIANT_FILTER)
515   else
516     # Except for the &#39;custom&#39; variant, where the default is to start with an
517     # empty set.
518     default_for_variant=&quot;&quot;
519   fi
520 
521   # Verify that explicitly enabled features are available
522   UTIL_GET_MATCHING_VALUES(enabled_but_unavailable, $JVM_FEATURES_ENABLED, \
523       $JVM_FEATURES_PLATFORM_UNAVAILABLE $JVM_FEATURES_VARIANT_UNAVAILABLE)
524   if test &quot;x$enabled_but_unavailable&quot; != x; then
525     AC_MSG_NOTICE([ERROR: Unavailable JVM features explicitly enabled for &#39;$variant&#39;: &#39;$enabled_but_unavailable&#39;])
526     AC_MSG_ERROR([Cannot continue])
527   fi
528 
529   # Notify the user if their command line options has no real effect
530   UTIL_GET_MATCHING_VALUES(enabled_but_default, $JVM_FEATURES_ENABLED, \
531       $default_for_variant)
532   if test &quot;x$enabled_but_default&quot; != x; then
533     AC_MSG_NOTICE([Default JVM features explicitly enabled for &#39;$variant&#39;: &#39;$enabled_but_default&#39;])
534   fi
535   UTIL_GET_MATCHING_VALUES(disabled_but_unavailable, $JVM_FEATURES_DISABLED, \
536       $JVM_FEATURES_PLATFORM_UNAVAILABLE $JVM_FEATURES_VARIANT_UNAVAILABLE)
537   if test &quot;x$disabled_but_unavailable&quot; != x; then
538     AC_MSG_NOTICE([Unavailable JVM features explicitly disabled for &#39;$variant&#39;: &#39;$disabled_but_unavailable&#39;])
539   fi
540 
541   # JVM_FEATURES_ACTIVE is the set of all default features and all explicitly
542   # enabled features, with the explicitly disabled features filtered out.
543   UTIL_GET_NON_MATCHING_VALUES(JVM_FEATURES_ACTIVE, $default_for_variant \
544       $JVM_FEATURES_ENABLED, $JVM_FEATURES_DISABLED)
545 ])
546 
547 ###############################################################################
548 # Helper function for JVM_FEATURES_VERIFY. Check if the specified JVM
549 # feature is active. To be used in shell if constructs, like this:
550 # &#39;if JVM_FEATURES_IS_ACTIVE(jvmti); then&#39;
551 #
552 # Definition kept in one line to allow inlining in if statements.
553 # Additional [] needed to keep m4 from mangling shell constructs.
554 AC_DEFUN([JVM_FEATURES_IS_ACTIVE],
555 [ [ [[ &quot; $JVM_FEATURES_ACTIVE &quot; =~ &#39; &#39;$1&#39; &#39; ]] ] ])
556 
557 ###############################################################################
558 # Verify that the resulting set of features is consistent and legal.
559 #
560 # arg 1: JVM variant
561 #
562 AC_DEFUN([JVM_FEATURES_VERIFY],
563 [
564   variant=$1
565 
566   # Verify that dependencies are met for inter-feature relations.
567   if JVM_FEATURES_IS_ACTIVE(aot) &amp;&amp; ! JVM_FEATURES_IS_ACTIVE(graal); then
568     AC_MSG_ERROR([Specified JVM feature &#39;aot&#39; requires feature &#39;graal&#39; for variant &#39;$variant&#39;])
569   fi
570 
571   if JVM_FEATURES_IS_ACTIVE(graal) &amp;&amp; ! JVM_FEATURES_IS_ACTIVE(jvmci); then
572     AC_MSG_ERROR([Specified JVM feature &#39;graal&#39; requires feature &#39;jvmci&#39; for variant &#39;$variant&#39;])
573   fi
574 
575   if JVM_FEATURES_IS_ACTIVE(jvmci) &amp;&amp; ! (JVM_FEATURES_IS_ACTIVE(compiler1) || \
576       JVM_FEATURES_IS_ACTIVE(compiler2)); then
577     AC_MSG_ERROR([Specified JVM feature &#39;jvmci&#39; requires feature &#39;compiler2&#39; or &#39;compiler1&#39; for variant &#39;$variant&#39;])
578   fi
579 
580   if JVM_FEATURES_IS_ACTIVE(jvmti) &amp;&amp; ! JVM_FEATURES_IS_ACTIVE(services); then
581     AC_MSG_ERROR([Specified JVM feature &#39;jvmti&#39; requires feature &#39;services&#39; for variant &#39;$variant&#39;])
582   fi
583 
584   if JVM_FEATURES_IS_ACTIVE(management) &amp;&amp; ! JVM_FEATURES_IS_ACTIVE(nmt); then
585     AC_MSG_ERROR([Specified JVM feature &#39;management&#39; requires feature &#39;nmt&#39; for variant &#39;$variant&#39;])
586   fi
587 
588   # For backwards compatibility, disable a feature &quot;globally&quot; if one variant
589   # is missing the feature.
590   if ! JVM_FEATURES_IS_ACTIVE(aot); then
591     ENABLE_AOT=&quot;false&quot;
592   fi
593   if ! JVM_FEATURES_IS_ACTIVE(cds); then
594     ENABLE_CDS=&quot;false&quot;
595   fi
596   if ! JVM_FEATURES_IS_ACTIVE(graal); then
597     INCLUDE_GRAAL=&quot;false&quot;
598   fi
599   if ! JVM_FEATURES_IS_ACTIVE(jvmci); then
600     INCLUDE_JVMCI=&quot;false&quot;
601   fi
602 
603   # Verify that we have at least one gc selected (i.e., feature named &quot;*gc&quot;).
604   if ! JVM_FEATURES_IS_ACTIVE(.*gc); then
605       AC_MSG_NOTICE([At least one gc needed for variant &#39;$variant&#39;.])
606       AC_MSG_NOTICE([Specified features: &#39;$JVM_FEATURES_ACTIVE&#39;])
607       AC_MSG_ERROR([Cannot continue])
608   fi
609 ])
610 
611 ###############################################################################
612 # Set up all JVM features for each enabled JVM variant. Requires that
613 # JVM_FEATURES_PARSE_OPTIONS has been called.
614 #
615 AC_DEFUN_ONCE([JVM_FEATURES_SETUP],
616 [
617   # Set up variant-independent factors
618   JVM_FEATURES_PREPARE_PLATFORM
619 
620   # For backwards compatibility, tentatively enable these features &quot;globally&quot;,
621   # and disable them in JVM_FEATURES_VERIFY if a variant is found that are
622   # missing any of them.
623   ENABLE_AOT=&quot;true&quot;
624   ENABLE_CDS=&quot;true&quot;
625   INCLUDE_GRAAL=&quot;true&quot;
626   INCLUDE_JVMCI=&quot;true&quot;
627 
628   for variant in $JVM_VARIANTS; do
629     # Figure out if any features are unavailable, or should be filtered out
630     # by default, for this variant.
631     # Store the result in JVM_FEATURES_VARIANT_UNAVAILABLE and
632     # JVM_FEATURES_VARIANT_FILTER.
633     JVM_FEATURES_PREPARE_VARIANT($variant)
634 
635     # Calculate the resulting set of enabled features for this variant.
636     # The result is stored in JVM_FEATURES_ACTIVE.
637     JVM_FEATURES_CALCULATE_ACTIVE($variant)
638 
639     # Verify consistency for JVM_FEATURES_ACTIVE.
640     JVM_FEATURES_VERIFY($variant)
641 
642     # Keep feature list sorted and free of duplicates
643     UTIL_SORT_LIST(JVM_FEATURES_ACTIVE, $JVM_FEATURES_ACTIVE)
644     AC_MSG_CHECKING([JVM features to use for variant &#39;$variant&#39;])
645     AC_MSG_RESULT([&#39;$JVM_FEATURES_ACTIVE&#39;])
646 
647     # Save this as e.g. JVM_FEATURES_server, using indirect variable
648     # referencing.
649     features_var_name=JVM_FEATURES_$variant
650     eval $features_var_name=\&quot;$JVM_FEATURES_ACTIVE\&quot;
651   done
652 
653   # Unfortunately AC_SUBST does not work with non-literally named variables,
654   # so list all variants here.
655   AC_SUBST(JVM_FEATURES_server)
656   AC_SUBST(JVM_FEATURES_client)
657   AC_SUBST(JVM_FEATURES_minimal)
658   AC_SUBST(JVM_FEATURES_core)
659   AC_SUBST(JVM_FEATURES_zero)
660   AC_SUBST(JVM_FEATURES_custom)
661 
662   AC_SUBST(ENABLE_AOT)
663   AC_SUBST(INCLUDE_GRAAL)
664   AC_SUBST(INCLUDE_JVMCI)
665 
666 ])
    </pre>
  </body>
</html>