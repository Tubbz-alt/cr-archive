<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <title>Frames make/autoconf/flags.m4</title>
    <link rel="stylesheet" href="../../style.css" />
    <script type="text/javascript" src="../../navigation.js"></script>
  </head>
<body onkeypress="keypress(event);">
<a name="0"></a>
<hr />
<pre>  1 #
  2 # Copyright (c) 2011, 2020, Oracle and/or its affiliates. All rights reserved.
  3 # DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.
  4 #
  5 # This code is free software; you can redistribute it and/or modify it
  6 # under the terms of the GNU General Public License version 2 only, as
  7 # published by the Free Software Foundation.  Oracle designates this
  8 # particular file as subject to the &quot;Classpath&quot; exception as provided
  9 # by Oracle in the LICENSE file that accompanied this code.
 10 #
 11 # This code is distributed in the hope that it will be useful, but WITHOUT
 12 # ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 13 # FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
 14 # version 2 for more details (a copy is included in the LICENSE file that
 15 # accompanied this code).
 16 #
 17 # You should have received a copy of the GNU General Public License version
 18 # 2 along with this work; if not, write to the Free Software Foundation,
 19 # Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA.
 20 #
 21 # Please contact Oracle, 500 Oracle Parkway, Redwood Shores, CA 94065 USA
 22 # or visit www.oracle.com if you need additional information or have any
 23 # questions.
 24 #
 25 
 26 m4_include([flags-cflags.m4])
 27 m4_include([flags-ldflags.m4])
 28 m4_include([flags-other.m4])
 29 
 30 ################################################################################
 31 #
 32 # Setup ABI profile (for arm)
 33 #
 34 AC_DEFUN([FLAGS_SETUP_ABI_PROFILE],
 35 [
 36   AC_ARG_WITH(abi-profile, [AS_HELP_STRING([--with-abi-profile],
 37       [specify ABI profile for ARM builds (arm-vfp-sflt,arm-vfp-hflt,arm-sflt, armv5-vfp-sflt,armv6-vfp-hflt,aarch64) @&lt;:@toolchain dependent@:&gt;@ ])])
 38 
 39   if test &quot;x$with_abi_profile&quot; != x; then
 40     if test &quot;x$OPENJDK_TARGET_CPU&quot; != xarm &amp;&amp; \
 41         test &quot;x$OPENJDK_TARGET_CPU&quot; != xaarch64; then
 42       AC_MSG_ERROR([--with-abi-profile only available on arm/aarch64])
 43     fi
 44 
 45     OPENJDK_TARGET_ABI_PROFILE=$with_abi_profile
 46     AC_MSG_CHECKING([for ABI profle])
 47     AC_MSG_RESULT([$OPENJDK_TARGET_ABI_PROFILE])
 48 
 49     # --- Arm-sflt CFLAGS and ASFLAGS ---
 50     # Armv5te is required for assembler, because pld insn used in arm32 hotspot is only in v5E and above.
 51     # However, there is also a GCC bug which generates unaligned strd/ldrd instructions on armv5te:
 52     # https://gcc.gnu.org/bugzilla/show_bug.cgi?id=82445, and it was fixed in gcc 7.1.
 53     # The resulting compromise is to enable v5TE for assembler and let GCC generate code for v5T.
 54     if test &quot;x$OPENJDK_TARGET_ABI_PROFILE&quot; = xarm-vfp-sflt; then
 55       ARM_FLOAT_TYPE=vfp-sflt
 56       ARM_ARCH_TYPE_FLAGS=&#39;-march=armv7-a -mthumb&#39;
 57       ARM_ARCH_TYPE_ASFLAGS=&#39;-march=armv7-a -mthumb&#39;
 58     elif test &quot;x$OPENJDK_TARGET_ABI_PROFILE&quot; = xarm-vfp-hflt; then
 59       ARM_FLOAT_TYPE=vfp-hflt
 60       ARM_ARCH_TYPE_FLAGS=&#39;-march=armv7-a -mthumb&#39;
 61       ARM_ARCH_TYPE_ASFLAGS=&#39;-march=armv7-a -mthumb&#39;
 62     elif test &quot;x$OPENJDK_TARGET_ABI_PROFILE&quot; = xarm-sflt; then
 63       ARM_FLOAT_TYPE=sflt
 64       ARM_ARCH_TYPE_FLAGS=&#39;-march=armv5t -marm&#39;
 65       ARM_ARCH_TYPE_ASFLAGS=&#39;-march=armv5te&#39;
 66     elif test &quot;x$OPENJDK_TARGET_ABI_PROFILE&quot; = xarmv5-vfp-sflt; then
 67       ARM_FLOAT_TYPE=vfp-sflt
 68       ARM_ARCH_TYPE_FLAGS=&#39;-march=armv5t -marm&#39;
 69       ARM_ARCH_TYPE_ASFLAGS=&#39;-march=armv5te&#39;
 70     elif test &quot;x$OPENJDK_TARGET_ABI_PROFILE&quot; = xarmv6-vfp-hflt; then
 71       ARM_FLOAT_TYPE=vfp-hflt
 72       ARM_ARCH_TYPE_FLAGS=&#39;-march=armv6 -marm&#39;
 73       ARM_ARCH_TYPE_ASFLAGS=&#39;-march=armv6&#39;
 74     elif test &quot;x$OPENJDK_TARGET_ABI_PROFILE&quot; = xaarch64; then
 75       # No special flags, just need to trigger setting JDK_ARCH_ABI_PROP_NAME
 76       ARM_FLOAT_TYPE=
 77       ARM_ARCH_TYPE_FLAGS=
 78       ARM_ARCH_TYPE_ASFLAGS=
 79     else
 80       AC_MSG_ERROR([Invalid ABI profile: &quot;$OPENJDK_TARGET_ABI_PROFILE&quot;])
 81     fi
 82 
 83     if test &quot;x$ARM_FLOAT_TYPE&quot; = xvfp-sflt; then
 84       ARM_FLOAT_TYPE_FLAGS=&#39;-mfloat-abi=softfp -mfpu=vfp -DFLOAT_ARCH=-vfp-sflt&#39;
 85       ARM_FLOAT_TYPE_ASFLAGS=&quot;-mfloat-abi=softfp -mfpu=vfp&quot;
 86     elif test &quot;x$ARM_FLOAT_TYPE&quot; = xvfp-hflt; then
 87       ARM_FLOAT_TYPE_FLAGS=&#39;-mfloat-abi=hard -mfpu=vfp -DFLOAT_ARCH=-vfp-hflt&#39;
 88       ARM_FLOAT_TYPE_ASFLAGS=&quot;-mfloat-abi=hard -mfpu=vfp&quot;
 89     elif test &quot;x$ARM_FLOAT_TYPE&quot; = xsflt; then
 90       ARM_FLOAT_TYPE_FLAGS=&#39;-msoft-float -mfpu=vfp&#39;
 91       ARM_FLOAT_TYPE_ASFLAGS=&quot;-mfloat-abi=soft -mfpu=vfp&quot;
 92     fi
 93     AC_MSG_CHECKING([for $ARM_FLOAT_TYPE floating point flags])
 94     AC_MSG_RESULT([$ARM_FLOAT_TYPE_FLAGS])
 95     AC_MSG_CHECKING([for $ARM_FLOAT_TYPE floating point flags for assembler])
 96     AC_MSG_RESULT([$ARM_FLOAT_TYPE_ASFLAGS])
 97 
 98     AC_MSG_CHECKING([for arch type flags])
 99     AC_MSG_RESULT([$ARM_ARCH_TYPE_FLAGS])
100     AC_MSG_CHECKING([for arch type flags for assembler])
101     AC_MSG_RESULT([$ARM_ARCH_TYPE_ASFLAGS])
102 
103     # Now set JDK_ARCH_ABI_PROP_NAME. This is equivalent to the last part of the
104     # autoconf target triplet.
105     [ JDK_ARCH_ABI_PROP_NAME=`$ECHO $OPENJDK_TARGET_AUTOCONF_NAME | $SED -e &#39;s/.*-\([^-]*\)$/\1/&#39;` ]
106     # Sanity check that it is a known ABI.
107     if test &quot;x$JDK_ARCH_ABI_PROP_NAME&quot; != xgnu &amp;&amp; \
108         test &quot;x$JDK_ARCH_ABI_PROP_NAME&quot; != xgnueabi  &amp;&amp; \
109         test &quot;x$JDK_ARCH_ABI_PROP_NAME&quot; != xgnueabihf; then
110           AC_MSG_WARN([Unknown autoconf target triplet ABI: &quot;$JDK_ARCH_ABI_PROP_NAME&quot;])
111     fi
112     AC_MSG_CHECKING([for ABI property name])
113     AC_MSG_RESULT([$JDK_ARCH_ABI_PROP_NAME])
114     AC_SUBST(JDK_ARCH_ABI_PROP_NAME)
115   fi
116 
117   # When building with an abi profile, the name of that profile is appended on the
118   # bundle platform, which is used in bundle names.
119   if test &quot;x$OPENJDK_TARGET_ABI_PROFILE&quot; != x; then
120     OPENJDK_TARGET_BUNDLE_PLATFORM=&quot;$OPENJDK_TARGET_OS_BUNDLE-$OPENJDK_TARGET_ABI_PROFILE&quot;
121   fi
122 ])
123 
124 AC_DEFUN([FLAGS_SETUP_MACOSX_VERSION],
125 [
126   # Additional macosx handling
127   if test &quot;x$OPENJDK_TARGET_OS&quot; = xmacosx; then
128     # MACOSX_VERSION_MIN specifies the lowest version of Macosx that the built
129     # binaries should be compatible with, even if compiled on a newer version
130     # of the OS. It currently has a hard coded value. Setting this also limits
131     # exposure to API changes in header files. Bumping this is likely to
132     # require code changes to build.
133     MACOSX_VERSION_MIN=10.9.0
134     MACOSX_VERSION_MIN_NODOTS=${MACOSX_VERSION_MIN//\./}
135 
136     AC_SUBST(MACOSX_VERSION_MIN)
137 
138     # Setting --with-macosx-version-max=&lt;version&gt; makes it an error to build or
139     # link to macosx APIs that are newer than the given OS version. The expected
140     # format for &lt;version&gt; is either nn.n.n or nn.nn.nn. See /usr/include/AvailabilityMacros.h.
141     AC_ARG_WITH([macosx-version-max], [AS_HELP_STRING([--with-macosx-version-max],
142         [error on use of newer functionality. @&lt;:@macosx@:&gt;@])],
143         [
144           if echo &quot;$with_macosx_version_max&quot; | $GREP -q &quot;^[[0-9]][[0-9]]\.[[0-9]]\.[[0-9]]\$&quot;; then
145               MACOSX_VERSION_MAX=$with_macosx_version_max
146           elif echo &quot;$with_macosx_version_max&quot; | $GREP -q &quot;^[[0-9]][[0-9]]\.[[0-9]][[0-9]]\.[[0-9]][[0-9]]\$&quot;; then
147               MACOSX_VERSION_MAX=$with_macosx_version_max
148           elif test &quot;x$with_macosx_version_max&quot; = &quot;xno&quot;; then
149               # Use build system default
150               MACOSX_VERSION_MAX=
151           else
152               AC_MSG_ERROR([osx version format must be nn.n.n or nn.nn.nn])
153           fi
154         ],
155         [MACOSX_VERSION_MAX=]
156     )
157     MACOSX_VERSION_MAX_NODOTS=${MACOSX_VERSION_MAX//\./}
158 
159     AC_SUBST(MACOSX_VERSION_MAX)
160   fi
161 ])
162 
163 # Reset the global CFLAGS/LDFLAGS variables and initialize them with the
164 # corresponding configure arguments instead
165 AC_DEFUN_ONCE([FLAGS_SETUP_USER_SUPPLIED_FLAGS],
166 [
167   if test &quot;x$CFLAGS&quot; != &quot;x&quot;; then
168     AC_MSG_WARN([Ignoring CFLAGS($CFLAGS) found in environment. Use --with-extra-cflags])
169   fi
170 
171   if test &quot;x$CXXFLAGS&quot; != &quot;x&quot;; then
172     AC_MSG_WARN([Ignoring CXXFLAGS($CXXFLAGS) found in environment. Use --with-extra-cxxflags])
173   fi
174 
175   if test &quot;x$LDFLAGS&quot; != &quot;x&quot;; then
176     AC_MSG_WARN([Ignoring LDFLAGS($LDFLAGS) found in environment. Use --with-extra-ldflags])
177   fi
178 
179   if test &quot;x$ASFLAGS&quot; != &quot;x&quot;; then
180     AC_MSG_WARN([Ignoring ASFLAGS($ASFLAGS) found in environment. Use --with-extra-asflags])
181   fi
182 
183   AC_ARG_WITH(extra-cflags, [AS_HELP_STRING([--with-extra-cflags],
184       [extra flags to be used when compiling jdk c-files])])
185 
186   AC_ARG_WITH(extra-cxxflags, [AS_HELP_STRING([--with-extra-cxxflags],
187       [extra flags to be used when compiling jdk c++-files])])
188 
189   AC_ARG_WITH(extra-ldflags, [AS_HELP_STRING([--with-extra-ldflags],
190       [extra flags to be used when linking jdk])])
191 
192   AC_ARG_WITH(extra-asflags, [AS_HELP_STRING([--with-extra-asflags],
193       [extra flags to be passed to the assembler])])
194 
195   USER_CFLAGS=&quot;$with_extra_cflags&quot;
196   USER_CXXFLAGS=&quot;$with_extra_cxxflags&quot;
197   USER_LDFLAGS=&quot;$with_extra_ldflags&quot;
198   USER_ASFLAGS=&quot;$with_extra_asflags&quot;
199 ])
200 
201 # Setup the sysroot flags and add them to global CFLAGS and LDFLAGS so
202 # that configure can use them while detecting compilers.
203 # TOOLCHAIN_TYPE is available here.
204 # Param 1 - Optional prefix to all variables. (e.g BUILD_)
205 AC_DEFUN([FLAGS_SETUP_SYSROOT_FLAGS],
206 [
207   if test &quot;x[$]$1SYSROOT&quot; != &quot;x&quot;; then
208     if test &quot;x$TOOLCHAIN_TYPE&quot; = xsolstudio; then
209       if test &quot;x$OPENJDK_TARGET_OS&quot; = xsolaris; then
210         # Solaris Studio does not have a concept of sysroot. Instead we must
211         # make sure the default include and lib dirs are appended to each
212         # compile and link command line. Must also add -I-xbuiltin to enable
213         # inlining of system functions and intrinsics.
214         $1SYSROOT_CFLAGS=&quot;-I-xbuiltin -I[$]$1SYSROOT/usr/include&quot;
215         $1SYSROOT_LDFLAGS=&quot;-L[$]$1SYSROOT/usr/lib$OPENJDK_TARGET_CPU_ISADIR \
216             -L[$]$1SYSROOT/lib$OPENJDK_TARGET_CPU_ISADIR&quot;
217         # If the devkit contains the ld linker, make sure we use it.
218         AC_PATH_PROG(SOLARIS_LD, ld, , $DEVKIT_TOOLCHAIN_PATH:$DEVKIT_EXTRA_PATH)
219         # Make sure this ld is runnable.
220         if test -f &quot;$SOLARIS_LD&quot;; then
221           if &quot;$SOLARIS_LD&quot; -V &gt; /dev/null 2&gt; /dev/null; then
222             $1SYSROOT_LDFLAGS=&quot;[$]$1SYSROOT_LDFLAGS -Yl,$(dirname $SOLARIS_LD)&quot;
223           else
224             AC_MSG_WARN([Could not run $SOLARIS_LD found in devkit, reverting to system ld])
225           fi
226         fi
227       fi
228     elif test &quot;x$TOOLCHAIN_TYPE&quot; = xgcc; then
229       $1SYSROOT_CFLAGS=&quot;--sysroot=[$]$1SYSROOT&quot;
230       $1SYSROOT_LDFLAGS=&quot;--sysroot=[$]$1SYSROOT&quot;
231     elif test &quot;x$TOOLCHAIN_TYPE&quot; = xclang; then
232       $1SYSROOT_CFLAGS=&quot;-isysroot [$]$1SYSROOT&quot;
233       $1SYSROOT_LDFLAGS=&quot;-isysroot [$]$1SYSROOT&quot;
234     fi
235   fi
236 
237   if test &quot;x$OPENJDK_TARGET_OS&quot; = xmacosx; then
238     # We also need -iframework&lt;path&gt;/System/Library/Frameworks
239     $1SYSROOT_CFLAGS=&quot;[$]$1SYSROOT_CFLAGS -iframework [$]$1SYSROOT/System/Library/Frameworks&quot;
240     $1SYSROOT_LDFLAGS=&quot;[$]$1SYSROOT_LDFLAGS -iframework [$]$1SYSROOT/System/Library/Frameworks&quot;
241     # These always need to be set, or we can&#39;t find the frameworks embedded in JavaVM.framework
242     # set this here so it doesn&#39;t have to be peppered throughout the forest
243     $1SYSROOT_CFLAGS=&quot;[$]$1SYSROOT_CFLAGS -F [$]$1SYSROOT/System/Library/Frameworks/JavaVM.framework/Frameworks&quot;
244     $1SYSROOT_LDFLAGS=&quot;[$]$1SYSROOT_LDFLAGS -F [$]$1SYSROOT/System/Library/Frameworks/JavaVM.framework/Frameworks&quot;
245   fi
246 
247   AC_SUBST($1SYSROOT_CFLAGS)
248   AC_SUBST($1SYSROOT_LDFLAGS)
249 ])
250 
251 AC_DEFUN_ONCE([FLAGS_PRE_TOOLCHAIN],
252 [
253   # We should always include user supplied flags
254   FLAGS_SETUP_USER_SUPPLIED_FLAGS
255   # The sysroot flags are needed for configure to be able to run the compilers
256   FLAGS_SETUP_SYSROOT_FLAGS
257 
258   # For solstudio and xlc, the word size flag is required for correct behavior.
259   # For clang/gcc, the flag is only strictly required for reduced builds, but
260   # set it always where possible (x86, sparc and ppc).
261   if test &quot;x$TOOLCHAIN_TYPE&quot; = xxlc; then
262     MACHINE_FLAG=&quot;-q${OPENJDK_TARGET_CPU_BITS}&quot;
263   elif test &quot;x$TOOLCHAIN_TYPE&quot; = xsolstudio; then
264     MACHINE_FLAG=&quot;-m${OPENJDK_TARGET_CPU_BITS}&quot;
265   elif test &quot;x$TOOLCHAIN_TYPE&quot; = xgcc || test &quot;x$TOOLCHAIN_TYPE&quot; = xclang; then
266     if test &quot;x$OPENJDK_TARGET_CPU_ARCH&quot; = xx86 &amp;&amp;
267         test &quot;x$OPENJDK_TARGET_CPU&quot; != xx32 ||
268         test &quot;x$OPENJDK_TARGET_CPU_ARCH&quot; = xsparc ||
269         test &quot;x$OPENJDK_TARGET_CPU_ARCH&quot; = xppc; then
270       MACHINE_FLAG=&quot;-m${OPENJDK_TARGET_CPU_BITS}&quot;
271     fi
272   fi
273 
274   # FIXME: global flags are not used yet...
275   # The &quot;global&quot; flags will *always* be set. Without them, it is not possible to
276   # get a working compilation.
277   GLOBAL_CFLAGS=&quot;$MACHINE_FLAG $SYSROOT_CFLAGS $USER_CFLAGS&quot;
278   GLOBAL_CXXFLAGS=&quot;$MACHINE_FLAG $SYSROOT_CFLAGS $USER_CXXFLAGS&quot;
279   GLOBAL_LDFLAGS=&quot;$MACHINE_FLAG $SYSROOT_LDFLAGS $USER_LDFLAGS&quot;
280   # FIXME: Don&#39;t really know how to do with this, but this was the old behavior
<a name="1" id="anc1"></a><span class="line-modified">281   if test &quot;x$OPENJDK_TARGET_OS&quot; = xios; then</span>
<span class="line-added">282     GLOBAL_CPPFLAGS=&quot;$MACHINE_FLAG $USER_CFLAGS $SYSROOT_CFLAGS&quot;</span>
<span class="line-added">283   else</span>
<span class="line-added">284     GLOBAL_CPPFLAGS=&quot;$SYSROOT_CFLAGS&quot;</span>
<span class="line-added">285   fi</span>
286   AC_SUBST(GLOBAL_CFLAGS)
287   AC_SUBST(GLOBAL_CXXFLAGS)
288   AC_SUBST(GLOBAL_LDFLAGS)
289   AC_SUBST(GLOBAL_CPPFLAGS)
290 
291   # FIXME: For compatilibity, export this as EXTRA_CFLAGS for now.
292   EXTRA_CFLAGS=&quot;$MACHINE_FLAG $USER_CFLAGS&quot;
293   EXTRA_CXXFLAGS=&quot;$MACHINE_FLAG $USER_CXXFLAGS&quot;
294   EXTRA_LDFLAGS=&quot;$MACHINE_FLAG $USER_LDFLAGS&quot;
295   EXTRA_ASFLAGS=&quot;$USER_ASFLAGS&quot;
296 
297   AC_SUBST(EXTRA_CFLAGS)
298   AC_SUBST(EXTRA_CXXFLAGS)
299   AC_SUBST(EXTRA_LDFLAGS)
300   AC_SUBST(EXTRA_ASFLAGS)
301 
302   # For autoconf testing to work, the global flags must also be stored in the
303   # &quot;unnamed&quot; CFLAGS etc.
304   CFLAGS=&quot;$GLOBAL_CFLAGS&quot;
305   CXXFLAGS=&quot;$GLOBAL_CXXFLAGS&quot;
306   LDFLAGS=&quot;$GLOBAL_LDFLAGS&quot;
307   CPPFLAGS=&quot;$GLOBAL_CPPFLAGS&quot;
308 ])
309 
310 AC_DEFUN([FLAGS_SETUP_TOOLCHAIN_CONTROL],
311 [
312   # COMPILER_TARGET_BITS_FLAG  : option for selecting 32- or 64-bit output
313   # COMPILER_COMMAND_FILE_FLAG : option for passing a command file to the compiler
314   # COMPILER_BINDCMD_FILE_FLAG : option for specifying a file which saves the binder
315   #                              commands produced by the link step (currently AIX only)
316   if test &quot;x$TOOLCHAIN_TYPE&quot; = xxlc; then
317     COMPILER_TARGET_BITS_FLAG=&quot;-q&quot;
318     COMPILER_COMMAND_FILE_FLAG=&quot;-f&quot;
319     COMPILER_BINDCMD_FILE_FLAG=&quot;-bloadmap:&quot;
320   else
321     COMPILER_TARGET_BITS_FLAG=&quot;-m&quot;
322     COMPILER_COMMAND_FILE_FLAG=&quot;@&quot;
323     COMPILER_BINDCMD_FILE_FLAG=&quot;&quot;
324 
325     # The solstudio linker does not support @-files.
326     if test &quot;x$TOOLCHAIN_TYPE&quot; = xsolstudio; then
327       COMPILER_COMMAND_FILE_FLAG=
328     fi
329 
330     # Check if @file is supported by gcc
331     if test &quot;x$TOOLCHAIN_TYPE&quot; = xgcc; then
332       AC_MSG_CHECKING([if @file is supported by gcc])
333       # Extra emtpy &quot;&quot; to prevent ECHO from interpreting &#39;--version&#39; as argument
334       $ECHO &quot;&quot; &quot;--version&quot; &gt; command.file
335       if $CXX @command.file 2&gt;&amp;AS_MESSAGE_LOG_FD &gt;&amp;AS_MESSAGE_LOG_FD; then
336         AC_MSG_RESULT(yes)
337         COMPILER_COMMAND_FILE_FLAG=&quot;@&quot;
338       else
339         AC_MSG_RESULT(no)
340         COMPILER_COMMAND_FILE_FLAG=
341       fi
342       $RM command.file
343     fi
344   fi
345 
346   AC_SUBST(COMPILER_TARGET_BITS_FLAG)
347   AC_SUBST(COMPILER_COMMAND_FILE_FLAG)
348   AC_SUBST(COMPILER_BINDCMD_FILE_FLAG)
349 
350   # Check that the compiler supports -mX (or -qX on AIX) flags
351   # Set COMPILER_SUPPORTS_TARGET_BITS_FLAG to &#39;true&#39; if it does
352   FLAGS_COMPILER_CHECK_ARGUMENTS(ARGUMENT: [${COMPILER_TARGET_BITS_FLAG}${OPENJDK_TARGET_CPU_BITS}],
353       IF_TRUE: [COMPILER_SUPPORTS_TARGET_BITS_FLAG=true],
354       IF_FALSE: [COMPILER_SUPPORTS_TARGET_BITS_FLAG=false])
355   AC_SUBST(COMPILER_SUPPORTS_TARGET_BITS_FLAG)
356 
357   if test &quot;x$TOOLCHAIN_TYPE&quot; = xmicrosoft; then
358     CC_OUT_OPTION=-Fo
359     LD_OUT_OPTION=-out:
360     AR_OUT_OPTION=-out:
361   else
362     # The option used to specify the target .o,.a or .so file.
363     # When compiling, how to specify the to be created object file.
364     CC_OUT_OPTION=&#39;-o$(SPACE)&#39;
365     # When linking, how to specify the output
366     LD_OUT_OPTION=&#39;-o$(SPACE)&#39;
367     # When archiving, how to specify the destination static archive.
368     if test &quot;x$OPENJDK_TARGET_OS&quot; = xmacosx; then
369       AR_OUT_OPTION=&#39;-r -cs$(SPACE)&#39;
370     else
371       AR_OUT_OPTION=&#39;-rcs$(SPACE)&#39;
372     fi
373   fi
374   AC_SUBST(CC_OUT_OPTION)
375   AC_SUBST(LD_OUT_OPTION)
376   AC_SUBST(AR_OUT_OPTION)
377 
378   # Generate make dependency files
379   if test &quot;x$TOOLCHAIN_TYPE&quot; = xgcc; then
380     C_FLAG_DEPS=&quot;-MMD -MF&quot;
381   elif test &quot;x$TOOLCHAIN_TYPE&quot; = xclang; then
382     C_FLAG_DEPS=&quot;-MMD -MF&quot;
383   elif test &quot;x$TOOLCHAIN_TYPE&quot; = xsolstudio; then
384     C_FLAG_DEPS=&quot;-xMMD -xMF&quot;
385   elif test &quot;x$TOOLCHAIN_TYPE&quot; = xxlc; then
386     C_FLAG_DEPS=&quot;-qmakedep=gcc -MF&quot;
387   fi
388   CXX_FLAG_DEPS=&quot;$C_FLAG_DEPS&quot;
389   AC_SUBST(C_FLAG_DEPS)
390   AC_SUBST(CXX_FLAG_DEPS)
391 ])
392 
393 AC_DEFUN_ONCE([FLAGS_POST_TOOLCHAIN],
394 [
395   FLAGS_SETUP_TOOLCHAIN_CONTROL
396 
397   if test &quot;x$BUILD_SYSROOT&quot; != x; then
398     FLAGS_SETUP_SYSROOT_FLAGS([BUILD_])
399   else
400     if test &quot;x$COMPILE_TYPE&quot; != &quot;xcross&quot;; then
401       BUILD_SYSROOT_CFLAGS=&quot;$SYSROOT_CFLAGS&quot;
402       BUILD_SYSROOT_LDFLAGS=&quot;$SYSROOT_LDFLAGS&quot;
403     fi
404   fi
405   AC_SUBST(BUILD_SYSROOT_CFLAGS)
406   AC_SUBST(BUILD_SYSROOT_LDFLAGS)
407 
408 ])
409 
410 AC_DEFUN([FLAGS_SETUP_FLAGS],
411 [
412   FLAGS_SETUP_MACOSX_VERSION
413   FLAGS_SETUP_ABI_PROFILE
414 
415   FLAGS_SETUP_SHARED_LIBS
416   FLAGS_SETUP_DEBUG_SYMBOLS
417   FLAGS_SETUP_WARNINGS
418   FLAGS_SETUP_QUALITY_CHECKS
419   FLAGS_SETUP_OPTIMIZATION
420 
421   FLAGS_SETUP_CFLAGS
422   FLAGS_SETUP_LDFLAGS
423 
424   FLAGS_SETUP_ARFLAGS
425   FLAGS_SETUP_STRIPFLAGS
426   FLAGS_SETUP_RCFLAGS
427 
428   FLAGS_SETUP_ASFLAGS
429   FLAGS_SETUP_ASFLAGS_CPU_DEP([TARGET])
430   FLAGS_SETUP_ASFLAGS_CPU_DEP([BUILD], [OPENJDK_BUILD_])
431 ])
432 
433 # FLAGS_C_COMPILER_CHECK_ARGUMENTS(ARGUMENT: [ARGUMENT], IF_TRUE: [RUN-IF-TRUE],
434 #                                  IF_FALSE: [RUN-IF-FALSE])
435 # ------------------------------------------------------------
436 # Check that the C compiler supports an argument
437 UTIL_DEFUN_NAMED([FLAGS_C_COMPILER_CHECK_ARGUMENTS],
438     [*ARGUMENT IF_TRUE IF_FALSE PREFIX], [$@],
439 [
440   AC_MSG_CHECKING([if ARG_PREFIX[CC] supports &quot;ARG_ARGUMENT&quot;])
441   supports=yes
442 
443   saved_cflags=&quot;$CFLAGS&quot;
444   saved_cc=&quot;$CC&quot;
445   CFLAGS=&quot;$CFLAGS $CFLAGS_WARNINGS_ARE_ERRORS ARG_ARGUMENT&quot;
446   CC=&quot;$ARG_PREFIX[CC]&quot;
447   AC_LANG_PUSH([C])
448   AC_COMPILE_IFELSE([AC_LANG_SOURCE([[int i;]])], [],
449       [supports=no])
450   AC_LANG_POP([C])
451   CC=&quot;$saved_cc&quot;
452   CFLAGS=&quot;$saved_cflags&quot;
453 
454   AC_MSG_RESULT([$supports])
455   if test &quot;x$supports&quot; = &quot;xyes&quot; ; then
456     :
457     ARG_IF_TRUE
458   else
459     :
460     ARG_IF_FALSE
461   fi
462 ])
463 
464 # FLAGS_CXX_COMPILER_CHECK_ARGUMENTS(ARGUMENT: [ARGUMENT], IF_TRUE: [RUN-IF-TRUE],
465 #                                    IF_FALSE: [RUN-IF-FALSE])
466 # ------------------------------------------------------------
467 # Check that the C++ compiler supports an argument
468 UTIL_DEFUN_NAMED([FLAGS_CXX_COMPILER_CHECK_ARGUMENTS],
469     [*ARGUMENT IF_TRUE IF_FALSE PREFIX], [$@],
470 [
471   AC_MSG_CHECKING([if ARG_PREFIX[CXX] supports &quot;ARG_ARGUMENT&quot;])
472   supports=yes
473 
474   saved_cxxflags=&quot;$CXXFLAGS&quot;
475   saved_cxx=&quot;$CXX&quot;
476   CXXFLAGS=&quot;$CXXFLAG $CFLAGS_WARNINGS_ARE_ERRORS ARG_ARGUMENT&quot;
477   CXX=&quot;$ARG_PREFIX[CXX]&quot;
478   AC_LANG_PUSH([C++])
479   AC_COMPILE_IFELSE([AC_LANG_SOURCE([[int i;]])], [],
480       [supports=no])
481   AC_LANG_POP([C++])
482   CXX=&quot;$saved_cxx&quot;
483   CXXFLAGS=&quot;$saved_cxxflags&quot;
484 
485   AC_MSG_RESULT([$supports])
486   if test &quot;x$supports&quot; = &quot;xyes&quot; ; then
487     :
488     ARG_IF_TRUE
489   else
490     :
491     ARG_IF_FALSE
492   fi
493 ])
494 
495 # FLAGS_COMPILER_CHECK_ARGUMENTS(ARGUMENT: [ARGUMENT], IF_TRUE: [RUN-IF-TRUE],
496 #                                IF_FALSE: [RUN-IF-FALSE])
497 # ------------------------------------------------------------
498 # Check that the C and C++ compilers support an argument
499 UTIL_DEFUN_NAMED([FLAGS_COMPILER_CHECK_ARGUMENTS],
500     [*ARGUMENT IF_TRUE IF_FALSE PREFIX], [$@],
501 [
502   FLAGS_C_COMPILER_CHECK_ARGUMENTS(ARGUMENT: [ARG_ARGUMENT],
503       IF_TRUE: [C_COMP_SUPPORTS=&quot;yes&quot;],
504       IF_FALSE: [C_COMP_SUPPORTS=&quot;no&quot;],
505       PREFIX: [ARG_PREFIX])
506   FLAGS_CXX_COMPILER_CHECK_ARGUMENTS(ARGUMENT: [ARG_ARGUMENT],
507       IF_TRUE: [CXX_COMP_SUPPORTS=&quot;yes&quot;],
508       IF_FALSE: [CXX_COMP_SUPPORTS=&quot;no&quot;],
509       PREFIX: [ARG_PREFIX])
510 
511   AC_MSG_CHECKING([if both ARG_PREFIX[CC] and ARG_PREFIX[CXX] support &quot;ARG_ARGUMENT&quot;])
512   supports=no
513   if test &quot;x$C_COMP_SUPPORTS&quot; = &quot;xyes&quot; -a &quot;x$CXX_COMP_SUPPORTS&quot; = &quot;xyes&quot;; then
514     supports=yes;
515   fi
516 
517   AC_MSG_RESULT([$supports])
518   if test &quot;x$supports&quot; = &quot;xyes&quot; ; then
519     :
520     ARG_IF_TRUE
521   else
522     :
523     ARG_IF_FALSE
524   fi
525 ])
526 
527 # FLAGS_LINKER_CHECK_ARGUMENTS(ARGUMENT: [ARGUMENT], IF_TRUE: [RUN-IF-TRUE],
528 #                                   IF_FALSE: [RUN-IF-FALSE])
529 # ------------------------------------------------------------
530 # Check that the linker support an argument
531 UTIL_DEFUN_NAMED([FLAGS_LINKER_CHECK_ARGUMENTS],
532     [*ARGUMENT IF_TRUE IF_FALSE], [$@],
533 [
534   AC_MSG_CHECKING([if linker supports &quot;ARG_ARGUMENT&quot;])
535   supports=yes
536 
537   saved_ldflags=&quot;$LDFLAGS&quot;
538   LDFLAGS=&quot;$LDFLAGS ARG_ARGUMENT&quot;
539   AC_LANG_PUSH([C])
540   AC_LINK_IFELSE([AC_LANG_PROGRAM([[]],[[]])],
541       [], [supports=no])
542   AC_LANG_POP([C])
543   LDFLAGS=&quot;$saved_ldflags&quot;
544 
545   AC_MSG_RESULT([$supports])
546   if test &quot;x$supports&quot; = &quot;xyes&quot; ; then
547     :
548     ARG_IF_TRUE
549   else
550     :
551     ARG_IF_FALSE
552   fi
553 ])
<a name="2" id="anc2"></a><b style="font-size: large; color: red">--- EOF ---</b>
















































































</pre>
<input id="eof" value="2" type="hidden" />
</body>
</html>