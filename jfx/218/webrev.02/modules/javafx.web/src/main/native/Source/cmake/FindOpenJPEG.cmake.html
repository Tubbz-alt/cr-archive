<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <title>New modules/javafx.web/src/main/native/Source/cmake/FindOpenJPEG.cmake</title>
    <link rel="stylesheet" href="../../../../../../../style.css" />
  </head>
  <body>
    <pre>
  1 # Copyright (C) 2019 Sony Interactive Entertainment Inc.
  2 #
  3 # Redistribution and use in source and binary forms, with or without
  4 # modification, are permitted provided that the following conditions
  5 # are met:
  6 # 1. Redistributions of source code must retain the above copyright
  7 #    notice, this list of conditions and the following disclaimer.
  8 # 2. Redistributions in binary form must reproduce the above copyright
  9 #    notice, this list of conditions and the following disclaimer in the
 10 #    documentation and/or other materials provided with the distribution.
 11 #
 12 # THIS SOFTWARE IS PROVIDED BY APPLE INC. AND ITS CONTRIBUTORS ``AS IS&#39;&#39;
 13 # AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO,
 14 # THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR
 15 # PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL APPLE INC. OR ITS CONTRIBUTORS
 16 # BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 17 # CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 18 # SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 19 # INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 20 # CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 21 # ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF
 22 # THE POSSIBILITY OF SUCH DAMAGE.
 23 
 24 #[=======================================================================[.rst:
 25 FindOpenJPEG
 26 --------------
 27 
 28 Find OpenJPEG headers and libraries.
 29 
 30 Imported Targets
 31 ^^^^^^^^^^^^^^^^
 32 
 33 ``OpenJPEG::OpenJPEG``
 34   The OpenJPEG library, if found.
 35 
 36 Result Variables
 37 ^^^^^^^^^^^^^^^^
 38 
 39 This will define the following variables in your project:
 40 
 41 ``OpenJPEG_FOUND``
 42   true if (the requested version of) OpenJPEG is available.
 43 ``OpenJPEG_VERSION``
 44   the version of OpenJPEG.
 45 ``OpenJPEG_LIBRARIES``
 46   the libraries to link against to use OpenJPEG.
 47 ``OpenJPEG_INCLUDE_DIRS``
 48   where to find the OpenJPEG headers.
 49 ``OpenJPEG_COMPILE_OPTIONS``
 50   this should be passed to target_compile_options(), if the
 51   target is not used for linking
 52 
 53 #]=======================================================================]
 54 
 55 include(FindPkgConfig)
 56 pkg_check_modules(PC_OPENJPEG QUIET libopenjp2)
 57 set(OpenJPEG_COMPILE_OPTIONS ${PC_OPENJPEG_CFLAGS_OTHER})
 58 set(OpenJPEG_VERSION ${PC_OPENJPEG_VERSION})
 59 
 60 find_path(OpenJPEG_INCLUDE_DIR
 61     NAMES opj_config.h
 62     HINTS ${PC_OPENJPEG_INCLUDEDIR} ${PC_OPENJPEG_INCLUDE_DIRS}
 63 )
 64 
 65 find_library(OpenJPEG_LIBRARY
 66     NAMES ${OpenJPEG_NAMES} openjp2
 67     HINTS ${PC_OPENJPEG_LIBDIR} ${PC_OPENJPEG_LIBRARY_DIRS}
 68 )
 69 
 70 if (OpenJPEG_INCLUDE_DIR AND NOT OpenJPEG_VERSION)
 71     if (EXISTS &quot;${OpenJPEG_INCLUDE_DIR}/opj_config.h&quot;)
 72         file(READ &quot;${OpenJPEG_INCLUDE_DIR}/opj_config.h&quot; OpenJPEG_VERSION_CONTENT)
 73 
 74         string(REGEX MATCH &quot;#define +OPJ_VERSION_MAJOR +([0-9]+)&quot; _dummy &quot;${OpenJPEG_VERSION_CONTENT}&quot;)
 75         set(OpenJPEG_VERSION_MAJOR &quot;${CMAKE_MATCH_1}&quot;)
 76 
 77         string(REGEX MATCH &quot;#define +OPJ_VERSION_MINOR +([0-9]+)&quot; _dummy &quot;${OpenJPEG_VERSION_CONTENT}&quot;)
 78         set(OpenJPEG_VERSION_MINOR &quot;${CMAKE_MATCH_1}&quot;)
 79 
 80         string(REGEX MATCH &quot;#define +OPJ_VERSION_BUILD +([0-9]+)&quot; _dummy &quot;${OpenJPEG_VERSION_CONTENT}&quot;)
 81         set(OpenJPEG_VERSION_PATCH &quot;${CMAKE_MATCH_1}&quot;)
 82 
 83         set(OpenJPEG_VERSION &quot;${OpenJPEG_VERSION_MAJOR}.${OpenJPEG_VERSION_MINOR}.${OpenJPEG_VERSION_PATCH}&quot;)
 84     endif ()
 85 endif ()
 86 
 87 include(FindPackageHandleStandardArgs)
 88 find_package_handle_standard_args(OpenJPEG
 89     FOUND_VAR OpenJPEG_FOUND
 90     REQUIRED_VARS OpenJPEG_LIBRARY OpenJPEG_INCLUDE_DIR
 91     VERSION_VAR OpenJPEG_VERSION
 92 )
 93 
 94 if (OpenJPEG_LIBRARY AND NOT TARGET OpenJPEG::OpenJPEG)
 95     add_library(OpenJPEG::OpenJPEG UNKNOWN IMPORTED GLOBAL)
 96     set_target_properties(OpenJPEG::OpenJPEG PROPERTIES
 97         IMPORTED_LOCATION &quot;${OpenJPEG_LIBRARY}&quot;
 98         INTERFACE_COMPILE_OPTIONS &quot;${OpenJPEG_COMPILE_OPTIONS}&quot;
 99         INTERFACE_INCLUDE_DIRECTORIES &quot;${OpenJPEG_INCLUDE_DIR}&quot;
100     )
101 endif ()
102 
103 mark_as_advanced(OpenJPEG_INCLUDE_DIR OpenJPEG_LIBRARY)
104 
105 if (OpenJPEG_FOUND)
106     set(OpenJPEG_LIBRARIES ${OpenJPEG_LIBRARY})
107     set(OpenJPEG_INCLUDE_DIRS ${OpenJPEG_INCLUDE_DIR})
108 endif ()
    </pre>
  </body>
</html>