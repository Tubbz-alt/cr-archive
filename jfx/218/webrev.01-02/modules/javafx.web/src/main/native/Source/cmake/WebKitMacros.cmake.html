<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <title>New modules/javafx.web/src/main/native/Source/cmake/WebKitMacros.cmake</title>
    <link rel="stylesheet" href="../../../../../../../style.css" />
  </head>
  <body>
    <pre>
  1 # This file is for macros that are used by multiple projects. If your macro is
  2 # exclusively needed in only one subdirectory of Source (e.g. only needed by
  3 # WebCore), then put it there instead.
  4 
  5 macro(WEBKIT_COMPUTE_SOURCES _framework)
  6     set(_derivedSourcesPath ${${_framework}_DERIVED_SOURCES_DIR})
  7 
  8     foreach (_sourcesListFile IN LISTS ${_framework}_UNIFIED_SOURCE_LIST_FILES)
  9       configure_file(&quot;${CMAKE_CURRENT_SOURCE_DIR}/${_sourcesListFile}&quot; &quot;${_derivedSourcesPath}/${_sourcesListFile}&quot; COPYONLY)
 10       message(STATUS &quot;Using source list file: ${_sourcesListFile}&quot;)
 11 
 12       list(APPEND _sourceListFileTruePaths &quot;${CMAKE_CURRENT_SOURCE_DIR}/${_sourcesListFile}&quot;)
 13     endforeach ()
 14 
 15     if (ENABLE_UNIFIED_BUILDS)
 16         execute_process(COMMAND ${RUBY_EXECUTABLE} ${WTF_SCRIPTS_DIR}/generate-unified-source-bundles.rb
 17             &quot;--derived-sources-path&quot; &quot;${_derivedSourcesPath}&quot;
 18             &quot;--source-tree-path&quot; ${CMAKE_CURRENT_SOURCE_DIR}
 19             &quot;--print-bundled-sources&quot;
 20             &quot;--feature-flags&quot; &quot;${UNIFIED_SOURCE_LIST_ENABLED_FEATURES}&quot;
 21             ${_sourceListFileTruePaths}
 22             RESULT_VARIABLE _resultTmp
 23             OUTPUT_VARIABLE _outputTmp)
 24 
 25         if (${_resultTmp})
 26              message(FATAL_ERROR &quot;generate-unified-source-bundles.rb exited with non-zero status, exiting&quot;)
 27         endif ()
 28 
 29         foreach (_sourceFileTmp IN LISTS _outputTmp)
 30             set_source_files_properties(${_sourceFileTmp} PROPERTIES HEADER_FILE_ONLY ON)
 31             list(APPEND ${_framework}_HEADERS ${_sourceFileTmp})
 32         endforeach ()
 33         unset(_sourceFileTmp)
 34 
 35         execute_process(COMMAND ${RUBY_EXECUTABLE} ${WTF_SCRIPTS_DIR}/generate-unified-source-bundles.rb
 36             &quot;--derived-sources-path&quot; &quot;${_derivedSourcesPath}&quot;
 37             &quot;--source-tree-path&quot; ${CMAKE_CURRENT_SOURCE_DIR}
 38             &quot;--feature-flags&quot; &quot;${UNIFIED_SOURCE_LIST_ENABLED_FEATURES}&quot;
 39             ${_sourceListFileTruePaths}
 40             RESULT_VARIABLE  _resultTmp
 41             OUTPUT_VARIABLE _outputTmp)
 42 
 43         if (${_resultTmp})
 44             message(FATAL_ERROR &quot;generate-unified-source-bundles.rb exited with non-zero status, exiting&quot;)
 45         endif ()
 46 
 47         list(APPEND ${_framework}_SOURCES ${_outputTmp})
 48         unset(_resultTmp)
 49         unset(_outputTmp)
 50     else ()
 51         execute_process(COMMAND ${RUBY_EXECUTABLE} ${WTF_SCRIPTS_DIR}/generate-unified-source-bundles.rb
 52             &quot;--derived-sources-path&quot; &quot;${_derivedSourcesPath}&quot;
 53             &quot;--source-tree-path&quot; ${CMAKE_CURRENT_SOURCE_DIR}
 54             &quot;--print-all-sources&quot;
 55             &quot;--feature-flags&quot; &quot;${UNIFIED_SOURCE_LIST_ENABLED_FEATURES}&quot;
 56             ${_sourceListFileTruePaths}
 57             RESULT_VARIABLE _resultTmp
 58             OUTPUT_VARIABLE _outputTmp)
 59 
 60         if (${_resultTmp})
 61              message(FATAL_ERROR &quot;generate-unified-source-bundles.rb exited with non-zero status, exiting&quot;)
 62         endif ()
 63 
 64         list(APPEND ${_framework}_SOURCES ${_outputTmp})
 65         unset(_resultTmp)
 66         unset(_outputTmp)
 67     endif ()
 68 endmacro()
 69 
 70 macro(WEBKIT_INCLUDE_CONFIG_FILES_IF_EXISTS)
 71     set(_file ${CMAKE_CURRENT_SOURCE_DIR}/Platform${PORT}.cmake)
 72     if (EXISTS ${_file})
 73         message(STATUS &quot;Using platform-specific CMakeLists: ${_file}&quot;)
 74         include(${_file})
 75     else ()
 76         message(STATUS &quot;Platform-specific CMakeLists not found: ${_file}&quot;)
 77     endif ()
 78 endmacro()
 79 
 80 # Append the given dependencies to the source file
 81 macro(WEBKIT_ADD_SOURCE_DEPENDENCIES _source _deps)
 82     set(_tmp)
 83     get_source_file_property(_tmp ${_source} OBJECT_DEPENDS)
 84     if (NOT _tmp)
 85         set(_tmp &quot;&quot;)
 86     endif ()
 87 
 88     foreach (f ${_deps})
 89         list(APPEND _tmp &quot;${f}&quot;)
 90     endforeach ()
 91 
 92     set_source_files_properties(${_source} PROPERTIES OBJECT_DEPENDS &quot;${_tmp}&quot;)
 93     unset(_tmp)
 94 endmacro()
 95 
 96 macro(WEBKIT_ADD_PRECOMPILED_HEADER _header _cpp _source)
 97     if (MSVC)
 98         get_filename_component(PrecompiledBasename ${_cpp} NAME_WE)
 99         file(MAKE_DIRECTORY &quot;${CMAKE_CURRENT_BINARY_DIR}/${_source}&quot;)
100         set(PrecompiledBinary &quot;${CMAKE_CURRENT_BINARY_DIR}/${_source}/${PrecompiledBasename}.pch&quot;)
101         set(_sources ${${_source}})
102 
103         # clang-cl requires /FI with /Yc
104         if (COMPILER_IS_CLANG_CL)
105             set_source_files_properties(${_cpp}
106                 PROPERTIES COMPILE_FLAGS &quot;/Yc\&quot;${_header}\&quot; /Fp\&quot;${PrecompiledBinary}\&quot; /FI\&quot;${_header}\&quot;&quot;
107                 OBJECT_OUTPUTS &quot;${PrecompiledBinary}&quot;)
108         else ()
109             set_source_files_properties(${_cpp}
110                 PROPERTIES COMPILE_FLAGS &quot;/Yc\&quot;${_header}\&quot; /Fp\&quot;${PrecompiledBinary}\&quot;&quot;
111                 OBJECT_OUTPUTS &quot;${PrecompiledBinary}&quot;)
112         endif ()
113         set_source_files_properties(${_sources}
114             PROPERTIES COMPILE_FLAGS &quot;/Yu\&quot;${_header}\&quot; /FI\&quot;${_header}\&quot; /Fp\&quot;${PrecompiledBinary}\&quot;&quot;)
115 
116         foreach (_src ${_sources})
117             WEBKIT_ADD_SOURCE_DEPENDENCIES(${_src} ${PrecompiledBinary})
118         endforeach ()
119 
120         list(APPEND ${_source} ${_cpp})
121     endif ()
122     #FIXME: Add support for Xcode.
123 endmacro()
124 
125 macro(WEBKIT_WRAP_SOURCELIST)
126     foreach (_file ${ARGN})
127         get_filename_component(_basename ${_file} NAME_WE)
128         get_filename_component(_path ${_file} PATH)
129 
130         if (NOT _file MATCHES &quot;${DERIVED_SOURCES_WEBCORE_DIR}&quot;)
131             string(REGEX REPLACE &quot;/&quot; &quot;\\\\\\\\&quot; _sourcegroup &quot;${_path}&quot;)
132             source_group(&quot;${_sourcegroup}&quot; FILES ${_file})
133         endif ()
134     endforeach ()
135 
136     source_group(&quot;DerivedSources&quot; REGULAR_EXPRESSION &quot;${DERIVED_SOURCES_WEBCORE_DIR}&quot;)
137 endmacro()
138 
139 macro(WEBKIT_FRAMEWORK_DECLARE _target)
140     # add_library() without any source files triggers CMake warning
141     # Addition of dummy &quot;source&quot; file does not result in any changes in generated build.ninja file
142     add_library(${_target} ${${_target}_LIBRARY_TYPE} &quot;${CMAKE_BINARY_DIR}/cmakeconfig.h&quot;)
143 endmacro()
144 
145 macro(WEBKIT_EXECUTABLE_DECLARE _target)
146     add_executable(${_target} &quot;${CMAKE_BINARY_DIR}/cmakeconfig.h&quot;)
147 endmacro()
148 
149 # Private macro for setting the properties of a target.
150 # Rather than just having _target like WEBKIT_FRAMEWORK and WEBKIT_EXECUTABLE the parameters are
151 # split into _target_logical_name, which is used for variable expansion, and _target_cmake_name.
152 # This is done to support WEBKIT_WRAP_EXECUTABLE which uses the _target_logical_name variables
153 # but requires a different _target_cmake_name.
154 macro(_WEBKIT_TARGET _target_logical_name _target_cmake_name)
155     target_sources(${_target_cmake_name} PRIVATE
156         ${${_target_logical_name}_HEADERS}
157         ${${_target_logical_name}_SOURCES}
158     )
159     target_include_directories(${_target_cmake_name} PUBLIC &quot;$&lt;BUILD_INTERFACE:${${_target_logical_name}_INCLUDE_DIRECTORIES}&gt;&quot;)
160     target_include_directories(${_target_cmake_name} SYSTEM PRIVATE &quot;$&lt;BUILD_INTERFACE:${${_target_logical_name}_SYSTEM_INCLUDE_DIRECTORIES}&gt;&quot;)
161     target_include_directories(${_target_cmake_name} PRIVATE &quot;$&lt;BUILD_INTERFACE:${${_target_logical_name}_PRIVATE_INCLUDE_DIRECTORIES}&gt;&quot;)
162 
163     target_compile_definitions(${_target_cmake_name} PRIVATE &quot;BUILDING_${_target_logical_name}&quot;)
164     if (${_target_logical_name}_DEFINITIONS)
165         target_compile_definitions(${_target_cmake_name} PUBLIC ${${_target_logical_name}_DEFINITIONS})
166     endif ()
167     if (${_target_logical_name}_PRIVATE_DEFINITIONS)
168         target_compile_definitions(${_target_cmake_name} PRIVATE ${${_target_logical_name}_PRIVATE_DEFINITIONS})
169     endif ()
170 
171     if (${_target_logical_name}_LIBRARIES)
172         target_link_libraries(${_target_cmake_name} PUBLIC ${${_target_logical_name}_LIBRARIES})
173     endif ()
174     if (${_target_logical_name}_PRIVATE_LIBRARIES)
175         target_link_libraries(${_target_cmake_name} PRIVATE ${${_target_logical_name}_PRIVATE_LIBRARIES})
176     endif ()
177 
178     if (${_target_logical_name}_DEPENDENCIES)
179         add_dependencies(${_target_cmake_name} ${${_target_logical_name}_DEPENDENCIES})
180     endif ()
181 endmacro()
182 
183 macro(WEBKIT_FRAMEWORK _target)
184     _WEBKIT_TARGET(${_target} ${_target})
185 
186     if (${_target}_OUTPUT_NAME)
187         set_target_properties(${_target} PROPERTIES OUTPUT_NAME ${${_target}_OUTPUT_NAME})
188     endif ()
189 
190     if (${_target}_PRE_BUILD_COMMAND)
191         add_custom_target(_${_target}_PreBuild COMMAND ${${_target}_PRE_BUILD_COMMAND} VERBATIM)
192         add_dependencies(${_target} _${_target}_PreBuild)
193     endif ()
194 
195     if (${_target}_POST_BUILD_COMMAND)
196         add_custom_command(TARGET ${_target} POST_BUILD COMMAND ${${_target}_POST_BUILD_COMMAND} VERBATIM)
197     endif ()
198 
199     if (APPLE AND NOT PORT STREQUAL &quot;GTK&quot; AND NOT PORT STREQUAL &quot;Java&quot; AND NOT ${${_target}_LIBRARY_TYPE} MATCHES STATIC)
200         set_target_properties(${_target} PROPERTIES FRAMEWORK TRUE)
201         install(TARGETS ${_target} FRAMEWORK DESTINATION ${LIB_INSTALL_DIR})
202     endif ()
203 endmacro()
204 
205 # FIXME Move into WEBKIT_FRAMEWORK after all libraries are using this macro
206 macro(WEBKIT_FRAMEWORK_TARGET _target)
207     add_library(${_target}_PostBuild INTERFACE)
208     target_link_libraries(${_target}_PostBuild INTERFACE ${${_target}_INTERFACE_LIBRARIES})
209     target_include_directories(${_target}_PostBuild INTERFACE ${${_target}_INTERFACE_INCLUDE_DIRECTORIES})
210     add_dependencies(${_target}_PostBuild ${${_target}_INTERFACE_DEPENDENCIES})
211     add_library(WebKit::${_target} ALIAS ${_target}_PostBuild)
212 endmacro()
213 
214 macro(WEBKIT_EXECUTABLE _target)
215     _WEBKIT_TARGET(${_target} ${_target})
216 
217     if (${_target}_OUTPUT_NAME)
218         set_target_properties(${_target} PROPERTIES OUTPUT_NAME ${${_target}_OUTPUT_NAME})
219     endif ()
220 endmacro()
221 
222 macro(WEBKIT_WRAP_EXECUTABLE _target)
223     set(oneValueArgs TARGET_NAME)
224     set(multiValueArgs SOURCES LIBRARIES)
225     cmake_parse_arguments(opt &quot;&quot; &quot;${oneValueArgs}&quot; &quot;${multiValueArgs}&quot; ${ARGN})
226 
227     if (opt_TARGET_NAME)
228         set(_wrapped_target_name ${opt_TARGET_NAME})
229     else ()
230         set(_wrapped_target_name ${_target}Lib)
231     endif ()
232 
233     add_library(${_wrapped_target_name} SHARED &quot;${CMAKE_BINARY_DIR}/cmakeconfig.h&quot;)
234 
235     _WEBKIT_TARGET(${_target} ${_wrapped_target_name})
236 
237     # Unset values
238     unset(${_target}_HEADERS)
239     unset(${_target}_DEFINITIONS)
240     unset(${_target}_PRIVATE_DEFINITIONS)
241     unset(${_target}_INCLUDE_DIRECTORIES)
242     unset(${_target}_SYSTEM_INCLUDE_DIRECTORIES)
243     unset(${_target}_PRIVATE_INCLUDE_DIRECTORIES)
244 
245     # Reset the sources
246     set(${_target}_SOURCES ${opt_SOURCES})
247     set(${_target}_LIBRARIES ${opt_LIBRARIES})
248     set(${_target}_DEPENDENCIES ${_wrapped_target_name})
249 endmacro()
250 
251 macro(WEBKIT_CREATE_FORWARDING_HEADER _target_directory _file)
252     get_filename_component(_source_path &quot;${CMAKE_SOURCE_DIR}/Source/&quot; ABSOLUTE)
253     get_filename_component(_absolute &quot;${_file}&quot; ABSOLUTE)
254     get_filename_component(_name &quot;${_file}&quot; NAME)
255     set(_target_filename &quot;${_target_directory}/${_name}&quot;)
256 
257     # Try to make the path in the forwarding header relative to the Source directory
258     # so that these forwarding headers are compatible with the ones created by the
259     # WebKit2 generate-forwarding-headers script.
260     string(REGEX REPLACE &quot;${_source_path}/&quot; &quot;&quot; _relative ${_absolute})
261 
262     set(_content &quot;#include \&quot;${_relative}\&quot;\n&quot;)
263 
264     if (EXISTS &quot;${_target_filename}&quot;)
265         file(READ &quot;${_target_filename}&quot; _old_content)
266     endif ()
267 
268     if (NOT _old_content STREQUAL _content)
269         file(WRITE &quot;${_target_filename}&quot; &quot;${_content}&quot;)
270     endif ()
271 endmacro()
272 
273 macro(WEBKIT_CREATE_FORWARDING_HEADERS _framework)
274     # On Windows, we copy the entire contents of forwarding headers.
275     if (NOT WIN32 or PORT STREQUAL &quot;Java&quot;)
276         set(_processing_directories 0)
277         set(_processing_files 0)
278         set(_target_directory &quot;${FORWARDING_HEADERS_DIR}/${_framework}&quot;)
279 
280         file(GLOB _files &quot;${_target_directory}/*.h&quot;)
281         foreach (_file ${_files})
282             file(READ &quot;${_file}&quot; _content)
283             string(REGEX MATCH &quot;^#include \&quot;([^\&quot;]*)\&quot;&quot; _matched ${_content})
284             if (_matched AND NOT EXISTS &quot;${CMAKE_SOURCE_DIR}/Source/${CMAKE_MATCH_1}&quot;)
285                file(REMOVE &quot;${_file}&quot;)
286             endif ()
287         endforeach ()
288 
289         foreach (_currentArg ${ARGN})
290             if (&quot;${_currentArg}&quot; STREQUAL &quot;DIRECTORIES&quot;)
291                 set(_processing_directories 1)
292                 set(_processing_files 0)
293             elseif (&quot;${_currentArg}&quot; STREQUAL &quot;FILES&quot;)
294                 set(_processing_directories 0)
295                 set(_processing_files 1)
296             elseif (_processing_directories)
297                 file(GLOB _files &quot;${_currentArg}/*.h&quot;)
298                 foreach (_file ${_files})
299                     WEBKIT_CREATE_FORWARDING_HEADER(${_target_directory} ${_file})
300                 endforeach ()
301             elseif (_processing_files)
302                 WEBKIT_CREATE_FORWARDING_HEADER(${_target_directory} ${_currentArg})
303             endif ()
304         endforeach ()
305     endif ()
306 endmacro()
307 
308 function(WEBKIT_MAKE_FORWARDING_HEADERS framework)
309     set(options FLATTENED)
310     set(oneValueArgs DESTINATION TARGET_NAME)
311     set(multiValueArgs DIRECTORIES FILES)
312     cmake_parse_arguments(opt &quot;${options}&quot; &quot;${oneValueArgs}&quot; &quot;${multiValueArgs}&quot; ${ARGN})
313     set(headers ${opt_FILES})
314     file(MAKE_DIRECTORY ${opt_DESTINATION})
315     foreach (dir IN LISTS opt_DIRECTORIES)
316         file(GLOB files RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} ${dir}/*.h)
317         list(APPEND headers ${files})
318     endforeach ()
319     set(fwd_headers)
320     foreach (header IN LISTS headers)
321         if (IS_ABSOLUTE ${header})
322             set(src_header ${header})
323         else ()
324             set(src_header ${CMAKE_CURRENT_SOURCE_DIR}/${header})
325         endif ()
326         if (opt_FLATTENED)
327             get_filename_component(header_filename ${header} NAME)
328             set(fwd_header ${opt_DESTINATION}/${header_filename})
329         else ()
330             get_filename_component(header_dir ${header} DIRECTORY)
331             file(MAKE_DIRECTORY ${opt_DESTINATION}/${header_dir})
332             set(fwd_header ${opt_DESTINATION}/${header})
333         endif ()
334         add_custom_command(OUTPUT ${fwd_header}
335             COMMAND ${CMAKE_COMMAND} -E copy ${src_header} ${fwd_header}
336             MAIN_DEPENDENCY ${header}
337             VERBATIM
338         )
339         list(APPEND fwd_headers ${fwd_header})
340     endforeach ()
341     if (opt_TARGET_NAME)
342         set(target_name ${opt_TARGET_NAME})
343     else ()
344         set(target_name ${framework}ForwardingHeaders)
345     endif ()
346     add_custom_target(${target_name} DEPENDS ${fwd_headers})
347     add_dependencies(${framework} ${target_name})
348 endfunction()
349 
350 function(WEBKIT_COPY_FILES target_name)
351     set(options FLATTENED)
352     set(oneValueArgs DESTINATION)
353     set(multiValueArgs FILES)
354     cmake_parse_arguments(opt &quot;${options}&quot; &quot;${oneValueArgs}&quot; &quot;${multiValueArgs}&quot; ${ARGN})
355     set(files ${opt_FILES})
356     set(dst_files)
357     foreach (file IN LISTS files)
358         if (IS_ABSOLUTE ${file})
359             set(src_file ${file})
360         else ()
361             set(src_file ${CMAKE_CURRENT_SOURCE_DIR}/${file})
362         endif ()
363         if (opt_FLATTENED)
364             get_filename_component(filename ${file} NAME)
365             set(dst_file ${opt_DESTINATION}/${filename})
366         else ()
367             get_filename_component(file_dir ${file} DIRECTORY)
368             file(MAKE_DIRECTORY ${opt_DESTINATION}/${file_dir})
369             set(dst_file ${opt_DESTINATION}/${file})
370         endif ()
371         add_custom_command(OUTPUT ${dst_file}
372             COMMAND ${CMAKE_COMMAND} -E copy ${src_file} ${dst_file}
373             MAIN_DEPENDENCY ${file}
374             VERBATIM
375         )
376         list(APPEND dst_files ${dst_file})
377     endforeach ()
378     add_custom_target(${target_name} ALL DEPENDS ${dst_files})
379 endfunction()
380 
381 # Helper macros for debugging CMake problems.
382 macro(WEBKIT_DEBUG_DUMP_COMMANDS)
383     set(CMAKE_VERBOSE_MAKEFILE ON)
384 endmacro()
385 
386 macro(WEBKIT_DEBUG_DUMP_VARIABLES)
387     set_cmake_property(_variableNames VARIABLES)
388     foreach (_variableName ${_variableNames})
389        message(STATUS &quot;${_variableName}=${${_variableName}}&quot;)
390     endforeach ()
391 endmacro()
392 
393 # Append the given flag to the target property.
394 # Builds on top of get_target_property() and set_target_properties()
395 macro(WEBKIT_ADD_TARGET_PROPERTIES _target _property _flags)
396     get_target_property(_tmp ${_target} ${_property})
397     if (NOT _tmp)
398         set(_tmp &quot;&quot;)
399     endif (NOT _tmp)
400 
401     foreach (f ${_flags})
402         set(_tmp &quot;${_tmp} ${f}&quot;)
403     endforeach (f ${_flags})
404 
405     set_target_properties(${_target} PROPERTIES ${_property} ${_tmp})
406     unset(_tmp)
407 endmacro()
408 
409 macro(WEBKIT_POPULATE_LIBRARY_VERSION library_name)
410     if (NOT DEFINED ${library_name}_VERSION_MAJOR)
411         set(${library_name}_VERSION_MAJOR ${PROJECT_VERSION_MAJOR})
412     endif ()
413     if (NOT DEFINED ${library_name}_VERSION_MINOR)
414         set(${library_name}_VERSION_MINOR ${PROJECT_VERSION_MINOR})
415     endif ()
416     if (NOT DEFINED ${library_name}_VERSION_MICRO)
417         set(${library_name}_VERSION_MICRO ${PROJECT_VERSION_MICRO})
418     endif ()
419     if (NOT DEFINED ${library_name}_VERSION)
420         set(${library_name}_VERSION ${PROJECT_VERSION})
421     endif ()
422 endmacro()
423 
424 macro(WEBKIT_CREATE_SYMLINK target src dest)
425     add_custom_command(TARGET ${target} POST_BUILD
426         COMMAND ln -sf ${src} ${dest}
427         DEPENDS ${dest}
428         COMMENT &quot;Create symlink from ${src} to ${dest}&quot;)
429 endmacro()
    </pre>
  </body>
</html>