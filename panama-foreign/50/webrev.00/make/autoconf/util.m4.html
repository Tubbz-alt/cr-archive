<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <title>New make/autoconf/util.m4</title>
    <link rel="stylesheet" href="../../style.css" />
  </head>
  <body>
    <pre>
  1 #
  2 # Copyright (c) 2011, 2020, Oracle and/or its affiliates. All rights reserved.
  3 # DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.
  4 #
  5 # This code is free software; you can redistribute it and/or modify it
  6 # under the terms of the GNU General Public License version 2 only, as
  7 # published by the Free Software Foundation.  Oracle designates this
  8 # particular file as subject to the &quot;Classpath&quot; exception as provided
  9 # by Oracle in the LICENSE file that accompanied this code.
 10 #
 11 # This code is distributed in the hope that it will be useful, but WITHOUT
 12 # ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 13 # FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
 14 # version 2 for more details (a copy is included in the LICENSE file that
 15 # accompanied this code).
 16 #
 17 # You should have received a copy of the GNU General Public License version
 18 # 2 along with this work; if not, write to the Free Software Foundation,
 19 # Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA.
 20 #
 21 # Please contact Oracle, 500 Oracle Parkway, Redwood Shores, CA 94065 USA
 22 # or visit www.oracle.com if you need additional information or have any
 23 # questions.
 24 #
 25 
 26 m4_include([util_paths.m4])
 27 m4_include([util_windows.m4])
 28 
 29 ###############################################################################
 30 # Create a function/macro that takes a series of named arguments. The call is
 31 # similar to AC_DEFUN, but the setup of the function looks like this:
 32 # UTIL_DEFUN_NAMED([MYFUNC], [FOO *BAR], [$@], [
 33 # ... do something
 34 #   AC_MSG_NOTICE([Value of BAR is ARG_BAR])
 35 # ])
 36 # A star (*) in front of a named argument means that it is required and it&#39;s
 37 # presence will be verified. To pass e.g. the first value as a normal indexed
 38 # argument, use [m4_shift($@)] as the third argument instead of [$@]. These
 39 # arguments are referenced in the function by their name prefixed by ARG_, e.g.
 40 # &quot;ARG_FOO&quot;.
 41 #
 42 # The generated function can be called like this:
 43 # MYFUNC(FOO: [foo-val],
 44 #     BAR: [
 45 #         $ECHO hello world
 46 #     ])
 47 # Note that the argument value must start on the same line as the argument name.
 48 #
 49 # Argument 1: Name of the function to define
 50 # Argument 2: List of legal named arguments, with a * prefix for required arguments
 51 # Argument 3: Argument array to treat as named, typically $@
 52 # Argument 4: The main function body
 53 AC_DEFUN([UTIL_DEFUN_NAMED],
 54 [
 55   AC_DEFUN($1, [
 56     m4_foreach(arg, m4_split(m4_normalize($2)), [
 57       m4_if(m4_bregexp(arg, [^\*]), -1,
 58         [
 59           m4_set_add(legal_named_args, arg)
 60         ],
 61         [
 62           m4_set_add(legal_named_args, m4_substr(arg, 1))
 63           m4_set_add(required_named_args, m4_substr(arg, 1))
 64         ]
 65       )
 66     ])
 67 
 68     m4_foreach([arg], [$3], [
 69       m4_if(m4_bregexp(arg, [: ]), -1, m4_define([arg], m4_bpatsubst(arg, [:], [: ])))
 70       m4_define(arg_name, m4_substr(arg, 0, m4_bregexp(arg, [: ])))
 71       m4_set_contains(legal_named_args, arg_name, [],[AC_MSG_ERROR([Internal error: m4_if(arg_name, , arg, arg_name) is not a valid named argument to [$1]. Valid arguments are &#39;m4_set_contents(defined_args, [ ]) m4_set_contents(legal_named_args, [ ])&#39;.])])
 72       m4_set_remove(required_named_args, arg_name)
 73       m4_set_remove(legal_named_args, arg_name)
 74       m4_pushdef([ARG_][]arg_name, m4_bpatsubst(m4_substr(arg, m4_incr(m4_incr(m4_bregexp(arg, [: ])))), [^\s*], []))
 75       m4_set_add(defined_args, arg_name)
 76       m4_undefine([arg_name])
 77     ])
 78     m4_set_empty(required_named_args, [], [
 79       AC_MSG_ERROR([Internal error: Required named arguments are missing for [$1]. Missing arguments: &#39;m4_set_contents(required_named_args, [ ])&#39;])
 80     ])
 81     m4_foreach([arg], m4_indir([m4_dquote]m4_set_listc([legal_named_args])), [
 82       m4_pushdef([ARG_][]arg, [])
 83       m4_set_add(defined_args, arg)
 84     ])
 85     m4_set_delete(legal_named_args)
 86     m4_set_delete(required_named_args)
 87 
 88     # Execute function body
 89     $4
 90 
 91     m4_foreach([arg], m4_indir([m4_dquote]m4_set_listc([defined_args])), [
 92       m4_popdef([ARG_][]arg)
 93     ])
 94 
 95     m4_set_delete(defined_args)
 96   ])
 97 ])
 98 
 99 ###############################################################################
100 # Assert that a programmatic condition holds. If not, exit with an error message.
101 # Check that a shell expression gives return code 0
102 #
103 # $1: The shell expression to evaluate
104 # $2: A message to describe the expression in case of failure
105 # $2: An message to print in case of failure [optional]
106 #
107 AC_DEFUN([UTIL_ASSERT_SHELL_TEST],
108 [
109   ASSERTION_MSG=&quot;m4_normalize([$3])&quot;
110   if $1; then
111     $ECHO Assertion failed: $2
112     if test &quot;x$3&quot; != x; then
113       $ECHO Assertion message: &quot;$3&quot;
114     fi
115     exit 1
116   fi
117 ])
118 
119 
120 ###############################################################################
121 # Assert that a programmatic condition holds. If not, exit with an error message.
122 # Check that two strings are equal.
123 #
124 # $1: The actual string found
125 # $2: The expected string
126 # $3: An message to print in case of failure [optional]
127 #
128 AC_DEFUN([UTIL_ASSERT_STRING_EQUALS],
129 [
130   UTIL_ASSERT_SHELL_TEST(
131       [test &quot;x[$1]&quot; != &quot;x[$2]&quot;],
132       [Actual value &#39;[$1]&#39; \(&quot;[$1]&quot;\) did not match expected value &#39;[$2]&#39; \(&quot;[$2]&quot;\)],
133       $3)
134 ])
135 
136 ###############################################################################
137 # Assert that a programmatic condition holds. If not, exit with an error message.
138 # Check that two strings not are equal.
139 #
140 # $1: The actual string found
141 # $2: The expected string
142 # $3: An message to print in case of failure [optional]
143 #
144 AC_DEFUN([UTIL_ASSERT_STRING_NOT_EQUALS],
145 [
146   UTIL_ASSERT_SHELL_TEST(
147       [test &quot;x[$1]&quot; = &quot;x[$2]&quot;],
148       [Actual value &#39;[$1]&#39; \(&quot;[$1]&quot;\) unexpectedly matched &#39;[$2]&#39; \(&quot;[$2]&quot;\)],
149       $3)
150 ])
151 
152 ###############################################################################
153 # Assert that a programmatic condition holds. If not, exit with an error message.
154 # Check that the given expression evaluates to the string &#39;true&#39;
155 #
156 # $1: The expression to evaluate
157 # $2: An message to print in case of failure [optional]
158 #
159 AC_DEFUN([UTIL_ASSERT_TRUE],
160 [
161   UTIL_ASSERT_STRING_EQUALS($1, true, $3)
162 ])
163 
164 ###############################################################################
165 # Assert that a programmatic condition holds. If not, exit with an error message.
166 # Check that the given expression does not evaluate to the string &#39;true&#39;
167 #
168 # $1: The expression to evaluate
169 # $2: An message to print in case of failure [optional]
170 #
171 AC_DEFUN([UTIL_ASSERT_NOT_TRUE],
172 [
173   UTIL_ASSERT_STRING_NOT_EQUALS($1, true, $3)
174 ])
175 
176 ###############################################################################
177 # Check if a list of space-separated words are selected only from a list of
178 # space-separated legal words. Typical use is to see if a user-specified
179 # set of words is selected from a set of legal words.
180 #
181 # Sets the specified variable to list of non-matching (offending) words, or to
182 # the empty string if all words are matching the legal set.
183 #
184 # $1: result variable name
185 # $2: list of values to check
186 # $3: list of legal values
187 AC_DEFUN([UTIL_GET_NON_MATCHING_VALUES],
188 [
189   # grep filter function inspired by a comment to http://stackoverflow.com/a/1617326
190   # Notice that the original variant fails on SLES 10 and 11
191   # Some grep versions (at least bsd) behaves strangely on the base case with
192   # no legal_values, so make it explicit.
193   values_to_check=`$ECHO $2 | $TR &#39; &#39; &#39;\n&#39;`
194   legal_values=`$ECHO $3 | $TR &#39; &#39; &#39;\n&#39;`
195   if test -z &quot;$legal_values&quot;; then
196     $1=&quot;$2&quot;
197   else
198     result=`$GREP -Fvx &quot;$legal_values&quot; &lt;&lt;&lt; &quot;$values_to_check&quot; | $GREP -v &#39;^$&#39;`
199     $1=${result//$&#39;\n&#39;/ }
200   fi
201 ])
202 
203 ###############################################################################
204 # Check if a list of space-separated words contains any word(s) from a list of
205 # space-separated illegal words. Typical use is to see if a user-specified
206 # set of words contains any from a set of illegal words.
207 #
208 # Sets the specified variable to list of matching illegal words, or to
209 # the empty string if no words are matching the illegal set.
210 #
211 # $1: result variable name
212 # $2: list of values to check
213 # $3: list of illegal values
214 AC_DEFUN([UTIL_GET_MATCHING_VALUES],
215 [
216   # grep filter function inspired by a comment to http://stackoverflow.com/a/1617326
217   # Notice that the original variant fails on SLES 10 and 11
218   # Some grep versions (at least bsd) behaves strangely on the base case with
219   # no legal_values, so make it explicit.
220   values_to_check=`$ECHO $2 | $TR &#39; &#39; &#39;\n&#39;`
221   illegal_values=`$ECHO $3 | $TR &#39; &#39; &#39;\n&#39;`
222   if test -z &quot;$illegal_values&quot;; then
223     $1=&quot;&quot;
224   else
225     result=`$GREP -Fx &quot;$illegal_values&quot; &lt;&lt;&lt; &quot;$values_to_check&quot; | $GREP -v &#39;^$&#39;`
226     $1=${result//$&#39;\n&#39;/ }
227   fi
228 ])
229 
230 ###############################################################################
231 # Sort a space-separated list, and remove duplicates.
232 #
233 # Sets the specified variable to the resulting list.
234 #
235 # $1: result variable name
236 # $2: list of values to sort
237 AC_DEFUN([UTIL_SORT_LIST],
238 [
239   values_to_sort=`$ECHO $2 | $TR &#39; &#39; &#39;\n&#39;`
240   result=`$SORT -u &lt;&lt;&lt; &quot;$values_to_sort&quot; | $GREP -v &#39;^$&#39;`
241   $1=${result//$&#39;\n&#39;/ }
242 ])
243 
244 ###############################################################################
245 # Test if $1 is a valid argument to $3 (often is $JAVA passed as $3)
246 # If so, then append $1 to $2 \
247 # Also set JVM_ARG_OK to true/false depending on outcome.
248 AC_DEFUN([UTIL_ADD_JVM_ARG_IF_OK],
249 [
250   $ECHO &quot;Check if jvm arg is ok: $1&quot; &gt;&amp;AS_MESSAGE_LOG_FD
251   $ECHO &quot;Command: $3 $1 -version&quot; &gt;&amp;AS_MESSAGE_LOG_FD
252   OUTPUT=`$3 $1 $USER_BOOT_JDK_OPTIONS -version 2&gt;&amp;1`
253   FOUND_WARN=`$ECHO &quot;$OUTPUT&quot; | $GREP -i warn`
254   FOUND_VERSION=`$ECHO $OUTPUT | $GREP &quot; version \&quot;&quot;`
255   if test &quot;x$FOUND_VERSION&quot; != x &amp;&amp; test &quot;x$FOUND_WARN&quot; = x; then
256     $2=&quot;[$]$2 $1&quot;
257     JVM_ARG_OK=true
258   else
259     $ECHO &quot;Arg failed:&quot; &gt;&amp;AS_MESSAGE_LOG_FD
260     $ECHO &quot;$OUTPUT&quot; &gt;&amp;AS_MESSAGE_LOG_FD
261     JVM_ARG_OK=false
262   fi
263 ])
264 
265 ###############################################################################
266 # Register a --with argument but mark it as deprecated
267 # $1: The name of the with argument to deprecate, not including --with-
268 AC_DEFUN([UTIL_DEPRECATED_ARG_WITH],
269 [
270   AC_ARG_WITH($1, [AS_HELP_STRING([--with-$1],
271       [Deprecated. Option is kept for backwards compatibility and is ignored])],
272       [AC_MSG_WARN([Option --with-$1 is deprecated and will be ignored.])])
273 ])
274 
275 ###############################################################################
276 # Register a --enable argument but mark it as deprecated
277 # $1: The name of the with argument to deprecate, not including --enable-
278 # $2: The name of the argument to deprecate, in shell variable style (i.e. with _ instead of -)
279 # $3: Messages to user.
280 AC_DEFUN([UTIL_DEPRECATED_ARG_ENABLE],
281 [
282   AC_ARG_ENABLE($1, [AS_HELP_STRING([--enable-$1],
283       [Deprecated. Option is kept for backwards compatibility and is ignored])])
284   if test &quot;x$enable_$2&quot; != x; then
285     AC_MSG_WARN([Option --enable-$1 is deprecated and will be ignored.])
286 
287     if test &quot;x$3&quot; != x; then
288       AC_MSG_WARN([$3])
289     fi
290 
291   fi
292 ])
293 
294 ###############################################################################
295 # Register an --enable-* argument as an alias for another argument.
296 # $1: The name of the enable argument for the new alias, not including --enable-
297 # $2: The full name of the argument of which to make this an alias, including
298 #     --enable- or --with-.
299 AC_DEFUN([UTIL_ALIASED_ARG_ENABLE],
300 [
301   AC_ARG_ENABLE($1, [AS_HELP_STRING([--enable-$1], [alias for $2])], [
302     # Use m4 to strip initial -- from target ($2), convert - to _, prefix enable_
303     # to new alias name, and create a shell variable assignment,
304     # e.g.: enable_old_style=&quot;$enable_new_alias&quot;
305     m4_translit(m4_bpatsubst($2, --), -, _)=&quot;$[enable_]m4_translit($1, -, _)&quot;
306   ])
307 ])
308 
309 ###############################################################################
310 # Creates a command-line option using the --enable-* pattern. Will return a
311 # value of &#39;true&#39; or &#39;false&#39; in the RESULT variable, depending on whether the
312 # option was enabled or not by the user. The option can not be turned on if it
313 # is not available, as specified by AVAILABLE and/or AVAILABLE_CHECK.
314 #
315 # Arguments:
316 #   NAME: The base name of this option (i.e. what follows --enable-). Required.
317 #   RESULT: The name of the variable to set to the result. Defaults to
318 #     &lt;NAME in uppercase&gt;_RESULT.
319 #   DEFAULT: The default value for this option. Can be true, false or auto.
320 #     Defaults to true.
321 #   AVAILABLE: If true, this option is allowed to be selected. Defaults to true.
322 #   DESC: A description of this option. Defaults to a generic and unhelpful
323 #     string.
324 #   DEFAULT_DESC: A message describing the default value, for the help. Defaults
325 #     to the literal value of DEFAULT.
326 #   CHECKING_MSG: The message to present to user when checking this option.
327 #     Defaults to a generic message.
328 #   CHECK_AVAILABLE: An optional code block to execute to determine if the
329 #     option should be available. Must set AVAILABLE to &#39;false&#39; if not.
330 #   IF_GIVEN:  An optional code block to execute if the option was given on the
331 #     command line (regardless of the value).
332 #   IF_ENABLED:  An optional code block to execute if the option is turned on.
333 #   IF_DISABLED:  An optional code block to execute if the option is turned off.
334 #
335 UTIL_DEFUN_NAMED([UTIL_ARG_ENABLE],
336     [*NAME RESULT DEFAULT AVAILABLE DESC DEFAULT_DESC CHECKING_MSG
337     CHECK_AVAILABLE IF_GIVEN IF_ENABLED IF_DISABLED], [$@],
338 [
339   ##########################
340   # Part 1: Set up m4 macros
341   ##########################
342 
343   # If DEFAULT is not specified, set it to &#39;true&#39;.
344   m4_define([ARG_DEFAULT], m4_if(ARG_DEFAULT, , true, ARG_DEFAULT))
345 
346   # If AVAILABLE is not specified, set it to &#39;true&#39;.
347   m4_define([ARG_AVAILABLE], m4_if(ARG_AVAILABLE, , true, ARG_AVAILABLE))
348 
349   # If DEFAULT_DESC is not specified, calculate it from DEFAULT.
350   m4_define([ARG_DEFAULT_DESC], m4_if(ARG_DEFAULT_DESC, , m4_if(ARG_DEFAULT, true, enabled, m4_if(ARG_DEFAULT, false, disabled, ARG_DEFAULT)), ARG_DEFAULT_DESC))
351 
352   # If RESULT is not specified, set it to &#39;ARG_NAME[_ENABLED]&#39;.
353   m4_define([ARG_RESULT], m4_if(ARG_RESULT, , m4_translit(ARG_NAME, [a-z-], [A-Z_])[_ENABLED], ARG_RESULT))
354   # Construct shell variable names for the option
355   m4_define(ARG_OPTION, [enable_]m4_translit(ARG_NAME, [-], [_]))
356   m4_define(ARG_GIVEN, m4_translit(ARG_NAME, [a-z-], [A-Z_])[_GIVEN])
357 
358   # If DESC is not specified, set it to a generic description.
359   m4_define([ARG_DESC], m4_if(ARG_DESC, , [Enable the ARG_NAME feature], m4_normalize(ARG_DESC)))
360 
361   # If CHECKING_MSG is not specified, set it to a generic description.
362   m4_define([ARG_CHECKING_MSG], m4_if(ARG_CHECKING_MSG, , [for --enable-ARG_NAME], ARG_CHECKING_MSG))
363 
364   # If the code blocks are not given, set them to the empty statements to avoid
365   # tripping up bash.
366   m4_define([ARG_CHECK_AVAILABLE], m4_if(ARG_CHECK_AVAILABLE, , :, ARG_CHECK_AVAILABLE))
367   m4_define([ARG_IF_GIVEN], m4_if(ARG_IF_GIVEN, , :, ARG_IF_GIVEN))
368   m4_define([ARG_IF_ENABLED], m4_if(ARG_IF_ENABLED, , :, ARG_IF_ENABLED))
369   m4_define([ARG_IF_DISABLED], m4_if(ARG_IF_DISABLED, , :, ARG_IF_DISABLED))
370 
371   ##########################
372   # Part 2: Set up autoconf shell code
373   ##########################
374 
375   # Check that DEFAULT has a valid value
376   if test &quot;[x]ARG_DEFAULT&quot; != xtrue &amp;&amp; test &quot;[x]ARG_DEFAULT&quot; != xfalse &amp;&amp; \
377       test &quot;[x]ARG_DEFAULT&quot; != xauto ; then
378     AC_MSG_ERROR([Internal error: Argument DEFAULT to [UTIL_ARG_ENABLE] can only be true, false or auto, was: &#39;ARG_DEFAULT&#39;])
379   fi
380 
381   # Check that AVAILABLE has a valid value
382   if test &quot;[x]ARG_AVAILABLE&quot; != xtrue &amp;&amp; test &quot;[x]ARG_AVAILABLE&quot; != xfalse; then
383     AC_MSG_ERROR([Internal error: Argument AVAILABLE to [UTIL_ARG_ENABLE] can only be true or false, was: &#39;ARG_AVAILABLE&#39;])
384   fi
385 
386   AC_ARG_ENABLE(ARG_NAME, AS_HELP_STRING([--enable-]ARG_NAME,
387       [ARG_DESC [ARG_DEFAULT_DESC]]), [ARG_GIVEN=true], [ARG_GIVEN=false])
388 
389   # Check if the option is available
390   AVAILABLE=ARG_AVAILABLE
391   # Run the available check block (if any), which can overwrite AVAILABLE.
392   ARG_CHECK_AVAILABLE
393 
394   # Check if the option should be turned on
395   AC_MSG_CHECKING(ARG_CHECKING_MSG)
396   if test x$ARG_GIVEN = xfalse; then
397     if test ARG_DEFAULT = auto; then
398       # If not given, and default is auto, set it to true iff it&#39;s available.
399       ARG_RESULT=$AVAILABLE
400       REASON=&quot;from default &#39;auto&#39;&quot;
401     else
402       ARG_RESULT=ARG_DEFAULT
403       REASON=&quot;default&quot;
404     fi
405   else
406     if test x$ARG_OPTION = xyes; then
407       ARG_RESULT=true
408       REASON=&quot;from command line&quot;
409     elif test x$ARG_OPTION = xno; then
410       ARG_RESULT=false
411       REASON=&quot;from command line&quot;
412     elif test x$ARG_OPTION = xauto; then
413       if test ARG_DEFAULT = auto; then
414         # If both given and default is auto, set it to true iff it&#39;s available.
415         ARG_RESULT=$AVAILABLE
416       else
417         ARG_RESULT=ARG_DEFAULT
418       fi
419       REASON=&quot;from command line &#39;auto&#39;&quot;
420     else
421       AC_MSG_ERROR([Option [--enable-]ARG_NAME can only be &#39;yes&#39;, &#39;no&#39; or &#39;auto&#39;])
422     fi
423   fi
424 
425   if test x$ARG_RESULT = xtrue; then
426     AC_MSG_RESULT([enabled, $REASON])
427     if test x$AVAILABLE = xfalse; then
428       AC_MSG_ERROR([Option [--enable-]ARG_NAME is not available])
429     fi
430   else
431     AC_MSG_RESULT([disabled, $REASON])
432   fi
433 
434   # Execute result payloads, if present
435   if test x$ARG_GIVEN = xtrue; then
436     ARG_IF_GIVEN
437   fi
438 
439   if test x$ARG_RESULT = xtrue; then
440     ARG_IF_ENABLED
441   else
442     ARG_IF_DISABLED
443   fi
444 ])
445 
446 ###############################################################################
447 # Test that variable $1 denoting a program is not empty. If empty, exit with an error.
448 # $1: variable to check
449 AC_DEFUN([UTIL_CHECK_NONEMPTY],
450 [
451   if test &quot;x[$]$1&quot; = x; then
452     AC_MSG_ERROR([Could not find required tool for $1])
453   fi
454 ])
455 
456 ###############################################################################
457 # Setup a tool for the given variable. If correctly specified by the user,
458 # use that value, otherwise search for the tool using the supplied code snippet.
459 # $1: variable to set
460 # $2: code snippet to call to look for the tool
461 # $3: code snippet to call if variable was used to find tool
462 AC_DEFUN([UTIL_SETUP_TOOL],
463 [
464   # Publish this variable in the help.
465   AC_ARG_VAR($1, [Override default value for $1])
466 
467   if [[ -z &quot;${$1+x}&quot; ]]; then
468     # The variable is not set by user, try to locate tool using the code snippet
469     $2
470   else
471     # The variable is set, but is it from the command line or the environment?
472 
473     # Try to remove the string !$1! from our list.
474     try_remove_var=${CONFIGURE_OVERRIDDEN_VARIABLES//!$1!/}
475     if test &quot;x$try_remove_var&quot; = &quot;x$CONFIGURE_OVERRIDDEN_VARIABLES&quot;; then
476       # If it failed, the variable was not from the command line. Ignore it,
477       # but warn the user (except for BASH, which is always set by the calling BASH).
478       if test &quot;x$1&quot; != xBASH; then
479         AC_MSG_WARN([Ignoring value of $1 from the environment. Use command line variables instead.])
480       fi
481       # Try to locate tool using the code snippet
482       $2
483     else
484       # If it succeeded, then it was overridden by the user. We will use it
485       # for the tool.
486 
487       # First remove it from the list of overridden variables, so we can test
488       # for unknown variables in the end.
489       CONFIGURE_OVERRIDDEN_VARIABLES=&quot;$try_remove_var&quot;
490 
491       tool_override=[$]$1
492       AC_MSG_NOTICE([User supplied override $1=&quot;$tool_override&quot;])
493 
494       # Check if we try to supply an empty value
495       if test &quot;x$tool_override&quot; = x; then
496         AC_MSG_CHECKING([for $1])
497         AC_MSG_RESULT([disabled])
498       else
499         # Split up override in command part and argument part
500         tool_and_args=($tool_override)
501         [ tool_command=${tool_and_args[0]} ]
502         [ unset &#39;tool_and_args[0]&#39; ]
503         [ tool_args=${tool_and_args[@]} ]
504 
505         # Check if the provided tool contains a complete path.
506         tool_basename=&quot;${tool_command##*/}&quot;
507         if test &quot;x$tool_basename&quot; = &quot;x$tool_command&quot;; then
508           # A command without a complete path is provided, search $PATH.
509           AC_MSG_NOTICE([Will search for user supplied tool &quot;$tool_basename&quot;])
510           AC_PATH_PROG($1, $tool_basename)
511           if test &quot;x[$]$1&quot; = x; then
512             AC_MSG_ERROR([User supplied tool $1=&quot;$tool_basename&quot; could not be found])
513           fi
514         else
515           # Otherwise we believe it is a complete path. Use it as it is.
516           AC_MSG_NOTICE([Will use user supplied tool &quot;$tool_command&quot;])
517           AC_MSG_CHECKING([for $tool_command])
518           if test ! -x &quot;$tool_command&quot;; then
519             AC_MSG_RESULT([not found])
520             AC_MSG_ERROR([User supplied tool $1=&quot;$tool_command&quot; does not exist or is not executable])
521           fi
522            $1=&quot;$tool_command&quot;
523           AC_MSG_RESULT([found])
524         fi
525         if test &quot;x$tool_args&quot; != x; then
526           # If we got arguments, re-append them to the command after the fixup.
527           $1=&quot;[$]$1 $tool_args&quot;
528         fi
529       fi
530     fi
531     $3
532   fi
533 ])
534 
535 ###############################################################################
536 # Call UTIL_SETUP_TOOL with AC_PATH_PROGS to locate the tool
537 # $1: variable to set
538 # $2: executable name (or list of names) to look for
539 # $3: [path]
540 AC_DEFUN([UTIL_PATH_PROGS],
541 [
542   UTIL_SETUP_TOOL($1, [AC_PATH_PROGS($1, $2, , $3)])
543 ])
544 
545 ###############################################################################
546 # Call UTIL_SETUP_TOOL with AC_CHECK_TOOLS to locate the tool
547 # $1: variable to set
548 # $2: executable name (or list of names) to look for
549 AC_DEFUN([UTIL_CHECK_TOOLS],
550 [
551   UTIL_SETUP_TOOL($1, [AC_CHECK_TOOLS($1, $2)])
552 ])
553 
554 ###############################################################################
555 # Like UTIL_PATH_PROGS but fails if no tool was found.
556 # $1: variable to set
557 # $2: executable name (or list of names) to look for
558 # $3: [path]
559 AC_DEFUN([UTIL_REQUIRE_PROGS],
560 [
561   UTIL_PATH_PROGS($1, $2, , $3)
562   UTIL_CHECK_NONEMPTY($1)
563 ])
564 
565 ###############################################################################
566 # Like UTIL_SETUP_TOOL but fails if no tool was found.
567 # $1: variable to set
568 # $2: autoconf macro to call to look for the special tool
569 AC_DEFUN([UTIL_REQUIRE_SPECIAL],
570 [
571   UTIL_SETUP_TOOL($1, [$2])
572   UTIL_CHECK_NONEMPTY($1)
573 ])
574 
575 ###############################################################################
576 # Like UTIL_REQUIRE_PROGS but also allows for bash built-ins
577 # $1: variable to set
578 # $2: executable name (or list of names) to look for
579 # $3: [path]
580 AC_DEFUN([UTIL_REQUIRE_BUILTIN_PROGS],
581 [
582   UTIL_SETUP_TOOL($1, [AC_PATH_PROGS($1, $2, , $3)])
583   if test &quot;x[$]$1&quot; = x; then
584     AC_MSG_NOTICE([Required tool $2 not found in PATH, checking built-in])
585     if help $2 &gt; /dev/null 2&gt;&amp;1; then
586       AC_MSG_NOTICE([Found $2 as shell built-in. Using it])
587       $1=&quot;$2&quot;
588     else
589       AC_MSG_ERROR([Required tool $2 also not found as built-in.])
590     fi
591   fi
592   UTIL_CHECK_NONEMPTY($1)
593 ])
    </pre>
  </body>
</html>