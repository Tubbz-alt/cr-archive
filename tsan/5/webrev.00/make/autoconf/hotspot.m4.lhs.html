<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <title>Frames make/autoconf/hotspot.m4</title>
    <link rel="stylesheet" href="../../style.css" />
    <script type="text/javascript" src="../../navigation.js"></script>
  </head>
<body onkeypress="keypress(event);">
<a name="0"></a>
<hr />
<pre>  1 #
  2 # Copyright (c) 2011, 2019, Oracle and/or its affiliates. All rights reserved.
  3 # DO NOT ALTER OR REMOVE COPYRIGHT NOTICES OR THIS FILE HEADER.
  4 #
  5 # This code is free software; you can redistribute it and/or modify it
  6 # under the terms of the GNU General Public License version 2 only, as
  7 # published by the Free Software Foundation.  Oracle designates this
  8 # particular file as subject to the &quot;Classpath&quot; exception as provided
  9 # by Oracle in the LICENSE file that accompanied this code.
 10 #
 11 # This code is distributed in the hope that it will be useful, but WITHOUT
 12 # ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
 13 # FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
 14 # version 2 for more details (a copy is included in the LICENSE file that
 15 # accompanied this code).
 16 #
 17 # You should have received a copy of the GNU General Public License version
 18 # 2 along with this work; if not, write to the Free Software Foundation,
 19 # Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA.
 20 #
 21 # Please contact Oracle, 500 Oracle Parkway, Redwood Shores, CA 94065 USA
 22 # or visit www.oracle.com if you need additional information or have any
 23 # questions.
 24 #
 25 
 26 # All valid JVM features, regardless of platform
 27 VALID_JVM_FEATURES=&quot;compiler1 compiler2 zero minimal dtrace jvmti jvmci \
 28     graal vm-structs jni-check services management epsilongc g1gc parallelgc serialgc shenandoahgc zgc nmt cds \
 29     static-build link-time-opt aot jfr tsan&quot;
 30 
 31 # Deprecated JVM features (these are ignored, but with a warning)
 32 DEPRECATED_JVM_FEATURES=&quot;trace cmsgc&quot;
 33 
 34 # All valid JVM variants
 35 VALID_JVM_VARIANTS=&quot;server client minimal core zero custom&quot;
 36 
 37 ###############################################################################
 38 # Check if the specified JVM variant should be built. To be used in shell if
 39 # constructs, like this:
 40 # if HOTSPOT_CHECK_JVM_VARIANT(server); then
 41 #
 42 # Only valid to use after HOTSPOT_SETUP_JVM_VARIANTS has setup variants.
 43 
 44 # Definition kept in one line to allow inlining in if statements.
 45 # Additional [] needed to keep m4 from mangling shell constructs.
 46 AC_DEFUN([HOTSPOT_CHECK_JVM_VARIANT],
 47 [ [ [[ &quot; $JVM_VARIANTS &quot; =~ &quot; $1 &quot; ]] ] ])
 48 
 49 ###############################################################################
 50 # Check if the specified JVM feature is enabled. To be used in shell if
 51 # constructs, like this:
 52 # if HOTSPOT_CHECK_JVM_FEATURE(jvmti); then
 53 #
 54 # Only valid to use after HOTSPOT_SETUP_JVM_FEATURES has setup features.
 55 
 56 # Definition kept in one line to allow inlining in if statements.
 57 # Additional [] needed to keep m4 from mangling shell constructs.
 58 AC_DEFUN([HOTSPOT_CHECK_JVM_FEATURE],
 59 [ [ [[ &quot; $JVM_FEATURES &quot; =~ &quot; $1 &quot; ]] ] ])
 60 
 61 ###############################################################################
 62 # Check if the specified JVM feature is explicitly disabled. To be used in
 63 # shell if constructs, like this:
 64 # if HOTSPOT_IS_JVM_FEATURE_DISABLED(jvmci); then
 65 #
 66 # This function is internal to hotspot.m4, and is only used when constructing
 67 # the valid set of enabled JVM features. Users outside of hotspot.m4 should just
 68 # use HOTSPOT_CHECK_JVM_FEATURE to check if a feature is enabled or not.
 69 
 70 # Definition kept in one line to allow inlining in if statements.
 71 # Additional [] needed to keep m4 from mangling shell constructs.
 72 AC_DEFUN([HOTSPOT_IS_JVM_FEATURE_DISABLED],
 73 [ [ [[ &quot; $DISABLED_JVM_FEATURES &quot; =~ &quot; $1 &quot; ]] ] ])
 74 
 75 ###############################################################################
 76 # Check which variants of the JVM that we want to build. Available variants are:
 77 #   server: normal interpreter, and a tiered C1/C2 compiler
 78 #   client: normal interpreter, and C1 (no C2 compiler)
 79 #   minimal: reduced form of client with optional features stripped out
 80 #   core: normal interpreter only, no compiler
 81 #   zero: C++ based interpreter only, no compiler
 82 #   custom: baseline JVM with no default features
 83 #
 84 AC_DEFUN_ONCE([HOTSPOT_SETUP_JVM_VARIANTS],
 85 [
 86   AC_ARG_WITH([jvm-variants], [AS_HELP_STRING([--with-jvm-variants],
 87       [JVM variants (separated by commas) to build (server,client,minimal,core,zero,custom) @&lt;:@server@:&gt;@])])
 88 
 89   if test &quot;x$with_jvm_variants&quot; = x; then
 90     with_jvm_variants=&quot;server&quot;
 91   fi
 92   JVM_VARIANTS_OPT=&quot;$with_jvm_variants&quot;
 93 
 94   # Has the user listed more than one variant?
 95   # Additional [] needed to keep m4 from mangling shell constructs.
 96   if [ [[ &quot;$JVM_VARIANTS_OPT&quot; =~ &quot;,&quot; ]] ]; then
 97     BUILDING_MULTIPLE_JVM_VARIANTS=true
 98   else
 99     BUILDING_MULTIPLE_JVM_VARIANTS=false
100   fi
101   # Replace the commas with AND for use in the build directory name.
102   JVM_VARIANTS_WITH_AND=`$ECHO &quot;$JVM_VARIANTS_OPT&quot; | $SED -e &#39;s/,/AND/g&#39;`
103 
104   AC_MSG_CHECKING([which variants of the JVM to build])
105   # JVM_VARIANTS is a space-separated list.
106   # Also use minimal, not minimal1 (which is kept for backwards compatibility).
107   JVM_VARIANTS=`$ECHO $JVM_VARIANTS_OPT | $SED -e &#39;s/,/ /g&#39; -e &#39;s/minimal1/minimal/&#39;`
108   AC_MSG_RESULT([$JVM_VARIANTS])
109 
110   # Check that the selected variants are valid
111   BASIC_GET_NON_MATCHING_VALUES(INVALID_VARIANTS, $JVM_VARIANTS, $VALID_JVM_VARIANTS)
112   if test &quot;x$INVALID_VARIANTS&quot; != x; then
113     AC_MSG_NOTICE([Unknown variant(s) specified: &quot;$INVALID_VARIANTS&quot;])
114     AC_MSG_NOTICE([The available JVM variants are: &quot;$VALID_JVM_VARIANTS&quot;])
115     AC_MSG_ERROR([Cannot continue])
116   fi
117 
118   # All &quot;special&quot; variants share the same output directory (&quot;server&quot;)
119   VALID_MULTIPLE_JVM_VARIANTS=&quot;server client minimal&quot;
120   BASIC_GET_NON_MATCHING_VALUES(INVALID_MULTIPLE_VARIANTS, $JVM_VARIANTS, $VALID_MULTIPLE_JVM_VARIANTS)
121   if  test &quot;x$INVALID_MULTIPLE_VARIANTS&quot; != x &amp;&amp; test &quot;x$BUILDING_MULTIPLE_JVM_VARIANTS&quot; = xtrue; then
122     AC_MSG_ERROR([You cannot build multiple variants with anything else than $VALID_MULTIPLE_JVM_VARIANTS.])
123   fi
124 
125   # The &quot;main&quot; variant is the one used by other libs to link against during the
126   # build.
127   if test &quot;x$BUILDING_MULTIPLE_JVM_VARIANTS&quot; = &quot;xtrue&quot;; then
128     MAIN_VARIANT_PRIO_ORDER=&quot;server client minimal&quot;
129     for variant in $MAIN_VARIANT_PRIO_ORDER; do
130       if HOTSPOT_CHECK_JVM_VARIANT($variant); then
131         JVM_VARIANT_MAIN=&quot;$variant&quot;
132         break
133       fi
134     done
135   else
136     JVM_VARIANT_MAIN=&quot;$JVM_VARIANTS&quot;
137   fi
138 
139   AC_SUBST(JVM_VARIANTS)
140   AC_SUBST(VALID_JVM_VARIANTS)
141   AC_SUBST(JVM_VARIANT_MAIN)
142 
143   if HOTSPOT_CHECK_JVM_VARIANT(zero); then
144     # zero behaves as a platform and rewrites these values. This is really weird. :(
145     # We are guaranteed that we do not build any other variants when building zero.
146     HOTSPOT_TARGET_CPU=zero
147     HOTSPOT_TARGET_CPU_ARCH=zero
148   fi
149 ])
150 
151 ###############################################################################
152 # Check if dtrace should be enabled and has all prerequisites present.
153 #
154 AC_DEFUN_ONCE([HOTSPOT_SETUP_DTRACE],
155 [
156   # Test for dtrace dependencies
157   AC_ARG_ENABLE([dtrace], [AS_HELP_STRING([--enable-dtrace@&lt;:@=yes/no/auto@:&gt;@],
158       [enable dtrace. Default is auto, where dtrace is enabled if all dependencies
159       are present.])])
160 
161   DTRACE_DEP_MISSING=false
162 
163   AC_MSG_CHECKING([for dtrace tool])
164   if test &quot;x$DTRACE&quot; != &quot;x&quot; &amp;&amp; test -x &quot;$DTRACE&quot;; then
165     AC_MSG_RESULT([$DTRACE])
166   else
167     AC_MSG_RESULT([not found, cannot build dtrace])
168     DTRACE_DEP_MISSING=true
169   fi
170 
171   AC_CHECK_HEADERS([sys/sdt.h], [DTRACE_HEADERS_OK=yes],[DTRACE_HEADERS_OK=no])
172   if test &quot;x$DTRACE_HEADERS_OK&quot; != &quot;xyes&quot;; then
173     DTRACE_DEP_MISSING=true
174   fi
175 
176   AC_MSG_CHECKING([if dtrace should be built])
177   if test &quot;x$enable_dtrace&quot; = &quot;xyes&quot;; then
178     if test &quot;x$DTRACE_DEP_MISSING&quot; = &quot;xtrue&quot;; then
179       AC_MSG_RESULT([no, missing dependencies])
180       HELP_MSG_MISSING_DEPENDENCY([dtrace])
181       AC_MSG_ERROR([Cannot enable dtrace with missing dependencies. See above. $HELP_MSG])
182     else
183       INCLUDE_DTRACE=true
184       AC_MSG_RESULT([yes, forced])
185     fi
186   elif test &quot;x$enable_dtrace&quot; = &quot;xno&quot;; then
187     INCLUDE_DTRACE=false
188     AC_MSG_RESULT([no, forced])
189   elif test &quot;x$enable_dtrace&quot; = &quot;xauto&quot; || test &quot;x$enable_dtrace&quot; = &quot;x&quot;; then
190     if test &quot;x$DTRACE_DEP_MISSING&quot; = &quot;xtrue&quot;; then
191       INCLUDE_DTRACE=false
192       AC_MSG_RESULT([no, missing dependencies])
193     else
194       INCLUDE_DTRACE=true
195       AC_MSG_RESULT([yes, dependencies present])
196     fi
197   else
198     AC_MSG_ERROR([Invalid value for --enable-dtrace: $enable_dtrace])
199   fi
200 ])
201 
202 ################################################################################
203 # Check if AOT should be enabled
204 #
205 AC_DEFUN_ONCE([HOTSPOT_ENABLE_DISABLE_AOT],
206 [
207   AC_ARG_ENABLE([aot], [AS_HELP_STRING([--enable-aot@&lt;:@=yes/no/auto@:&gt;@],
208       [enable ahead of time compilation feature. Default is auto, where aot is enabled if all dependencies are present.])])
209 
210   if test &quot;x$enable_aot&quot; = &quot;x&quot; || test &quot;x$enable_aot&quot; = &quot;xauto&quot;; then
211     ENABLE_AOT=&quot;true&quot;
212   elif test &quot;x$enable_aot&quot; = &quot;xyes&quot;; then
213     ENABLE_AOT=&quot;true&quot;
214   elif test &quot;x$enable_aot&quot; = &quot;xno&quot;; then
215     ENABLE_AOT=&quot;false&quot;
216   else
217     AC_MSG_ERROR([Invalid value for --enable-aot: $enable_aot])
218   fi
219 
220   if test &quot;x$ENABLE_AOT&quot; = &quot;xtrue&quot;; then
221     # Only enable AOT on X64 platforms.
222     if test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xx86_64&quot; || test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xaarch64&quot; ; then
223       if test -e &quot;${TOPDIR}/src/jdk.aot&quot;; then
224         if test -e &quot;${TOPDIR}/src/jdk.internal.vm.compiler&quot;; then
225           ENABLE_AOT=&quot;true&quot;
226         else
227           ENABLE_AOT=&quot;false&quot;
228           if test &quot;x$enable_aot&quot; = &quot;xyes&quot;; then
229             AC_MSG_ERROR([Cannot build AOT without src/jdk.internal.vm.compiler sources. Remove --enable-aot.])
230           fi
231         fi
232       else
233         ENABLE_AOT=&quot;false&quot;
234         if test &quot;x$enable_aot&quot; = &quot;xyes&quot;; then
235           AC_MSG_ERROR([Cannot build AOT without src/jdk.aot sources. Remove --enable-aot.])
236         fi
237       fi
238     else
239       ENABLE_AOT=&quot;false&quot;
240       if test &quot;x$enable_aot&quot; = &quot;xyes&quot;; then
241         AC_MSG_ERROR([AOT is currently only supported on x86_64 and aarch64. Remove --enable-aot.])
242       fi
243     fi
244   fi
245 
246   AC_SUBST(ENABLE_AOT)
247 ])
248 
249 ################################################################################
250 # Allow to disable CDS
251 #
252 AC_DEFUN_ONCE([HOTSPOT_ENABLE_DISABLE_CDS],
253 [
254   AC_ARG_ENABLE([cds], [AS_HELP_STRING([--enable-cds@&lt;:@=yes/no/auto@:&gt;@],
255       [enable class data sharing feature in non-minimal VM. Default is auto, where cds is enabled if supported on the platform.])])
256 
257   if test &quot;x$enable_cds&quot; = &quot;x&quot; || test &quot;x$enable_cds&quot; = &quot;xauto&quot;; then
258     ENABLE_CDS=&quot;true&quot;
259   elif test &quot;x$enable_cds&quot; = &quot;xyes&quot;; then
260     ENABLE_CDS=&quot;true&quot;
261   elif test &quot;x$enable_cds&quot; = &quot;xno&quot;; then
262     ENABLE_CDS=&quot;false&quot;
263   else
264     AC_MSG_ERROR([Invalid value for --enable-cds: $enable_cds])
265   fi
266 
267   AC_SUBST(ENABLE_CDS)
268 ])
269 
270 ###############################################################################
271 # Set up all JVM features for each JVM variant.
272 #
273 AC_DEFUN_ONCE([HOTSPOT_SETUP_JVM_FEATURES],
274 [
275   # Prettify the VALID_JVM_FEATURES string
276   BASIC_SORT_LIST(VALID_JVM_FEATURES, $VALID_JVM_FEATURES)
277 
278   # The user can in some cases supply additional jvm features. For the custom
279   # variant, this defines the entire variant.
280   AC_ARG_WITH([jvm-features], [AS_HELP_STRING([--with-jvm-features],
281       [JVM features to enable (foo) or disable (-foo), separated by comma. Use &#39;--help&#39; to show possible values @&lt;:@none@:&gt;@])])
282   if test &quot;x$with_jvm_features&quot; != x; then
283     AC_MSG_CHECKING([user specified JVM feature list])
284     USER_JVM_FEATURE_LIST=`$ECHO $with_jvm_features | $SED -e &#39;s/,/ /g&#39;`
285     AC_MSG_RESULT([$user_jvm_feature_list])
286     # These features will be added to all variant defaults
287     JVM_FEATURES=`$ECHO $USER_JVM_FEATURE_LIST | $AWK &#39;{ for (i=1; i&lt;=NF; i++) if (!match($i, /^-.*/)) printf(&quot;%s &quot;, $i) }&#39;`
288     # These features will be removed from all variant defaults
289     DISABLED_JVM_FEATURES=`$ECHO $USER_JVM_FEATURE_LIST | $AWK &#39;{ for (i=1; i&lt;=NF; i++) if (match($i, /^-.*/)) printf(&quot;%s &quot;, substr($i, 2))}&#39;`
290 
291     # Verify that the user has provided valid features
292     BASIC_GET_NON_MATCHING_VALUES(INVALID_FEATURES, $JVM_FEATURES $DISABLED_JVM_FEATURES, $VALID_JVM_FEATURES $DEPRECATED_JVM_FEATURES)
293     if test &quot;x$INVALID_FEATURES&quot; != x; then
294       AC_MSG_NOTICE([Unknown JVM features specified: &quot;$INVALID_FEATURES&quot;])
295       AC_MSG_NOTICE([The available JVM features are: &quot;$VALID_JVM_FEATURES&quot;])
296       AC_MSG_ERROR([Cannot continue])
297     fi
298 
299     # Check if the user has provided deprecated features
300     BASIC_GET_MATCHING_VALUES(DEPRECATED_FEATURES, $JVM_FEATURES $DISABLED_JVM_FEATURES, $DEPRECATED_JVM_FEATURES)
301     if test &quot;x$DEPRECATED_FEATURES&quot; != x; then
302       AC_MSG_WARN([Deprecated JVM features specified (will be ignored): &quot;$DEPRECATED_FEATURES&quot;])
303       # Filter out deprecated features
304       BASIC_GET_NON_MATCHING_VALUES(JVM_FEATURES, $JVM_FEATURES, $DEPRECATED_FEATURES)
305       BASIC_GET_NON_MATCHING_VALUES(DISABLED_JVM_FEATURES, $DISABLED_JVM_FEATURES, $DEPRECATED_FEATURES)
306     fi
307 
308   fi
309 
310   # Override hotspot cpu definitions for ARM platforms
311   if test &quot;x$OPENJDK_TARGET_CPU&quot; = xarm; then
312     HOTSPOT_TARGET_CPU=arm_32
313     HOTSPOT_TARGET_CPU_DEFINE=&quot;ARM32&quot;
314   fi
315 
316   # Verify that dependencies are met for explicitly set features.
317   if HOTSPOT_CHECK_JVM_FEATURE(jvmti) &amp;&amp; ! HOTSPOT_CHECK_JVM_FEATURE(services); then
318     AC_MSG_ERROR([Specified JVM feature &#39;jvmti&#39; requires feature &#39;services&#39;])
319   fi
320 
321   if HOTSPOT_CHECK_JVM_FEATURE(management) &amp;&amp; ! HOTSPOT_CHECK_JVM_FEATURE(nmt); then
322     AC_MSG_ERROR([Specified JVM feature &#39;management&#39; requires feature &#39;nmt&#39;])
323   fi
324 
325   if HOTSPOT_CHECK_JVM_FEATURE(jvmci) &amp;&amp; ! (HOTSPOT_CHECK_JVM_FEATURE(compiler1) || HOTSPOT_CHECK_JVM_FEATURE(compiler2)); then
326     AC_MSG_ERROR([Specified JVM feature &#39;jvmci&#39; requires feature &#39;compiler2&#39; or &#39;compiler1&#39;])
327   fi
328 
329   # Enable JFR by default, except for Zero, linux-sparcv9 and on minimal.
330   if ! HOTSPOT_CHECK_JVM_VARIANT(zero); then
331     if test &quot;x$OPENJDK_TARGET_OS&quot; != xaix; then
332       if test &quot;x$OPENJDK_TARGET_OS&quot; != xlinux || test &quot;x$OPENJDK_TARGET_CPU&quot; != xsparcv9; then
333         NON_MINIMAL_FEATURES=&quot;$NON_MINIMAL_FEATURES jfr&quot;
334       fi
335     fi
336   fi
337 
338   # Only enable Shenandoah on supported arches
339   AC_MSG_CHECKING([if shenandoah can be built])
340   if test &quot;x$OPENJDK_TARGET_CPU_ARCH&quot; = &quot;xx86&quot; || test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xaarch64&quot; ; then
341     AC_MSG_RESULT([yes])
342   else
343     DISABLED_JVM_FEATURES=&quot;$DISABLED_JVM_FEATURES shenandoahgc&quot;
344     AC_MSG_RESULT([no, platform not supported])
345   fi
346 
347   # Only enable ZGC on supported platforms
348   if (test &quot;x$OPENJDK_TARGET_OS&quot; = &quot;xwindows&quot; &amp;&amp; test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xx86_64&quot;); then
349     AC_MSG_CHECKING([if zgc can be built on windows])
350     AC_COMPILE_IFELSE(
351       [AC_LANG_PROGRAM([[#include &lt;windows.h&gt;]],
352         [[struct MEM_EXTENDED_PARAMETER x;]])
353       ],
354       [
355         AC_MSG_RESULT([yes])
356         CAN_BUILD_ZGC_ON_WINDOWS=&quot;yes&quot;
357       ],
358       [
359         AC_MSG_RESULT([no, missing required APIs])
360         CAN_BUILD_ZGC_ON_WINDOWS=&quot;no&quot;
361       ]
362     )
363   fi
364 
365   AC_MSG_CHECKING([if zgc can be built])
366   if (test &quot;x$OPENJDK_TARGET_OS&quot; = &quot;xlinux&quot; &amp;&amp; test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xx86_64&quot;) || \
367      (test &quot;x$OPENJDK_TARGET_OS&quot; = &quot;xlinux&quot; &amp;&amp; test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xaarch64&quot;) || \
368      (test &quot;x$CAN_BUILD_ZGC_ON_WINDOWS&quot; = &quot;xyes&quot;) || \
369      (test &quot;x$OPENJDK_TARGET_OS&quot; = &quot;xmacosx&quot; &amp;&amp; test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xx86_64&quot;); then
370     AC_MSG_RESULT([yes])
371   else
372     DISABLED_JVM_FEATURES=&quot;$DISABLED_JVM_FEATURES zgc&quot;
373     AC_MSG_RESULT([no, platform not supported])
374   fi
375 
376   # Disable unsupported GCs for Zero
377   if HOTSPOT_CHECK_JVM_VARIANT(zero); then
378     DISABLED_JVM_FEATURES=&quot;$DISABLED_JVM_FEATURES epsilongc g1gc zgc shenandoahgc&quot;
379   fi
380 
381   # Turn on additional features based on other parts of configure
382   if test &quot;x$INCLUDE_DTRACE&quot; = &quot;xtrue&quot;; then
383     JVM_FEATURES=&quot;$JVM_FEATURES dtrace&quot;
384   else
385     if HOTSPOT_CHECK_JVM_FEATURE(dtrace); then
386       AC_MSG_ERROR([To enable dtrace, you must use --enable-dtrace])
387     fi
388   fi
389 
390   if test &quot;x$STATIC_BUILD&quot; = &quot;xtrue&quot;; then
391     JVM_FEATURES=&quot;$JVM_FEATURES static-build&quot;
392   else
393     if HOTSPOT_CHECK_JVM_FEATURE(static-build); then
394       AC_MSG_ERROR([To enable static-build, you must use --enable-static-build])
395     fi
396   fi
397 
398   if ! HOTSPOT_CHECK_JVM_VARIANT(zero); then
399     if HOTSPOT_CHECK_JVM_FEATURE(zero); then
400       AC_MSG_ERROR([To enable zero, you must use --with-jvm-variants=zero])
401     fi
402   fi
403 
404   AC_MSG_CHECKING([if jvmci module jdk.internal.vm.ci should be built])
405   # Check if jvmci is diabled
406   if HOTSPOT_IS_JVM_FEATURE_DISABLED(jvmci); then
407     AC_MSG_RESULT([no, forced])
408     JVM_FEATURES_jvmci=&quot;&quot;
409     INCLUDE_JVMCI=&quot;false&quot;
410   else
411     # Only enable jvmci on x86_64 and aarch64
412     if test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xx86_64&quot; || \
413        test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xaarch64&quot; ; then
414       AC_MSG_RESULT([yes])
415       JVM_FEATURES_jvmci=&quot;jvmci&quot;
416       INCLUDE_JVMCI=&quot;true&quot;
417     else
418       AC_MSG_RESULT([no])
419       JVM_FEATURES_jvmci=&quot;&quot;
420       INCLUDE_JVMCI=&quot;false&quot;
421       if HOTSPOT_CHECK_JVM_FEATURE(jvmci); then
422         AC_MSG_ERROR([JVMCI is currently not supported on this platform.])
423       fi
424     fi
425   fi
426 
427   AC_SUBST(INCLUDE_JVMCI)
428 
429   AC_MSG_CHECKING([if graal module jdk.internal.vm.compiler should be built])
430   # Check if graal is diabled
431   if HOTSPOT_IS_JVM_FEATURE_DISABLED(graal); then
432     AC_MSG_RESULT([no, forced])
433     JVM_FEATURES_graal=&quot;&quot;
434     INCLUDE_GRAAL=&quot;false&quot;
435   else
436     if HOTSPOT_CHECK_JVM_FEATURE(graal); then
437       AC_MSG_RESULT([yes, forced])
438       if test &quot;x$JVM_FEATURES_jvmci&quot; != &quot;xjvmci&quot; ; then
439         AC_MSG_ERROR([Specified JVM feature &#39;graal&#39; requires feature &#39;jvmci&#39;])
440       fi
441       JVM_FEATURES_graal=&quot;graal&quot;
442       INCLUDE_GRAAL=&quot;true&quot;
443     else
444       # By default enable graal build on x64 or where AOT is available.
445       # graal build requires jvmci.
446       if test &quot;x$JVM_FEATURES_jvmci&quot; = &quot;xjvmci&quot; &amp;&amp; \
447           (test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xx86_64&quot; || \
448            test &quot;x$ENABLE_AOT&quot; = &quot;xtrue&quot;) ; then
449         AC_MSG_RESULT([yes])
450         JVM_FEATURES_graal=&quot;graal&quot;
451         INCLUDE_GRAAL=&quot;true&quot;
452       else
453         AC_MSG_RESULT([no])
454         JVM_FEATURES_graal=&quot;&quot;
455         INCLUDE_GRAAL=&quot;false&quot;
456       fi
457     fi
458   fi
459 
460   AC_SUBST(INCLUDE_GRAAL)
461 
462   # Disable aot with &#39;--with-jvm-features=-aot&#39;
463   if HOTSPOT_IS_JVM_FEATURE_DISABLED(aot); then
464     ENABLE_AOT=&quot;false&quot;
465   fi
466 
467   AC_MSG_CHECKING([if aot should be enabled])
468   if test &quot;x$ENABLE_AOT&quot; = &quot;xtrue&quot;; then
469     if test &quot;x$JVM_FEATURES_graal&quot; != &quot;xgraal&quot;; then
470       if test &quot;x$enable_aot&quot; = &quot;xyes&quot; || HOTSPOT_CHECK_JVM_FEATURE(aot); then
471         AC_MSG_RESULT([yes, forced])
472         AC_MSG_ERROR([Specified JVM feature &#39;aot&#39; requires feature &#39;graal&#39;])
473       else
474         AC_MSG_RESULT([no])
475       fi
476       JVM_FEATURES_aot=&quot;&quot;
477       ENABLE_AOT=&quot;false&quot;
478     else
479       if test &quot;x$enable_aot&quot; = &quot;xyes&quot; || HOTSPOT_CHECK_JVM_FEATURE(aot); then
480         AC_MSG_RESULT([yes, forced])
481       else
482         AC_MSG_RESULT([yes])
483       fi
484       JVM_FEATURES_aot=&quot;aot&quot;
485     fi
486   else
487     if test &quot;x$enable_aot&quot; = &quot;xno&quot; || HOTSPOT_IS_JVM_FEATURE_DISABLED(aot); then
488       AC_MSG_RESULT([no, forced])
489     else
490       AC_MSG_RESULT([no])
491     fi
492     JVM_FEATURES_aot=&quot;&quot;
493     if HOTSPOT_CHECK_JVM_FEATURE(aot); then
494       AC_MSG_ERROR([To enable aot, you must use --enable-aot])
495     fi
496   fi
497 
498   AC_SUBST(ENABLE_AOT)
499 
500   if test &quot;x$OPENJDK_TARGET_CPU&quot; = xarm ; then
501     # Default to use link time optimizations on minimal on arm
502     JVM_FEATURES_link_time_opt=&quot;link-time-opt&quot;
503   else
504     JVM_FEATURES_link_time_opt=&quot;&quot;
505   fi
506 
507   # All variants but minimal (and custom) get these features
508   NON_MINIMAL_FEATURES=&quot;$NON_MINIMAL_FEATURES g1gc parallelgc serialgc epsilongc shenandoahgc jni-check jvmti management nmt services vm-structs zgc&quot;
509 
510   # Disable CDS on AIX.
511   if test &quot;x$OPENJDK_TARGET_OS&quot; = &quot;xaix&quot;; then
512     ENABLE_CDS=&quot;false&quot;
513     if test &quot;x$enable_cds&quot; = &quot;xyes&quot;; then
514       AC_MSG_ERROR([CDS is currently not supported on AIX. Remove --enable-cds.])
515     fi
516   fi
517 
518   # Disable CDS if user requested it with --with-jvm-features=-cds.
519   if HOTSPOT_IS_JVM_FEATURE_DISABLED(cds); then
520     ENABLE_CDS=&quot;false&quot;
521     if test &quot;x$enable_cds&quot; = &quot;xyes&quot;; then
522       AC_MSG_ERROR([CDS was disabled by --with-jvm-features=-cds. Remove --enable-cds.])
523     fi
524   fi
525 
526   if ! HOTSPOT_CHECK_JVM_VARIANT(server) &amp;&amp; ! HOTSPOT_CHECK_JVM_VARIANT(client); then
527     # ..except when the user explicitely requested it with --enable-jvm-features
528     if ! HOTSPOT_CHECK_JVM_FEATURE(cds); then
529       ENABLE_CDS=&quot;false&quot;
530       if test &quot;x$enable_cds&quot; = &quot;xyes&quot;; then
531         AC_MSG_ERROR([CDS not implemented for variants zero, minimal, core. Remove --enable-cds.])
532       fi
533     fi
534   fi
535 
536   AC_MSG_CHECKING([if cds should be enabled])
537   if test &quot;x$ENABLE_CDS&quot; = &quot;xtrue&quot;; then
538     if test &quot;x$enable_cds&quot; = &quot;xyes&quot;; then
539       AC_MSG_RESULT([yes, forced])
540     else
541       AC_MSG_RESULT([yes])
542     fi
543     NON_MINIMAL_FEATURES=&quot;$NON_MINIMAL_FEATURES cds&quot;
544   else
545     if test &quot;x$enable_cds&quot; = &quot;xno&quot;; then
546       AC_MSG_RESULT([no, forced])
547     else
548       AC_MSG_RESULT([no])
549     fi
550   fi
551 
552   AC_MSG_CHECKING([if tsan should be built])
553   # Check if user explicitly disabled tsan
554   if HOTSPOT_IS_JVM_FEATURE_DISABLED(tsan); then
555     AC_MSG_RESULT([no, forced])
556     INCLUDE_TSAN=&quot;false&quot;
557   # Only enable ThreadSanitizer on supported platforms
<a name="1" id="anc1"></a><span class="line-modified">558   elif test &quot;x$OPENJDK_TARGET_OS&quot; = &quot;xlinux&quot; &amp;&amp; test &quot;x$OPENJDK_TARGET_CPU&quot; = &quot;xx86_64&quot;; then</span>
559     AC_MSG_RESULT([yes])
560     NON_MINIMAL_FEATURES=&quot;$NON_MINIMAL_FEATURES tsan&quot;
561     INCLUDE_TSAN=&quot;true&quot;
562   else
563     AC_MSG_RESULT([no, platform not supported])
564     INCLUDE_TSAN=&quot;false&quot;
565     if HOTSPOT_CHECK_JVM_FEATURE(tsan); then
566       AC_MSG_ERROR([ThreadSanitizer is currently not supported on this platform.])
567     fi
568   fi
569 
570   # Add a configure option --&lt;enable|disable&gt;-tsan-launcher to allow
571   # more control on whether to link TSAN runtime with the launcher.
572   AC_ARG_ENABLE([tsan-launcher], [AS_HELP_STRING([--enable-tsan-launcher],
573       [link tsan runtime with the default JDK launcher. Default is consistent with whether tsan feature is enabled.])])
574   AC_MSG_CHECKING([if tsan should be linked with JDK launcher])
575   if test &quot;x$INCLUDE_TSAN&quot; = &quot;xtrue&quot;; then
576     if test &quot;x$enable_tsan_launcher&quot; = &quot;xyes&quot;; then
577       AC_MSG_RESULT([yes, forced])
578     elif test &quot;x$enable_tsan_launcher&quot; = &quot;xno&quot;; then
579       AC_MSG_RESULT([no, forced])
580       INCLUDE_TSAN=&quot;false&quot;
581     else
582       AC_MSG_RESULT([yes, default])
583     fi
584   else
585     AC_MSG_RESULT([no, tsan feature is disabled])
586     if test &quot;x$enable_tsan_launcher&quot; = &quot;xyes&quot;; then
587       AC_MSG_ERROR([--enable-tsan-launcher can only be used when tsan feature is enabled.])
588     fi
589   fi
590 
591   AC_SUBST(INCLUDE_TSAN)
592 
593   # Enable features depending on variant.
594   JVM_FEATURES_server=&quot;compiler1 compiler2 $NON_MINIMAL_FEATURES $JVM_FEATURES $JVM_FEATURES_jvmci $JVM_FEATURES_aot $JVM_FEATURES_graal&quot;
595   JVM_FEATURES_client=&quot;compiler1 $NON_MINIMAL_FEATURES $JVM_FEATURES&quot;
596   JVM_FEATURES_core=&quot;$NON_MINIMAL_FEATURES $JVM_FEATURES&quot;
597   JVM_FEATURES_minimal=&quot;compiler1 minimal serialgc $JVM_FEATURES $JVM_FEATURES_link_time_opt&quot;
598   JVM_FEATURES_zero=&quot;zero $NON_MINIMAL_FEATURES $JVM_FEATURES&quot;
599   JVM_FEATURES_custom=&quot;$JVM_FEATURES&quot;
600 
601   AC_SUBST(JVM_FEATURES_server)
602   AC_SUBST(JVM_FEATURES_client)
603   AC_SUBST(JVM_FEATURES_core)
604   AC_SUBST(JVM_FEATURES_minimal)
605   AC_SUBST(JVM_FEATURES_zero)
606   AC_SUBST(JVM_FEATURES_custom)
607 
608   # Used for verification of Makefiles by check-jvm-feature
609   AC_SUBST(VALID_JVM_FEATURES)
610 
611   # --with-cpu-port is no longer supported
612   BASIC_DEPRECATED_ARG_WITH(with-cpu-port)
613 ])
614 
615 ###############################################################################
616 # Finalize JVM features once all setup is complete, including custom setup.
617 #
618 AC_DEFUN_ONCE([HOTSPOT_FINALIZE_JVM_FEATURES],
619 [
620   for variant in $JVM_VARIANTS; do
621     AC_MSG_CHECKING([JVM features for JVM variant &#39;$variant&#39;])
622     features_var_name=JVM_FEATURES_$variant
623     JVM_FEATURES_FOR_VARIANT=${!features_var_name}
624 
625     # Filter out user-requested disabled features
626     BASIC_GET_NON_MATCHING_VALUES(JVM_FEATURES_FOR_VARIANT, $JVM_FEATURES_FOR_VARIANT, $DISABLED_JVM_FEATURES)
627 
628     # Keep feature lists sorted and free of duplicates
629     BASIC_SORT_LIST(JVM_FEATURES_FOR_VARIANT, $JVM_FEATURES_FOR_VARIANT)
630 
631     # Update real feature set variable
632     eval $features_var_name=&#39;&quot;&#39;$JVM_FEATURES_FOR_VARIANT&#39;&quot;&#39;
633     AC_MSG_RESULT([&quot;$JVM_FEATURES_FOR_VARIANT&quot;])
634 
635     # Verify that we have at least one gc selected
636     GC_FEATURES=`$ECHO $JVM_FEATURES_FOR_VARIANT | $GREP gc`
637     if test &quot;x$GC_FEATURES&quot; = x; then
638       AC_MSG_WARN([Invalid JVM features: No gc selected for variant $variant.])
639     fi
640 
641     # Validate features (for configure script errors, not user errors)
642     BASIC_GET_NON_MATCHING_VALUES(INVALID_FEATURES, $JVM_FEATURES_FOR_VARIANT, $VALID_JVM_FEATURES)
643     if test &quot;x$INVALID_FEATURES&quot; != x; then
644       AC_MSG_ERROR([Internal configure script error. Invalid JVM feature(s): $INVALID_FEATURES])
645     fi
646   done
647 ])
648 
649 ################################################################################
650 # Check if gtest should be built
651 #
652 AC_DEFUN_ONCE([HOTSPOT_ENABLE_DISABLE_GTEST],
653 [
654   AC_ARG_ENABLE([hotspot-gtest], [AS_HELP_STRING([--disable-hotspot-gtest],
655       [Disables building of the Hotspot unit tests @&lt;:@enabled@:&gt;@])])
656 
657   if test -e &quot;${TOPDIR}/test/hotspot/gtest&quot;; then
658     GTEST_DIR_EXISTS=&quot;true&quot;
659   else
660     GTEST_DIR_EXISTS=&quot;false&quot;
661   fi
662 
663   AC_MSG_CHECKING([if Hotspot gtest unit tests should be built])
664   if test &quot;x$enable_hotspot_gtest&quot; = &quot;xyes&quot;; then
665     if test &quot;x$GTEST_DIR_EXISTS&quot; = &quot;xtrue&quot;; then
666       AC_MSG_RESULT([yes, forced])
667       BUILD_GTEST=&quot;true&quot;
668     else
669       AC_MSG_ERROR([Cannot build gtest without the test source])
670     fi
671   elif test &quot;x$enable_hotspot_gtest&quot; = &quot;xno&quot;; then
672     AC_MSG_RESULT([no, forced])
673     BUILD_GTEST=&quot;false&quot;
674   elif test &quot;x$enable_hotspot_gtest&quot; = &quot;x&quot;; then
675     if test &quot;x$GTEST_DIR_EXISTS&quot; = &quot;xtrue&quot;; then
676       AC_MSG_RESULT([yes])
677       BUILD_GTEST=&quot;true&quot;
678     else
679       AC_MSG_RESULT([no])
680       BUILD_GTEST=&quot;false&quot;
681     fi
682   else
683     AC_MSG_ERROR([--enable-gtest must be either yes or no])
684   fi
685 
686   AC_SUBST(BUILD_GTEST)
687 ])
<a name="2" id="anc2"></a><b style="font-size: large; color: red">--- EOF ---</b>
















































































</pre>
<input id="eof" value="2" type="hidden" />
</body>
</html>