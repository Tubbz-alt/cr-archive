<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8" />
    <title>Old src/hotspot/share/gc/z/zDebug.gdb</title>
    <link rel="stylesheet" href="../../../../../style.css" />
  </head>
  <body>
    <pre>
  1 #
  2 # GDB functions for debugging the Z Garbage Collector
  3 #
  4 
  5 printf &quot;Loading zDebug.gdb\n&quot;
  6 
  7 # Print Klass*
  8 define zpk
  9     printf &quot;Klass: %s\n&quot;, (char*)((Klass*)($arg0))-&gt;_name-&gt;_body
 10 end
 11 
 12 # Print oop
 13 define zpo
 14     set $obj = (oopDesc*)($arg0)
 15 
 16     printf &quot;Oop:   0x%016llx\tState: &quot;, (uintptr_t)$obj
 17     if ((uintptr_t)$obj &amp; (uintptr_t)ZAddressGoodMask)
 18         printf &quot;Good &quot;
 19         if ((uintptr_t)$obj &amp; (uintptr_t)ZAddressMetadataRemapped)
 20             printf &quot;(Remapped)&quot;
 21         else
 22             if ((uintptr_t)$obj &amp; (uintptr_t)ZAddressMetadataMarked)
 23                 printf &quot;(Marked)&quot;
 24             else
 25                 printf &quot;(Unknown)&quot;
 26             end
 27         end
 28     else
 29         printf &quot;Bad &quot;
 30         if ((uintptr_t)ZAddressGoodMask &amp; (uintptr_t)ZAddressMetadataMarked)
 31             # Should be marked
 32             if ((uintptr_t)$obj &amp; (uintptr_t)ZAddressMetadataRemapped)
 33                 printf &quot;(Not Marked, Remapped)&quot;
 34             else
 35                 printf &quot;(Not Marked, Not Remapped)&quot;
 36             end
 37         else
 38             if ((uintptr_t)ZAddressGoodMask &amp; (uintptr_t)ZAddressMetadataRemapped)
 39                 # Should be remapped
 40                 if ((uintptr_t)$obj &amp; (uintptr_t)ZAddressMetadataMarked)
 41                     printf &quot;(Marked, Not Remapped)&quot;
 42                 else
 43                     printf &quot;(Not Marked, Not Remapped)&quot;
 44                 end
 45             else
 46                 # Unknown
 47                 printf &quot;(Unknown)&quot;
 48             end
 49         end
 50     end
 51     printf &quot;\t Page: %llu\n&quot;, ((uintptr_t)$obj &amp; ZAddressOffsetMask) &gt;&gt; ZGranuleSizeShift
 52     x/16gx $obj
 53     printf &quot;Mark:  0x%016llx\tKlass: %s\n&quot;, (uintptr_t)$obj-&gt;_mark, (char*)$obj-&gt;_metadata-&gt;_klass-&gt;_name-&gt;_body
 54 end
 55 
 56 # Print heap page by pagetable index
 57 define zpp
 58     set $page = (ZPage*)((uintptr_t)ZHeap::_heap._pagetable._map._map[($arg0)] &amp; ~1)
 59     printf &quot;Page %p\n&quot;, $page
 60     print *$page
 61 end
 62 
 63 # Print pagetable
 64 define zpt
 65     printf &quot;Pagetable (first 128 slots)\n&quot;
 66     x/128gx ZHeap::_heap._pagetable._map._map
 67 end
 68 
 69 # Print live map
 70 define __zmarked
 71     set $livemap   = $arg0
 72     set $bit        = $arg1
 73     set $size       = $livemap._bitmap._size
 74     set $segment    = $size / ZLiveMap::nsegments
 75     set $segment_bit = 1 &lt;&lt; $segment
 76 
 77     printf &quot;Segment is &quot;
 78     if !($livemap._segment_live_bits &amp; $segment_bit)
 79         printf &quot;NOT &quot;
 80     end
 81     printf &quot;live (segment %d)\n&quot;, $segment
 82 
 83     if $bit &gt;= $size
 84         print &quot;Error: Bit %z out of bounds (bitmap size %z)\n&quot;, $bit, $size
 85     else
 86         set $word_index = $bit / 64
 87         set $bit_index  = $bit % 64
 88         set $word       = $livemap._bitmap._map[$word_index]
 89         set $live_bit   = $word &amp; (1 &lt;&lt; $bit_index)
 90 
 91         printf &quot;Object is &quot;
 92         if $live_bit == 0
 93             printf &quot;NOT &quot;
 94         end
 95         printf &quot;live (word index %d, bit index %d)\n&quot;, $word_index, $bit_index
 96     end
 97 end
 98 
 99 define zmarked
100     set $addr          = $arg0
101     set $obj           = ((uintptr_t)$addr &amp; ZAddressOffsetMask)
102     set $page_index    = $obj &gt;&gt; ZGranuleSizeShift
103     set $page_entry    = (uintptr_t)ZHeap::_heap._pagetable._map._map[$page_index]
104     set $page          = (ZPage*)($page_entry &amp; ~1)
105     set $page_start    = (uintptr_t)$page._virtual._start
106     set $page_end      = (uintptr_t)$page._virtual._end
107     set $page_seqnum   = $page._livemap._seqnum
108     set $global_seqnum = ZGlobalSeqNum
109 
110     if $obj &lt; $page_start || $obj &gt;= $page_end
111         printf &quot;Error: %p not in page %p (start %p, end %p)\n&quot;, $obj, $page, $page_start, $page_end
112     else
113         printf &quot;Page is &quot;
114         if $page_seqnum != $global_seqnum
115             printf &quot;NOT &quot;
116         end
117         printf &quot;live (page %p, page seqnum %d, global seqnum %d)\n&quot;, $page, $page_seqnum, $global_seqnum
118 
119         #if $page_seqnum == $global_seqnum
120             set $offset = $obj - $page_start
121             set $bit = $offset / 8
122             __zmarked $page._livemap $bit
123         #end
124     end
125 end
126 
127 # Print heap information
128 define zph
129     printf &quot;Address Space\n&quot;
130     printf &quot;     Start:             0x%llx\n&quot;, ZAddressSpaceStart
131     printf &quot;     End:               0x%llx\n&quot;, ZAddressSpaceEnd
132     printf &quot;     Size:              %-15llu (0x%llx)\n&quot;, ZAddressSpaceSize, ZAddressSpaceSize
133     printf &quot;Heap\n&quot;
134     printf &quot;     GlobalPhase:       %u\n&quot;, ZGlobalPhase
135     printf &quot;     GlobalSeqNum:      %u\n&quot;, ZGlobalSeqNum
136     printf &quot;     Offset Max:        %-15llu (0x%llx)\n&quot;, ZAddressOffsetMax, ZAddressOffsetMax
137     printf &quot;     Page Size Small:   %-15llu (0x%llx)\n&quot;, ZPageSizeSmall, ZPageSizeSmall
138     printf &quot;     Page Size Medium:  %-15llu (0x%llx)\n&quot;, ZPageSizeMedium, ZPageSizeMedium
139     printf &quot;Metadata Bits\n&quot;
140     printf &quot;     Good:              0x%016llx\n&quot;, ZAddressGoodMask
141     printf &quot;     Bad:               0x%016llx\n&quot;, ZAddressBadMask
142     printf &quot;     WeakBad:           0x%016llx\n&quot;, ZAddressWeakBadMask
143     printf &quot;     Marked:            0x%016llx\n&quot;, ZAddressMetadataMarked
144     printf &quot;     Remapped:          0x%016llx\n&quot;, ZAddressMetadataRemapped
145 end
146 
147 # End of file
    </pre>
  </body>
</html>